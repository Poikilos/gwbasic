100 COLOR 1,7,4:CLS:SOUND 1000,3:SOUND 2000,3:KEY OFF
105 PRINT:PRINT
110 PRINT TAB(18)"*****  MATH AND ELECTRONIC PROGRAMS  *****"
115 PRINT TAB(35)"MAIN MENU"
120 PRINT
121 PRINT TAB(20)"(0)-RETURN TO SYSTEM"
122 PRINT TAB(20)"(1)-MATH3"
125 PRINT TAB(20)"(2)-MATH4"
130 PRINT TAB(20)"(3)-MATH5"
140 PRINT TAB(20)"(4)-DESIGN"
150 PRINT TAB(20)"(5)-NETWORK ANALYSIS"
160 PRINT TAB(20)"(6)-DIGITAL PROGRAMS"
170 PRINT TAB(20)"(7)-TRIGONOMETRY PROGRAMS"
172 PRINT TAB(20)"(8)-COMBINATIONS, PERMUTATIONS, ALGEBRAIC FACTORING,"
173 PRINT TAB(24)"INTERSECTION OF NON-LINEAR EQUATIONS"
175 PRINT
180 PRINT"INPUT NUMBER OF YOUR CHOICE"
190 A$=INKEY$
200 IF A$="" GOTO 190
205 IF A$="0" THEN SYSTEM
210 IF A$="1" GOTO 25000
220 IF A$="2" GOTO 20000
230 IF A$="3" GOTO 4000
240 IF A$="4" GOTO 10000
250 IF A$="5" GOTO 8000
260 IF A$="6" GOTO 1000
270 IF A$="7" GOTO 13000
280 IF A$="8" GOTO 41895
400 PRINT"INVALID INPUT":GOTO 190
1000 CLS:SOUND 2000,3:SOUND 1000,3
1010 PRINT TAB(20)"****** DIGITAL PROGRAMS ******"
1020 PRINT
1030 PRINT TAB(20)"(1)-MINTERM LOGIC ANALYSIS (SUM-OF-PRODUCTS EXPRESSION)"
1040 PRINT TAB(20)"(2)-LOGIC4 TRUTH TABLES"
1050 PRINT TAB(20)"(3)-BINARY TO DECIMAL CONVERSION"
1055 PRINT TAB(20)"(4)-FLOATING POINT FRACTION CONVERSION TO BINARY"
1057 PRINT TAB(20)"(5)-CONVERT INTEGERS TO ALL BASES"
1058 PRINT TAB(20)"(6)-GOTO MENU"
1059 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
1060 A$=INKEY$
1062 IF A$="" GOTO 1060
1064 IF A$="1" GOTO 1090
1066 IF A$="2" GOTO 2500
1068 IF A$="3" GOTO 2790
1070 IF A$="4" GOTO 3100
1072 IF A$="5" GOTO 30000
1073 IF A$="6" GOTO 100
1075 PRINT"INVALID INPUT" : GOTO 1060
1090 CLEAR:DIM MV(16),BI(16,4),BR(16,4),L$(4),BT(16,16)
1100 CLS:BEEP
1110 PRINT
1120 PRINT"    INPUT MINTERM VALUES AND REDUCE"
1130 PRINT"    USING QUINE-McCLUSKEY AND PRINT"
1140 PRINT"       RESULTING LOGIC EQUATION"
1150 PRINT:PRINT
1160 GOSUB 1290
1170 GOSUB 1400
1180 PRINT:PRINT"F(A,B,C,D) = ";
1190 GOSUB 1560
1200 PRINT:PRINT
1210 PRINT"AFTER USING QUINE-McCLUSKEY REDUCTION:"
1220 PRINT:PRINT"F(A,B,C,D) = ";
1230 GOSUB 1700
1240 GOSUB 2400
1250 PRINT:PRINT
1260 INPUT"(1)-REPEAT (2)-GOTO MENU";N
1270 ON N GOTO 1090,1000
1280 PRINT"INVALID INPUT":GOTO 1260
1290 REM ACCEPT INPUT OF MINTERM DATA
1300 PRINT:PRINT
1310 INPUT"HOW MANY MINTERMS";NM
1320 PRINT:PRINT"ENTER MINTERM VALUES (0 TO 15):":PRINT
1330 FOR K=1 TO NM
1340 PRINT"MINTERM(";K;")=";
1350 INPUT MV(K)
1360 IF MV(K)<0 OR MV(K)>15 THEN PRINT:PRINT"REDO INPUT - ":GOTO 1340
1370 NEXT K
1380 PRINT:PRINT
1390 RETURN
1400 REM EXPAND MINTERMS INTO BINARY DATA
1410 FOR K=1 TO 16
1420 FOR I=1 TO NM
1430 IF K=MV(I)+1 THEN 1500
1440 NEXT I
1450 FOR J=1 TO 4
1460 BI(K,J)=-1
1470 NEXT J
1480 NEXT K
1490 RETURN
1500 DV=MV(I)
1510 IF DV>=8 THEN BI(K,1)=1:DV=DV-8 ELSE BI(K,1)=0
1520 IF DV>=4 THEN BI(K,2)=1:DV=DV-4 ELSE BI(K,2)=0
1530 IF DV>=2 THEN BI(K,3)=1:DV=DV-2 ELSE BI(K,3)=0
1540 BI(K,4)=DV
1550 GOTO 1480
1560 REM PRINT DATA IN LOGIC EQUATION
1570 FOR J=1 TO 4
1580 READ L$(J)
1590 NEXT J
1600 DATA A,B,C,D
1610 FOR I=1 TO 16
1620 FOR J=1 TO 4
1630 IF BI(I,J)=-1 THEN 1680
1640 IF BI(I,J)=1 THEN PRINT L$(J);
1650 IF BI(I,J)=0 THEN PRINT L$(J);"'";
1660 NEXT J
1670 NP=NP+1:IF NP<NM THEN PRINT " + ";
1680 NEXT I
1690 RETURN
1700 REM QUINE-McCLUSKEY REDUCTION
1710 NI=16:NR=0
1720 GOSUB 1970
1730 FOR T=1 TO 4
1740 FOR I=1 TO NR
1750 IF I>NR THEN 1840
1760 TR=0
1770 FOR L=1 TO NR
1780 IF L>NR THEN 1820
1790 IF I=L THEN 1810
1800 GOSUB 2070
1810 NEXT L
1820 IF TR=0 THEN GOSUB 2310:GOSUB 1870
1830 NEXT I
1840 GOSUB 2350
1850 NEXT T
1860 RETURN
1870 REM
1880 IF NT=1 THEN RETURN
1890 FOR L1=1 TO NT-1
1900 FOR J=1 TO 4
1910 IF BT(NT,J)<>BT(L1,J) THEN 1950
1920 NEXT J
1930 NT=NT-1
1940 RETURN
1950 NEXT L1
1960 RETURN
1970 REM
1980 FOR I=1 TO NI
1990 FOR J=1 TO 4
2000 IF BI(I,J)<>-1 THEN 2040
2010 NEXT J
2020 NEXT I
2030 RETURN
2040 NR=NR+1
2050 FOR J=1 TO 4:BR(NR,J)=BI(I,J):NEXT J
2060 GOTO 2020
2070 REM
2080 S=0
2090 FOR J=1 TO 4
2100 IF BR(I,J)=BR(L,J) THEN S=S+1
2110 NEXT J
2120 IF S=3 THEN GOSUB 2150:GOSUB 1870:GOTO 2140
2130 IF S=4 THEN GOSUB 2220
2140 RETURN
2150 REM
2160 NT=NT+1:TR=1
2170 FOR J=1 TO 4
2180 IF BR(I,J)=BR(L,J) THEN BT(NT,J)=BR(I,J):GOTO 2200
2190 BT(NT,J)=-1
2200 NEXT J
2210 RETURN
2220 REM
2230 IF L=NR THEN 2290
2240 FOR M=L TO NR-1
2250 FOR J=1 TO 4
2260 BR(M,J)=BR(M+1,J)
2270 NEXT J
2280 NEXT M
2290 NR=NR-1:L=L-1
2300 RETURN
2310 REM
2320 NT=NT+1
2330 FOR J=1 TO 4:BT(NT,J)=BR(I,J):NEXT J
2340 RETURN
2350 REM
2360 FOR I=1 TO NT
2370 FOR J=1 TO 4:BR(I,J)=BT(I,J):NEXT J
2380 NEXT I
2390 NR=NT:NT=0:RETURN
2400 REM
2410 FOR I=1 TO NR
2420 FOR J=1 TO 4
2430 IF BR(I,J)=-1 THEN 2460
2440 IF BR(I,J)=1 THEN PRINT L$(J);
2450 IF BR(I,J)=0 THEN PRINT L$(J);"'";
2460 NEXT J
2470 IF I<=NR-1 THEN PRINT" + ";
2480 NEXT I
2490 RETURN
2500 CLS:BEEP:PRINT"      **** LOGIC TRUTH TABLES ***"
2510 PRINT
2520 PRINT"PUT EQUATION ON LINES 2620-2690"
2530 PRINT"GET PRINTER READY AND PRESS ANY KEY"
2540 N$=INKEY$
2550 IF N$="" GOTO 2540 ELSE 2560
2560 PRINT" A";"  B";"  C";"  D";" -->";" Y"
2570 PRINT
2580 FOR A=0 TO 1
2590 FOR B=0 TO 1
2600 FOR C=0 TO 1
2610 FOR D=0 TO 1
2620 Y1=SGN(A*(1-B)*C)
2630 Y2=SGN(A*(1-B)*(1-C))
2640 Y3=SGN(A*B*C)
2650 Y4=SGN(A*B*(1-C))
2660 Y=SGN(Y1+Y2+Y3+Y4)
2700 PRINT A;B;C;D;"-->";Y
2710 NEXT D
2720 NEXT C
2730 NEXT B
2740 NEXT A
2750 LIST 2620-2690:GOTO 2760
2760 INPUT"(1)-REPEAT (2)-GOTO MENU";N
2770 ON N GOTO 2500,1000
2780 PRINT"INVALID INPUT":GOTO 2760
2790 REM------------------------------
2800 REM         PROGRAM 16-5
2810 REM------------------------------
2820 REM  BINARY TO DECIMAL CONVERSION
2830 REM------------------------------
2840 REM
2850 CLEAR
2860 DIM B(40)
2870 CLS:BEEP:PRINT
2880 PRINT"       CONVERTS BINARY TO DECIMAL"
2890 PRINT"            UP TO 40 BITS"
2900 PRINT
2910 INPUT"  ENTER BINARY VALUE (ENTER 0 TO END)   ";B$
2920 IF B$="0" THEN GOTO 1000
2930 IF LEN(B$)>40 THEN PRINT" MAXIMUM OF 40 BITS":GOTO 2910
2940 GOSUB 2990
2950 IF ER=1 THEN 2910
2960 PRINT"THE DECIMAL VALUE IS";DE
2970 PRINT:PRINT
2980 GOTO 2900
2990 REM
3000 DE=0:ER=0
3010 FOR I=0 TO LEN(B$)-1
3020 B(I)=VAL(MID$(B$,LEN(B$)-I,1))
3030 IF B(I)>=2 THEN PRINT "INPUT ERROR - REDO":GOTO 3060
3040 DE=DE+B(I)*2^I
3050 NEXT I
3060 RETURN
3100 CLS:CLEAR:BEEP
3115 REM-----------------------------------------------------------------------
3120 REM    BINARY EQUIVALENT OF FRACTIONAL PART OF DECIMAL NUMBER
3135 REM-----------------------------------------------------------------------
3150 DIM R(50)
3160 INPUT" INPUT FRACTIONAL PART TO CONVERT. (USE DECIMAL POINT)";X
3165 PRINT
3170 INPUT" INPUT NUMBER OF DIGITS IN FRACTION";Y
3175 PRINT
3180 PRINT" BINARY EQUIVALENT OF FRACTION IS:";
3200 FOR I=1 TO Y
3210 R(I)=X*2
3220 X=R(I)
3230 IF INT(R(I))=1 THEN PRINT "1";
3240 IF INT(R(I))<>1 THEN PRINT "0";
3250 IF INT(R(I))=1 THEN X=X-1
3260 NEXT I
3270 PRINT:PRINT
3280 INPUT" INPUT (1)- TO REPEAT (2)-GOTO MENU";B
3290 ON B GOTO 3100,1000
3300 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 3280
4000 CLS:SOUND 2000,3:SOUND 1000,3
4010 PRINT TAB(30)"*** MATH5 MENU ***":PRINT
4020 PRINT TAB(20)"(1)-Y-AXIS TRANSLATION"
4030 PRINT TAB(20)"(2)-HORNER'S METHOD"
4040 PRINT TAB(20)"(3)-LEAST SQUARES CURVE FITTING"
4070 PRINT TAB(20)"(4)-POLYNOMIAL MULTIPLICATION"
4075 PRINT TAB(20)"(5)-POLYNOMIAL & 1st & 2nd DERIVATIVE NUMERICAL EVALUATION"
4076 PRINT TAB(20)"(6)-GOTO MEUN"
4077 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
4079 A$=INKEY$
4081 IF A$="" GOTO 4079
4083 IF A$="1" GOTO 4100
4085 IF A$="2" GOTO 4420
4087 IF A$="3" GOTO 5170
4093 IF A$="4" GOTO 6660
4095 IF A$="5" GOTO 50000
4096 IF A$="6" GOTO 100
4097 PRINT"INVALID INPUT" : GOTO 4079
4100 CLS
4110 REM BY DAVID BEARD AND ED MCCARTHY
4120 REM MAY 28,1988
4130 CLS:PRINT TAB(27)"**** Y-AXIS TRANSLATION ****"
4140 PRINT
4150 CLEAR 100
4160 DIM A(20),P(20),X(20)
4170 INPUT"INPUT THE DEGREE OF THE POLYNOMIAL";N
4180 Z=N
4190 PRINT"INPUT THE COEFFICIENTS (highest exponent to constant)"
4200 FOR I=N+1 TO 1 STEP -1
4210 INPUT A(I)
4220 NEXT I
4230 INPUT"ENTER AMOUNT TO SHIFT Y-AXIS";C
4240 LET P(N+1)=A(N+1)
4250 FOR M=Z+1 TO 1 STEP -1
4260 LET P(Z+1)=A(Z+1)
4270 LET P(M)=P(M+1)*C+A(M)
4280 LET A(M)=P(M)
4290 NEXT M
4300 L=L+1
4310 LET X(N+1)=P(L)
4320 N=N-1
4330 IF N>-1 GOTO 4250
4340 PRINT
4350 FOR A=1 TO Z STEP 1
4360 PRINT X(A)
4370 NEXT A
4380 PRINT "CONSTANT TERM =";X(Z+1)
4390 INPUT"(1)-REPEAT (2)-GOTO MENU";Q
4400 ON Q GOTO 4100,4000
4410 GOTO 4390
4420 CLS:BEEP
4430 F=5
4440 PRINT"       - INTERPOLATION PROGRAM -"
4450 PRINT"PUT EQUATION Y=F(X)=0 ON LINE 4840"
4460 INPUT"PRESS ENTER IF EQUATION ENTERED";D
4470 INPUT"INPUT HIGH AND LOW VALUES (ENTER VAL,VAL)";H,L
4480 LET X=H
4490 GOSUB 4810
4500 LET S1=S9
4510 LET X=L
4520 GOSUB 4810
4530 LET S2=S9
4540 IF S1<>S2 THEN 4580
4550 PRINT"HIGH GIVES SIGN OF ";S1;" LOW GIVES ";S2
4560 PRINT"CHOOSE NEW VALUES!"
4570 GOTO 4470
4580 INPUT"INPUT DESIRED ACCURACY";P
4590 PRINT"-- COMPUTING --"
4600 GOSUB 4650
4610 PRINT"THE ANSWER IS: ";X9
4620 INPUT"(1)-REPEAT (2)-GOTO MENU";E
4630 ON E GOTO 4420,4000
4640 PRINT"INVALID INPUT":GOTO 4620
4650 REM
4660 LET X9=(H+L)/2
4670 LET P9=100*ABS((H-L)/X9)
4680 LET X=H
4690 GOSUB 4810
4700 LET A9=S9
4710 LET X=X9
4720 GOSUB 4810
4730 LET B9=S9
4740 IF A9=B9 THEN 4760
4750 GOTO 4780
4760 LET H=X9
4770 GOTO 4790
4780 LET L=X9
4790 IF P9>P THEN 4660
4800 RETURN
4810 F=F-1
4820 IF F=0 GOTO 4870
4830 REM
4840 Y=X^2-4
4850 LET S9=SGN(Y)
4860 RETURN
4870 PRINT"CONVERGING NUMBER IS";X9
4880 F=5:GOTO 4830
5170 CLEAR 200:BEEP
5180 MX=50
5190 EF=999
5200 MD=5
5210 DIM X(MX),Y(MX)
5220 Q=MD+1:DIM A(Q,Q),R(Q),V(Q)
5230 Q=MD*2:DIM P(Q)
5240 CLS:PRINT"  ---- LEAST SQUARES CURVE FITTING ----"
5250 PRINT"ENTER ALL DATA PAIRS IN (X,Y) FORM"
5260 PRINT"ENTER (999,999) AFTER LAST PAIR"
5270 PRINT"PROGRAM IS SET FOR MAX OF 50 DATA PAIRS"
5280 PRINT:J=0
5290 J=J+1:INPUT"X,Y=";X(J),Y(J)
5300 IF X(J)<>EF THEN 5330
5310 IF Y(J)<>EF THEN 5330
5320 J=J-1:GOTO 5360
5330 IF J<MX THEN 5290
5340 PRINT" ** NO MORE DATA ALLOWED **":BEEP
5350 GOSUB 6230
5360 NP=J:PRINT
5370 IF NP>0 THEN 5400
5380 PRINT"** - FATAL ERROR - ** NO DATA ENTERED **"
5390 GOSUB 6000:STOP
5400 PRINT NP;"DATA PAIRS ENTERED"
5410 INPUT"DEGREE OF POLYNOMIAL TO BE FITTED";D
5420 IF D>=0 THEN 5440
5430 GOSUB 6000:GOTO 5410
5440 D=INT(D):IF D<NP THEN 5460
5450 GOSUB 6000:GOTO 5410
5460 D2=2*D:IF D<=MD THEN 5480
5470 GOSUB 6000:GOTO 5410
5480 N=D+1
5490 FOR J=1 TO D2:P(J)=0
5500 FOR K=1 TO NP:Q=1
5510 FOR L=1 TO J:Q=Q*X(K):NEXT
5520 P(J)=P(J)+Q:NEXT:NEXT
5530 P(0)=NP:R(1)=0:FOR J=1 TO NP
5540 R(1)=R(1)+Y(J):NEXT
5550 IF N=1 THEN 4540
5560 FOR J=2 TO N:R(J)=0
5570 FOR K=1 TO NP:Q=1
5580 FOR L=1 TO J-1
5590 Q=Q*X(K):NEXT
5600 R(J)=R(J)+Y(K)*Q:NEXT:NEXT
5610 FOR J=1 TO N:FOR K=1 TO N
5620 A(J,K)=P(J+K-2):NEXT:NEXT
5630 GOSUB 5990
5640 PRINT
5650 PRINT"  COEFFICIENT","   X-POWER"
5655 PRINT
5660 FOR J=1 TO N
5670 PRINT "      ";V(J),"    X^";J-1:NEXT
5680 Q=0:T=0:FOR J=1 TO NP
5690 Q=Q+Y(J):NEXT:M=Q/NP:G=0
5700 FOR J=1 TO NP:Q=0
5710 FOR K=1 TO N:Z=1
5720 K1=K-1:IF K1=0 THEN 5740
5730 FOR L=1 TO K1:Z=Z*X(J):NEXT
5740 Q=Q+V(K)*Z:NEXT
5750 T=T+(Y(J)-Q)*(Y(J)-Q)
5760 G=G+(Y(J)-M)*(Y(J)-M):NEXT
5770 IF G=0 THEN T=1:GOTO 5790
5780 T=1-T/G
5790 PRINT
5800 PRINT"GOODNESS OF FIT=";T
5810 PRINT"HIT ANY KEY TO CONTINUE"
5820 Q$=INKEY$
5830 IF Q$="" THEN 5820
5840 PRINT
5850 PRINT"CONTINUATION OPTIONS"
5860 INPUT"(1)-DETERMINE SPECIFIC POINTS (2)-FIT ANOTHER DEGREE (3)-START OVER (4)-MENU";Q
5870 Q=INT(Q):IF Q=3 THEN GOTO 5170
5880 IF Q=2 THEN 5410
5890 IF Q=4 THEN 4000
5900 IF Q<>1 THEN 5840
5910 INPUT"INPUT X";XV
5920 IF XV=EF THEN 5840
5930 YV=V(1):IF N=1 THEN 5970
5940 FOR K=2 TO N
5950 Q=1:FOR L=1 TO K-1:Q=Q*XV
5960 NEXT:YV=YV+V(K)*Q:NEXT
5970 PRINT"      Y=";YV:GOTO 5910
5980 FOR K=2 TO N
5990 IF N>1 THEN 6010
6000 V(1)=R(1)/A(1,1):RETURN
6010 FOR K=1 TO N-1:I=K+1
6020 L=K
6030 Q=ABS(A(I,K))-ABS(A(L,K))
6040 IF Q>O THEN L=I
6050 IF I<N THEN I=I+1:GOTO 6030
6060 IF L=K THEN 6100
6070 FOR J=K TO N:Q=A(K,J)
6080 A(K,J)=A(L,J):A(L,J)=Q:NEXT
6090 Q=R(K):R(K)=R(L):R(L)=Q
6100 I=K+1
6110 Q=A(I,K)/A(K,K):A(I,K)=0
6120 FOR J=K+1 TO N
6130 A(I,J)=A(I,J)-Q*A(K,J):NEXT
6140 R(I)=R(I)-Q*R(K)
6150 IF I<N THEN I=I+1:GOTO 6110
6160 NEXT
6170 V(N)=R(N)/A(N,N)
6180 FOR I=N-1 TO 1 STEP -1
6190 Q=0:FOR J=I+1 TO N
6200 Q=Q+A(I,J)*V(J)
6210 V(I)=(R(I)-Q)/A(I,I):NEXT
6220 NEXT:RETURN
6230 BEEP:BEEP:BEEP:RETURN
6240 Y=(X*(LOG(X)/LOG(10)))-.7:RETURN
6250 Y=(X*(LOG(X)/LOG(10)))-1.19:RETURN
6260 Y=(2.71828^X)-SQR(X)-1.5:RETURN
6270 Y=(SIN(X))-(.5*X^2):RETURN
6280 Y=(3*X)-(LOG(X)/LOG(10))-6:RETURN
6290 Y=(LOG(X)/LOG(10))+(X^2)-2:RETURN
6300 Y=(SQR(4-X^2))-(2.71828^X):RETURN
6310 Y=((LOG(SIN(X)+COS(X))/LOG(10)))-X+.5:RETURN
6660 CLS:BEEP
6670 PRINT"  ***  POLYNOMIAL MULTIPLICATION  ***"
6680 PRINT:PRINT
6690 CLEAR
6700 DIM A(21),B(21),G(41)
6710 PRINT"POLY#1 DEGREE=";:INPUT N1
6720 PRINT"INPUT REAL COEFFICIENTS:"
6730 FOR J=1 TO N1+1
6740 PRINT"   EXPONENT=";J-1;"COEFF=";:INPUT A(J)
6750 NEXT J
6760 PRINT"POLY#2 DEGREE=";:INPUT N2
6770 PRINT"INPUT REAL COEFFICIENTS;"
6780 FOR J=1 TO N2+1
6790 PRINT"   EXPONENT="J-1;"COEFF=";:INPUT B(J)
6800 NEXT J
6810 M=N1+N2+1
6820 FOR I=1 TO M
6830 G(I)=0
6840 NEXT I
6850 FOR J=1 TO N2+1
6860 FOR I=1 TO N1+1
6870 JT=I+J-1
6880 G(JT)=G(JT)+A(I)*B(J)
6890 NEXT I
6900 NEXT J
6910 PRINT"PRODUCT POLYNOMIAL IS:"
6920 FOR J=1 TO M
6930 PRINT"  EXPONENT=";J-1;"COEFF=";G(J)
6940 NEXT J
6950 IF M>20 THEN GOTO 7010
6960 FOR J=1 TO M
6970 A(J)=G(J)
6980 NEXT J
6990 N1=M-1
7000 PRINT"ENTER (1)-NEW VALUES (2)-GOTO MENU"
7010 INPUT Z
7020 IF Z=1 GOTO 6660
7030 IF Z=2 GOTO 4000
7040 PRINT"INVALID INPUT
7050 GOTO 7000
7100 REM SIMULTANEOUS SOLUTION OF COMPLEX EQUATIONS
7102 REM BASIC PROGRAMS FOR SCIENTISTS AND ENGINEERS PAGE 101
7104 CLEAR
7106 CLS:BEEP
7108 PI=3.14159
7110 A$=" ##.###^^^^ "
7112 B$=" = ##.###^^^^"
7114 C$=" ##.#####   ##.#####    ##.#####    ###.#####"
7116 M1%=8
7118 DIM Z(8),A(8,8),C1(8),W(8,1),B(8,8),I2%(8,3)
7120 DIM D4(4,4),D5(4,4),V(4,2)
7122 P8=180/PI
7124 REM
7126 PRINT TAB(15)"SIMULTANEOUS SOLUTION WITH COMPLEX COEFFICIENTS"
7128 PRINT TAB(25)"BY GAUSS-JORDAN ELIMINATION"
7130 GOSUB 7184
7132 GOSUB 7258
7134 REM
7136 IF(N1%>5) THEN 7152
7138 PRINT:PRINT"       MATRIX CONSTANTS":PRINT
7140 FOR I%=1 TO N1%
7142 FOR J%=1 TO N2%
7144 PRINT USING A$;A(I%,J%);
7146 NEXT J%
7148 PRINT USING B$;Z(I%)
7150 NEXT I%
7152 PRINT
7154 IF(E1%=1) THEN 7182
7156 PRINT"   REAL    IMAGINARY    MAGNITUDE    ANGLE"
7158 PRINT
7160 FOR I%=1 TO N2%/2
7162 J%=2*I%-1
7164 R2=C1(J%)
7166 I6=C1(J%+1)
7168 M3=SQR(R2^2+I6^2)
7170 IF(R2>0) THEN A1=ATN(I6/R2)*P8
7172 IF(R2=0) THEN A1=SGN(I6)*90
7174 IF(R2<0) THEN A1=ATN(I6/R2)*P8+180
7176 PRINT USING C$;R2,I6,M3,A1
7178 NEXT I%
7180 PRINT
7182 GOTO 7130
7184 REM
7186 PRINT
7188 INPUT" HOW MANY EQUATIONS";N1%
7189 IF N1%=0 GOTO 20000
7190 IF(N1%>M1%/2) THEN 7188
7192 IF(N1%<2) THEN 7458
7194 N2%=N1%
7196 FOR I%=1 TO N1%
7198 PRINT"EQUATION";I%
7200 K%=0
7202 L%=2*I%-1
7204 FOR J%=1 TO N1%
7206 K%=K%+1
7208 PRINT"REAL ";J%;" ";
7210 INPUT D4(I%,J%)
7212 A(L%,K%)=D4(I%,J%)
7214 A(L%+1,K%+1)=D4(I%,J%)
7216 K%=K%+1
7218 PRINT"Imag ";J%;" ";
7220 INPUT D5(I%,J%)
7222 A(L%,K%)=-D5(I%,J%)
7224 A(L%+1,K%-1)=D5(I%,J%)
7226 NEXT J%
7228 INPUT"REAL CONSTANT ";V(I%,1)
7230 Z(L%)=V(I%,1)
7232 INPUT"Imag CONSTANT ";V(I%,2)
7234 Z(L%+1)=V(I%,2)
7236 NEXT I%
7238 PRINT:PRINT
7240 FOR I%=1 TO N1%
7242 FOR J%=1 TO N2%
7244 PRINT D4(I%,J%);D5(I%,J%);
7246 NEXT J%
7248 PRINT V(I%,1);V(I%,2)
7250 NEXT I%
7252 N1%=2*N1%
7254 N2%=N1%
7256 RETURN
7258 REM
7260 E1%=0
7262 I5%=1
7264 N3%=1
7266 FOR I%=1 TO N2%
7268 FOR J%=1 TO N2%
7270 B(I%,J%)=A(I%,J%)
7272 NEXT J%
7274 W(I%,1)=Z(I%)
7276 I2%(I%,3)=0
7278 NEXT I%
7280 D3=1
7282 FOR I%=1 TO N2%
7284 B1=0
7286 FOR J%=1 TO N2%
7288 IF(I2%(J%,3)=1) THEN 7306
7290 FOR K%=1 TO N2%
7292 IF(I2%(K%,3)>1) THEN 7450
7294 IF(I2%(K%,3)=1) THEN 7304
7296 IF(B1>=ABS(B(J%,K%))) THEN 7304
7298 I3%=J%
7300 I4%=K%
7302 B1=ABS(B(J%,K%))
7304 NEXT K%
7306 NEXT J%
7308 I2%(I4%,3)=I2%(I4%,3)+1
7310 I2%(I%,1)=I3%
7312 I2%(I%,2)=I4%
7314 REM
7316 IF(I3%=I4%) THEN 7344
7318 D3=-D3
7320 FOR L%=1 TO N2%
7322 H1=B(I3%,L%)
7324 B(I3%,L%)=B(I4%,L%)
7326 B(I4%,L%)=H1
7328 NEXT L%
7330 IF(N3%<1) THEN 7344
7332 FOR L%=1 TO N3%
7334 H1=W(I3%,L%)
7336 W(I3%,L%)=W(I4%,L%)
7338 W(I4%,L%)=H1
7340 NEXT L%
7342 REM
7344 P1=B(I4%,I4%)
7346 D3=D3*P1
7348 B(I4%,I4%)=1
7350 FOR L%=1 TO N2%
7352 B(I4%,L%)=B(I4%,L%)/P1
7354 NEXT L%
7356 IF(N3%<1) THEN 7364
7358 FOR L%=1 TO N3%
7360 W(I4%,L%)=W(I4%,L%)/P1
7362 NEXT L%
7364 FOR L1%=1 TO N2%
7366 IF(L1%=I4%) THEN 7386
7368 T=B(L1%,I4%)
7370 B(L1%,I4%)=0
7372 FOR L%=1 TO N2%
7374 B(L1%,L%)=B(L1%,L%)-B(I4%,L%)*T
7376 NEXT L%
7378 IF(N3%<1) THEN 7386
7380 FOR L%=1 TO N3%
7382 W(L1%,L%)=W(L1%,L%)-W(I4%,L%)*T
7384 NEXT L%
7386 NEXT L1%
7388 NEXT I%
7390 FOR I%=1 TO N2%
7392 L%=N2%-I%+1
7394 IF (I2%(L%,1)=I2%(L%,2)) THEN 7410
7396 I3%=I2%(L%,1)
7398 I4%=I2%(L%,2)
7400 FOR K%=1 TO N2%
7402 H1=B(K%,I3%)
7404 B(K%,I3%)=B(K%,I4%)
7406 B(K%,I4%)=H1
7408 NEXT K%
7410 NEXT I%
7412 FOR K%=1 TO N2%
7414 IF(I2%(K%,3)<>1) THEN 7450
7416 NEXT K%
7418 E1%=0
7420 FOR I%=1 TO N2%
7422 C1(I%)=W(I%,1)
7424 NEXT I%
7426 IF(I5%=1) THEN 7454
7428 PRINT
7430 PRINT"   MATRIX INVERSE"
7432 FOR I%=1 TO N2%
7434 FOR J%=1 TO N2%
7436 PRINT USING A$;B(I%,J%);
7438 NEXT J%
7440 PRINT
7442 NEXT I%
7444 PRINT
7446 PRINT"DETERMINANT = ";D3
7448 RETURN
7450 E1%=1
7452 PRINT"ERROR - MATRIX SINGULAR "
7454 RETURN
7456 W(I4%,L%)=W(I4%,L%)/P1
7458 END
8000 CLS:SOUND 2000,3:SOUND 1000,3:PRINT"             *** NETAN ***"
8010 PRINT"   DO YOU WANT INSTRUCTIONS? Y OR N"
8020 A$=INKEY$
8030 IF A$="" GOTO 8020
8040 IF A$="Y" OR A$="y" GOTO 9600 ELSE 8050
8050 CLS:BEEP:C$="MR?X+&JQLESPV!F": PI=3.14159265#
8060 DIM X(3),Y(3),D(3),M(10,3)
8070 FOR J=1 TO 3
8080 X(J)=0:Y(J)=0:D(J)=0
8090 FOR K=1 TO 10
8100 M(K,J)=0
8110 NEXT:NEXT
8120 F=0
8130 GOSUB 9040:INPUT Z$
8140 T$=LEFT$(Z$,1)
8150 K=0
8160 IF (T$>="0" AND T$<="9") OR T$="." THEN K=1
8170 FOR J=1 TO LEN(C$)
8180 IF T$=MID$(C$,J,1) THEN K=J+1
8190 NEXT
8200 IF K<>0 GOTO 8220
8210 GOSUB 9380:GOTO 8130
8220 K1=INT(K/8):K=K-8*K1
8230 ON K1+1 GOSUB 8250,8260,8270
8240 GOTO 8130
8250 ON K GOTO 8280,8470,8500,8550,8580,8660,8740
8260 ON K+1 GOTO 8840,8850,8940,9170,9210,9300,9390,9520
8270 ON K+1 GOTO 9540
8280 P=0
8290 FOR J=1 TO LEN(Z$)
8300 T$=MID$(Z$,J,1)
8310 IF T$="+" OR T$="&" THEN P=J
8320 NEXT
8330 IF P=0 GOTO 8460
8340 D(3)=1:IF MID$(Z$,P,1)="&" THEN D(3)=-1
8350 D(1)=VAL(LEFT$(Z$,P-1)):X$=MID$(Z$,P+1,1)
8360 IF X$="C" OR X$="L" GOTO 8390
8370 IF (X$>"9" OR X$<"0") AND X$<>"." AND X$<>"-" GOTO 8460
8380 D(2)=VAL(MID$(Z$,P+1)):GOTO 8450
8390 X$=MID$(Z$,P+2,1)
8400 IF (X$>"9" OR X$<"0") AND X$<>"." GOTO 8460
8410 D(2)=VAL(MID$(Z$,P+2))
8420 GOSUB 8900
8430 IF MID$(Z$,P+1,1)="C" THEN D(2)=-1/(2*PI*F*D(2))
8440 IF MID$(Z$,P+1,1)="L" THEN D(2)=D(2)*2*PI*F
8450 GOSUB 9130:RETURN
8460 GOSUB 9380:RETURN
8470 N=VAL(MID$(Z$,2,1))
8480 FOR J=1 TO 3:M(N,J)=X(J):NEXT
8490 RETURN
8500 N=VAL(MID$(Z$,2,1))
8510 FOR J=1 TO 3:D(J)=M(N,J):NEXT
8520 IF D(3)=0 GOTO 8540
8530 GOSUB 9130:RETURN
8540 GOSUB 9380:RETURN
8550 N=VAL(MID$(Z$,2,1))
8560 FOR J=1 TO 3:D(J)=M(N,J):NEXT
8570 GOSUB 9090:NEXT
8580 GOSUB 8900
8590 IF X(2)<>0 GOTO 8610
8600 GOSUB 9380:RETURN
8610 IF X(2)>0 GOTO 8630
8620 T=-1/(2*PI*F*X(2)):GOSUB 9550:PRINT "F"
8630 IF X(2)<0 GOTO 8650
8640 T= X(2)/(2*PI*F):GOSUB 9550:PRINT"H"
8650 RETURN
8660 IF X(3)<>0 AND Y(3)<>0 GOTO 8680
8670 PRINT"CAN'T";:GOSUB 9380:RETURN
8680 IF X(3)<0 THEN GOSUB 9210
8690 GOSUB 9170
8700 IF X(3)<0 THEN GOSUB 9210
8710 X(1)=X(1)+Y(1):X(2)=X(2)+Y(2)
8720 Y(1)=0:Y(2)=0:Y(3)=0
8730 RETURN
8740 IF X(3)<>0 AND Y(3)<>0 GOTO 8760
8750 PRINT"CAN'T";:GOSUB 9380:RETURN
8760 IF X(3)>0 THEN GOSUB 9300
8770 GOSUB 9170
8780 IF X(3)>0 THEN GOSUB 9300
8790 T=X(1)+Y(1):IF ABS(T)<1E-15 THEN T=1E-15
8800 X(1)=X(1)*Y(1)/T
8810 T=X(2)+Y(2):IF ABS(T)<1E-15 THEN T=1E-15
8820 X(2)=X(2)*Y(2)/T
8830 Y(1)=0:Y(2)=0:Y(3)=0:RETURN
8840 X(2)=-X(2):RETURN
8850 IF X(1)<>0 AND X(2)<>0 GOTO 8870
8860 PRINT"CAN'T";:GOSUB 9380:RETURN
8870 QT=ABS(X(2)/X(1)):IF X(3)=-1 THEN QT=1/QT
8880 PRINT"Q =";: T=QT:GOSUB 9550:RETURN
8890 RETURN
8900 IF F=0 THEN INPUT "F(HZ)";F
8910 T=F:GOSUB 9550:PRINT"HZ"
8920 IF F<>0 THEN RETURN
8930 GOSUB 9380:GOTO 8900
8940 GOSUB 8900:INPUT "ZO";ZO
8950 IF ZO=0 GOTO 9030
8960 INPUT"VELOCITY FACTOR";V
8970 IF V=0 GOTO 9030
8980 L=ATN(X(2)/ZO)
8990 IF L<0 THEN L=L+PI
9000 L=L*V*1.499E+11/(F*PI)
9010 PRINT"LENGTH =";:T=L:GOSUB 9550:PRINT"MM"
9020 RETURN
9030 GOSUB 9380:RETURN
9040 PRINT TAB(16);"Y:";
9050 D(1)=Y(1):D(2)=Y(2):D(3)=Y(3):GOSUB 9090
9060 PRINT TAB(16);"X:";
9070 D(1)=X(1):D(2)=X(2):D(3)=X(3):GOSUB 9090
9080 RETURN
9090 T=D(1):GOSUB 9550
9100 PRINT" ";MID$("&*+",D(3)+2,1);
9110 T=D(2):GOSUB 9550
9120 PRINT:RETURN
9130 FOR J=1 TO 3
9140 Y(J)=X(J):X(J)=D(J)
9150 NEXT
9160 RETURN
9170 FOR J=1 TO 3
9180 T=X(J):X(J)=Y(J):Y(J)=T
9190 NEXT
9200 RETURN
9210 IF X(3)=1 THEN RETURN
9220 T=X(1)*X(1)+X(2)*X(2)
9230 IF ABS(T)<1E-15 THEN T=1E-15
9240 T=X(1)*X(2)/T
9250 T1=X(1)*T
9260 X(1)=X(2)*T
9270 X(2)=T1
9280 X(3)=1
9290 RETURN
9300 IF X(3)=-1 THEN RETURN
9310 IF X(1)<1E-15 THEN X(1)=1E-15
9320 IF ABS(X(2))<1E-15 THEN X(2)=1E-15
9330 S1=X(1)*X(1)+X(2)*X(2)
9340 X(1)=S1/X(1)
9350 X(2)=S1/X(2)
9360 X(3)=-1
9370 RETURN
9380 PRINT"--- ENTRY ERROR ---":BEEP:BEEP:RETURN
9390 T=X(3)
9400 IF T=-1 THEN GOSUB 9210
9410 INPUT"ZO";ZO
9420 IF ZO=0 GOTO 9510
9430 T1=X(1)-ZO:T1=T1*T1+X(2)*X(2)
9440 T2=X(1)+ZO:T2=T2*T2+X(2)*X(2)
9450 RH=SQR(T1/T2):SW=(1+RH)/(1-RH)
9460 PRINT"VSWR=";:T=SW:GOSUB 9550:PRINT:IF RH=0 GOTO 9490
9470 PRINT"RETURN LOSS=";
9480 T=-20*LOG(RH)/LOG(10):GOSUB 9550:PRINT"DB"
9490 IF T=-1 THEN GOSUB 9300
9500 RETURN
9510 GOSUB 9380:RETURN
9520 STOP
9530 RETURN
9540 F=VAL(MID$(Z$,2)):RETURN
9550 IF T=0 GOTO 9590
9560 LT=LOG(ABS(T))/LOG(10)
9570 LT=10^INT(LT)/1000
9580 T=INT(T/LT+.5)*LT
9590 PRINT T;:RETURN
9600 CLS
9610 PRINT"J-CONVERTS IMPEDANCE TO IT'S CONJUGATE"
9620 PRINT"Q-CALCULATES THE Q OF X"
9630 PRINT"S-CONVERTS X FROM PARALLEL TO SERIES"
9640 PRINT"P-CONVERTS X FROM SERIES TO PARALLEL"
9650 PRINT"X-CALCULATES C OR L"
9660 PRINT"L-CALCULATES LINE LENGTH"
9670 PRINT"V-CALCULATES VSWR"
9700 PRINT"MN-COPIES X TO 1 OF 10 MEMORIES"
9710 PRINT"RN-RECALLS MN"
9720 PRINT"E-EXCHANGES REGISTERS X AND Y"
9730 PRINT"(+)-ADDS IMPEDANCES IN SERIES"
9740 PRINT"(&)-ADDS IMPEDANCES IN PARALLES (IMPEDANCES MAY BE IN SERIES FORM)"
9750 PRINT"?N-WHERE N=0 TO 9 PRINTS CONTENTS"
9760 PRINT"FXXX-SETS FREQ. FOR Z CALCULATIONS"
9770 INPUT"PRESS 'ENTER' TO START";G:GOTO 8050
10000 CLS:SOUND 2000,3:SOUND 1000,3
10010 PRINT TAB(24)"*** ELECTRONIC DESIGN MENU ***"
10015 PRINT
10020 PRINT"(1)- PI NETWORK DESIGN"
10030 PRINT"(2)- COIL INDUCTANCE CALCULATOR"
10040 PRINT"(3)- GAMMA CAPACITOR CALCULATOR"
10050 PRINT"(4)- PI AND T RESISTIVE ATTENUATORS"
10060 PRINT"(5)- FILTER MENU"
10065 PRINT"(6)- GOTO MAIN MENU"
10067 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
10069 A$=INKEY$
10071 IF A$="" GOTO 10069
10073 IF A$="1" GOTO 10100
10075 IF A$="2" GOTO 11010
10077 IF A$="3" GOTO 12330
10079 IF A$="4" GOTO 10490
10081 IF A$="5" GOTO 11410
10083 IF A$="6" GOTO 100
10085 PRINT"INVALID INPUT" : GOTO 10069
10100 CLS
10110 REM ARTICLE-1
10115 CLS:BEEP:KEY OFF
10120 PRINT TAB(23)"*** PI-NETWORK COMPUTATIONS ***"
10125 PRINT TAB(18)"*****************************************"
10130 PRINT:PRINT
10135 INPUT"ENTER PLATE VOLTAGE IN VOLTS";E
10140 INPUT"ENTER MAXIMUM DC PLATE CURRENT IN MILLIAMPERES";I:I=I/1000
10145 INPUT"ENTER DESIRED OUTPUT IMPEDANCE IN OHMS";RL
10150 INPUT"ENTER DESIRED Q VALUE";Q
10155 INPUT"ENTER OPERATING FREQUENCY IN MEGAHERTZ";F:F=F*1000000!
10160 PI=4*ATN(1)
10165 RP=E/(1.8*I) : REM PLATE RESISTANCE
10170 XC1=RP/Q : REM INPUT CAPACITOR REACTANCE
10175 XC2=RL*SQR((RP/RL)/(Q^2+1-(RL/RP))) : REM OUTPUT CAPACITOR REACTANCE
10180 XL=((Q*RP)+((RP*RL)/XC2))/(Q^2+1) : REM INDUCTOR REACTANCE
10185 C1=1/(2*PI*F*XC1) : REM INPUT CAPACITOR VALUE IN FARADS
10190 C2=1/(2*PI*F*XC2) : REM OUTPUT CAPACITOR VALUE IN FARADS
10195 L=XL/(2*PI*F) : REM INDUCTOR VALUE IN HENRIES
10200 PRINT
10205 PRINT TAB(21)"*** COMPONENTS OF PI-NETWORK ***"
10210 PRINT
10215 PRINT"INPUT Z =";RP;"  OUTPUT Z =";RL;"  Q =";Q;"  FREQ.=";F
10220 PRINT
10225 PRINT"INPUT CAPACITOR =";C1*1E+12;" pF"
10230 PRINT
10235 PRINT"OUTPUT CAPACITOR =";C2*1E+12;" pF"
10240 PRINT
10245 PRINT"INDUCTOR =";L*1000000!;" uH"
10250 PRINT
10255 INPUT"(1)-REPEAT (2)-PRINT TO FILE NAMED PI-NET.TXT (3)-GOTO MENU";X
10256 CLOSE #1
10260 ON X GOTO 10110,10270,10000
10265 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 10255
10270 INPUT"TURN ON PRINTER AND PRESS 'ENTER'";Z
10271 OPEN "PI-NET.TXT" FOR OUTPUT AS #1
10275 WRITE #1,"   *** COMPONENTS OF PI-NETWORK ***"
10280 WRITE #1,
10285 WRITE #1,"INPUT IMPEDANCE =";RP
10290 WRITE #1,"OUTPUT IMPEDANCE =";RL
10295 WRITE #1,"Q VALUE =";Q
10300 WRITE #1,"OPERATING FREQUENCY =";F
10305 WRITE #1,
10310 WRITE #1,"INPUT CAPACITOR =";C1*1E+12;" pF"
10315 WRITE #1,
10320 WRITE #1,"OUTPUT CAPACITOR =";C2*1E+12;" pF"
10325 WRITE #1,
10330 WRITE #1,"INDUCTOR =";L*1000000!;" uH"
10335 WRITE #1,
10340 WRITE #1,
10345 CLS
10350 GOTO 10255
10355 PRINT"PROGRAM TERMINATED":STOP
10490 CLS
10500 PRINT" *** PI AND T RESISTIVE ATTENUATORS ***"
10510 PRINT"R(IN) MUST BE GREATER THAN R(OUT)"
10520 INPUT"ENTER--R(INPUT)";X
10530 LET W=N-1
10540 INPUT"ENTER--R(OUTPUT)";Y
10550 IF X<Y THEN BEEP:BEEP: GOTO 10490
10560 LET Z=X/Y
10570 LET Q=(SQR(Z)+SQR(Z-1))^2
10580 LET M=10*(LOG(Q)/LOG(10))
10590 PRINT"MINIMUM SYSTEM LOSS IN DECIBELS=";M
10600 INPUT"ENTER DESIRED LOSS IN DECIBELS";L
10610 LET N=EXP(L/(10*(1/LOG(10))))
10620 LET U=N+1
10630 LET A=(2*(SQR(X*Y*N)))/(N-1)
10640 LET B=(X*(U/W))-A
10650 LET C=(Y*(U/W))-A
10660 LET D=(W*SQR((X*Y)/N))/2
10670 LET E=1/(((U/W)/X)-(1/D))
10680 LET F=1/(((U/W)/Y)-(1/D))
10690 PRINT"R(IN)=";X,"R(OUT)=";Y
10700 PRINT"MINIMUM LOSS IN DECIBELS=";M
10710 PRINT"DESIRED LOSS=";L
10720 PRINT
10730 PRINT"T=ATTENUATOR"
10740 PRINT"R1=";B
10750 PRINT"R2=";C
10760 PRINT"R3=";A
10770 PRINT
10780 PRINT"PI-ATTENUATOR"
10790 PRINT"R1=";E
10800 PRINT"R2=";F
10810 PRINT"R3=";D
10820 INPUT"(1)-CONTINUE (2)-MENU (3)-HARD COPY";J
10830 ON J GOTO 10490,10000,10850
10840 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 10820
10850 INPUT"TURN ON PRINTER AND PRESS 'ENTER'";V
10860 LPRINT"  *** T AND PI ATTENUATORS ***"
10870 LPRINT"R(IN)=";X;"R(OUT)=";Y
10880 LPRINT"MINIMUM LOSS IN DECIBELS=";M
10890 LPRINT"DESIRED LOSS IN DECIBELS=";L
10900 LPRINT
10910 LPRINT"T-ATTENUATOR"
10920 LPRINT"R1=";B
10930 LPRINT"R2=";C
10940 LPRINT"R3=";A
10950 LPRINT
10960 LPRINT"PI-ATTENUATOR"
10970 LPRINT"R1=";E
10980 LPRINT"R2=";F
10990 LPRINT"R3=";D
11000 GOTO 10820
11010 CLS
11011 CLOSE #1
11020 PRINT"  *** COIL-INDUCTANCE CALCULATOR ***"
11030 ' ** BY DAVID BEARD **
11040 ' ** 3 JUNE 1986 **
11050 INPUT"INPUT COIL RADIUS (IN INCHES)";A
11060 INPUT"INPUT BEGINNING NUMBER OF TURNS";N
11070 INPUT"INPUT ENDING NUMBER OF TURNS";Y
11080 INPUT"INPUT INCREMENT OF EACH TURN TO CALCULATE";Z
11090 INPUT"INPUT TOTAL LENGTH OF COIL (IN INCHES)";B
11100 INPUT"INPUT (1)-SCREEN PRINT (2)-PRINT TO FILE NAMED COIL.TXT";M
11110 ON M GOTO 11130,11270
11120 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 11100
11130 G=Y/B
11140 E=N/G
11150 L=((A^2*N^2)/(9*A+10*E))
11160 PRINT"RADIUS =";A,"NO. TURNS =";N,"LENGTH =";E
11170 PRINT"INDUCTANCE IN MICROHENRIES =";L
11180 PRINT
11190 IF N<Y GOTO 11210
11200 GOTO 11240
11210 N=N+Z
11220 E=E+(Z/G)
11230 GOTO 11150
11240 INPUT"(1)-NEW VALUES (2)-MENU";G
11241 REM CLOSE #1
11250 ON G GOTO 11010,10000
11260 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 11240
11270 PRINT"    **** TURN ON PRINTER ****"
11280 INPUT"PRESS 'ENTER' WHEN READY";V
11290 OPEN "COIL.TXT" FOR OUTPUT AS #1
11291 WRITE #1, "*** COIL INDUCTANCE CALCULATOR ***"
11300 G=Y/B
11310 E=N/G
11320 L=((A^2*N^2)/(9*A+10*E))
11330 WRITE #1, "RADIUS =";A;"NO. TURNS =";N;"LENGTH =";E
11340 WRITE #1, "INDUCTANCE IN MICROHENRIES =";L
11350 WRITE #1,
11360 IF N<Y GOTO 11380
11370 GOTO 11240
11380 N=N+Z
11390 E=E+(Z/G)
11400 GOTO 11320
11410 CLS
11420 PI=3.14159
11430 PRINT"FILTER COMPUTATIONS OPTIONS"
11440 PRINT"(1)- RLC LOW PASS"
11450 PRINT"(2)- RLC HIGH PASS"
11460 PRINT"(3)- RLC BAND PASS OR NOTCH"
11470 PRINT"(4)- RC PASSIVE OR ACTIVE"
11480 PRINT"(5)- MASTER MENU":BEEP
11490 INPUT"INPUT NUMBER OF YOUR CHOICE";N
11500 ON N GOTO 11520,11790,12060,12160,10000
11510 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 11490
11520 CLS
11530 PRINT"RLC LOW PASS FILTER"
11540 GOSUB 12270
11550 IF R+F>0 THEN GOTO 11590
11560 LET R=SQR(L/C)
11570 PRINT"RESISTOR=";R;" OHMS"
11580 PRINT"FREQUENCY=";R/(PI*L);" HZ"
11590 IF R+L>0 THEN GOTO 11630
11600 LET R=1/(PI*F*C)
11610 PRINT"RESISTOR=";R;" OHMS"
11620 PRINT"INDUCTOR=";R^2*C;" HENRIES"
11630 IF R+C>0 THEN GOTO 11670
11640 LET R=PI*L*F
11650 PRINT"RESISTOR=";R;" OHMS"
11660 PRINT"CAPACITOR=";1/(PI*F*R);" FARADS"
11670 IF F+L>0 THEN GOTO 11700
11680 PRINT"INDUCTOR=";R^2*C;" HENRIES"
11690 PRINT"FREQUENCY=";1/(PI*C*R);" HZ"
11700 IF F+C>0 THEN GOTO 11730
11710 PRINT"FREQUENCY=";R/(PI*L);" HZ"
11720 PRINT"CAPACITOR=";L/(R^2);" FARADS"
11730 IF L+C>0 THEN GOTO 11760
11740 PRINT"INDUCTOR=";R/(PI*F);" HENRIES"
11750 PRINT"CAPACITOR=";1/(PI*F*R);" FARADS"
11760 INPUT"(1)- REPEAT (2)- GOTO FILTER MENU";N
11770 ON N GOTO 11520,11410
11780 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 11760
11790 CLS
11800 PRINT"RLC HIGH PASS FILTER"
11810 GOSUB 12270
11820 IF F+R>0 THEN GOTO 11860
11830 LET R=SQR(L/C)
11840 PRINT"RESISTOR=";R;" OHMS"
11850 PRINT"FREQUENCY=";R/(4*PI*L);" HZ"
11860 IF R+L>0 THEN GOTO 11900
11870 LET R=1/(4*PI*F*C)
11880 PRINT"RESISTOR=";R;" OHMS"
11890 PRINT"INDUCTOR=";R^2*C;" HENRIES"
11900 IF R+C>0 THEN GOTO 11940
11910 LET R=4*PI*F*L
11920 PRINT"RESISTOR=";R;" OHMS"
11930 PRINT"CAPACITOR=";L/(R^2);" FARADS"
11940 IF F+L>0 THEN GOTO 11970
11950 PRINT"FREQUENCY=";1/(4*PI*C*R);" HZ"
11960 PRINT"INDUCTOR=";R^2*C;" HENRIES"
11970 IF R+C>0 THEN GOTO 12000
11980 PRINT"FREQUENCY=";R/(4*PI*L);" HZ"
11990 PRINT"CAPACITOR=";L/(R^2);" FARADS"
12000 IF L+C>0 THEN GOTO 12030
12010 PRINT"INDUCTOR=";R/(4*PI*F);" HENRIES"
12020 PRINT"CAPACITOR=";1/(4*PI*F*R);" FARADS"
12030 INPUT"(1)- REPEAT (2)- GOTO FILTER MENU";N
12040 ON N GOTO 11790,11410
12050 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 12030
12060 CLS
12070 PRINT"RLC BAND PASS OR NOTCH"
12080 PRINT"INPUT (0) FOR ONLY ONE UNKNOWN VALUE"
12090 GOSUB 12290
12100 IF F=0 THEN PRINT"FREQUENCY=";1/(2*PI*SQR(L*C));" HZ"
12110 IF L=0 THEN PRINT"INDUCTANCE="1/(39.5*C*F^2);" HENRIES"
12120 IF C=0 THEN PRINT"CAPACITOR=";1/(39.5*L*F^2);" FARADS"
12130 INPUT"(1)- REPEAT (2)- GOTO FILTER MENU";N
12140 ON N GOTO 12060,11410
12150 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 12130
12160 CLS
12170 PRINT"RC FILTERS"
12180 INPUT"INPUT FREQ. IN HERTZ, (0) IF UNKNOWN";F
12190 INPUT"INPUT RESISTOR VALUE IN OHMS, (0) IF    UNKNOWN";R
12200 INPUT"INPUT CAPACITOR VALUE IN FARADS, (0)    IF UNKNOWN";C
12210 IF F=0 THEN PRINT"FREQUENCY=";1/(2*PI*R*C);" HZ"
12220 IF R=0 THEN PRINT"RESISTOR=";1/(2*PI*F*C);" OHMS"
12230 IF C=0 THEN PRINT"CAPACITOR=";1/(2*PI*F*R);" FARADS"
12240 INPUT"(1)- REPEAT (2)- GOTO FILTER MENU";N
12250 ON N GOTO 12160,11410
12260 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 12240
12270 PRINT"INPUT (0) FOR ANY TWO OF THE FOLLOWING   (R,F,L, OR C)"
12280 INPUT"INPUT RESISTOR VALUE IN OHMS";R
12290 INPUT"INPUT FREQUENCY IN HERTZ";F
12300 INPUT"INPUT INDUCTOR VALUE IN HENRIES";L
12310 INPUT"INPUT CAPACITOR VALUE IN FARADS";C
12320 RETURN
12330 CLS
12340 ' ** BY DAVID BEARD **
12350 ' ** 10 OCTOBER 1986 **
12360 CLEAR
12370 PRINT"   ** GAMMA CAPACITOR CALCULATOR **"
12380 REM GAMMA MATCH CALCULATOR
12390 REM BY:DAVID BEARD 3/9/87
12400 PI=3.14159
12410 PRINT"ENTER EACH VARIALBLE ONE AT A TIME."
12420 PRINT"FOR THE UNKNOWN VARIABLE ENTER A ZERO."
12430 PRINT"DIMENSIONS ARE IN INCHES."
12440 INPUT"PRESS 'ENTER' TO BEGIN.";S
12450 CLS
12460 INPUT"ENTER O.D. OF INNER ROD";D1
12470 IF D1=0 THEN GOSUB 12620
12480 INPUT"ENTER I.D. OF OUTER ROD";D2
12490 IF (D2=0 AND D1<>0) THEN GOSUB 12620;ELSE GOTO 12500
12500 IF (D1=0 AND D2=0) GOTO 12510;ELSE GOTO 12515
12510 CLS:PRINT"YOU MUST ENTER ONE OF THE ROD DIAMETERS":GOTO 12460
12515 IF D2=D1 OR (D2<D1 AND D2<>0) THEN CLS:PRINT"     ***  IMPOSSIBLE   ***     INNER ROD MUST BE SMALLER THAN OUTER ROD":BEEP:BEEP:GOTO 12460
12520 IF D1=0 OR D2=0 THEN GOTO 12570
12530 INPUT"ENTER DIELECTRIC CONSTANT";K
12540 INPUT"ENTER LENGTH OF OUTER ROD";L
12550 INPUT"ENTER MAX CAPACITANCE DESIRED (IN PF)";C
12560 T=(D2-D1)/2
12570 IF D1=0 THEN GOTO 12640
12580 IF D2=0 THEN GOTO 12660
12590 IF K=0 THEN GOTO 12680
12600 IF L=0 THEN GOTO 12700
12610 IF C=0 THEN GOTO 12720
12620 INPUT"ENTER THICKNESS OF DIELECTRIC MATERIAL";T
12630 RETURN
12640 D1=D2-(2*T)
12650 GOTO 12750
12660 D2=D1+(2*T)
12670 GOTO 12750
12680 K=C*(12/L)*(((LOG(D2/D1))/LOG(10)))/7.36: REM PG 134 ITT REFERENCE DATA #621.3840832
12690 GOTO 12750
12700 C1=(7.36*K)/(LOG(D2/D1)/LOG(10)):L=12*C/C1
12710 GOTO 12750
12720 C1=(7.36*K)/(LOG(D2/D1)/LOG(10))
12730 C=C1*(L/12)
12740 GOTO 12750
12750 PRINT"O.D. OF INNER ROD =";D1;" INCHES"
12760 PRINT"I.D. OF OUTER ROD =";D2;" INCHES"
12770 PRINT"DIELECTRIC THICKNESS =";T;" INCHES"
12780 PRINT"DIELECTRIC CONSTANT =";K
12790 PRINT"LENGTH OF OUTER ROD =";L;" INCHES"
12800 PRINT"MAXIMUM CAPACITANCE =";C;" PF"
12810 INPUT"ENTER (1)-REPEAT (2)-MENU (3)-HARD COPY";S
12820 ON S GOTO 12330,10000,12840
12830 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 12810
12840 INPUT"TURN ON PRINTER AND PRESS 'ENTER' WHEN   READY";S
12850 LPRINT"****************************************"
12860 LPRINT"  *** GAMMA CAPACITOR DIMENSIONS ***"
12870 LPRINT"****************************************"
12880 LPRINT"O.D. OF INNER ROD =";D1;" INCHES"
12890 LPRINT"I.D. OF OUTER ROD =";D2'" INCHES"
12900 LPRINT"DIELECTRIC THICKNESS =";T;" INCHES"
12910 LPRINT"DIELECTRIC CONSTANT =";K
12920 LPRINT"LENGTH OF OUTER ROD ="L;" INCHES"
12930 LPRINT"MAXIMUM CAPACITANCE ="C;" PF"
12940 LPRINT:LPRINT
12950 GOTO 12810
13000 REM BY DAVID BEARD MAY 1987
13010 CLS:SOUND 2000,3:SOUND 1000,3
13020 PRINT TAB(27)"*** TRIG SOLUTIONS ***"
13030 PRINT
13040 PRINT"(1)-SINES (SIDE BETWEEN TWO ANGLES)"
13050 PRINT"(2)-COSINES (ANGLE BETWEEN TWO SIDES)"
13060 PRINT"(3)-SINES (SIDE ADJ. & OPPOS. TO ANGLE)"
13065 PRINT"(4)-PARTS OF A TRIANGLE"
13066 PRINT"(5)-GOTO MENU"
13067 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
13069 A$=INKEY$
13071 IF A$="" GOTO 13069
13073 IF A$="1" GOTO 13270
13075 IF A$="2" GOTO 13100
13077 IF A$="3" GOTO 13480
13079 IF A$="4" GOTO 51500
13080 IF A$="5" GOTO 100
13081 PRINT"INVALID INPUT" : GOTO 13069
13100 CLS:BEEP
13110 PRINT"         *** LAW OF COSINES ***"
13120 PRINT"    NOTE: ANGLE 'A' MUST BE BETWEEN"
13130 PRINT"           SIDES 'A' AND 'B'"
13140 PRINT
13150 INPUT"INPUT SIDE 'B'";B
13160 INPUT"INPUT SIDE 'C'";C
13170 INPUT"INPUT ANGLE 'A' IN DEGREES";A
13180 A=A/57.3
13190 X=SQR((B^2+C^2)-(2*B*C*COS(A)))
13200 PRINT"SIDE 'A' =";X
13210 X1=(SIN(A)/X)*B
13220 X2=ATN(X1/SQR(-X1*X1+1)): REM ARCSIN
13230 PRINT"ANGLE B =";X2*57.3;"DEGREES"
13240 PRINT"ANGLE C ="180-((X2*57.3)+(A*57.3));"DEGREES"
13250 INPUT"(1)-REPEAT CALCULATION (2)- GOTO MENU";P
13260 ON P GOTO 13100,13010
13270 CLS:BEEP
13280 PRINT"        *** LAW OF SINES ***"
13290 PRINT"     NOTE:SIDE BETWEEN TWO ANGLES"
13300 PRINT"              MUST BE KNOWN"
13310 INPUT"INPUT ANGLE 'A' IN DEGREES";A
13320 INPUT"INPUT ANGLE 'B' IN DEGREES";B
13330 INPUT"INPUT LENGTH OF KNOWN SIDE";L
13340 C=180-(A+B)
13350 A=A/57.3
13360 B=B/57.3
13370 C=C/57.3
13380 D=SIN(A)*57.3
13390 E=SIN(B)*57.3
13400 F=SIN(C)*57.3
13410 X=(L*E)/F  :REM SIDE B LENGTH
13420 Y=(L*D)/F  :REM SIDE A LENGTH
13430 PRINT"SIDE A =";Y
13440 PRINT"SIDE B =";X
13450 PRINT"ANGLE 'C' IS ";C*57.3;"DEGREES"
13460 INPUT"(1)- REPEAT CALCULATION (2)- GOTO MENU";P
13470 ON P GOTO 13270,13010
13480 CLS:BEEP
13490 PRINT"NOTE: LAW OF SINES FOR SIDES ADJACENT"
13500 PRINT"      AND OPPOSITE TO ANGLE"
13510 INPUT"INPUT SIDE 'A' (opposite)";A
13520 INPUT"INPUT SIDE 'B' (adjacent)";B
13530 INPUT"INPUT ANGLE 'A' IN DEGREES";X
13540 Y=(SIN(X/57.3)/A)*B
13550 Y1=ATN(Y/SQR(-Y*Y+1)): REM ARCSIN
13560 PRINT"ANGLE 'B' =";Y1*57.3;"DEGREES"
13570 PRINT"ANGLE 'C' ="180-(X+(Y1*57.3));"DEGREES"
13580 Y2=180-(X+(Y1*57.3))
13590 Y3=Y2/57.3
13600 Y4=(SIN(Y3)/(SIN(X/57.3)/A))
13610 PRINT"SIDE 'C' =";Y4
13620 INPUT"(1)- REPEAT (2)- GOTO MENU";S
13630 ON S GOTO 13480,13010
13640 PRINT"INVALID INPUT":GOTO 13620
20000 CLS:SOUND 2000,3:SOUND 1000,3
20010 PRINT TAB(28)"*** MATH4 MENU ***"
20015 PRINT
20020 PRINT"(1)-SYNTHETIC DIVISION"
20050 PRINT"(2)-MEAN AND STANDARD DEVIATION"
20052 PRINT"(3)-SIMULTANEOUS EQUATIONS WITH COMPLEX COEFFICIENTS"
20054 PRINT"(4)-STATISTICS PROGRAM"
20055 PRINT"(5)-NORMALIZE DATA"
20056 PRINT"(6)-GOTO MENU"
20057 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
20058 A$=INKEY$
20059 IF A$="" GOTO 20058
20061 IF A$="1" GOTO 20080
20067 IF A$="2" GOTO 22000
20069 IF A$="3" GOTO 7100
20070 IF A$="4" GOTO 40000
20071 IF A$="5" GOTO 45000
20073 IF A$="6" GOTO 100
20075 PRINT"INVALID INPUT" : GOTO 20057
20080 CLS:BEEP
20090 INPUT"DO YOU WANT INSTRUCTIONS?";V$
20100 IF V$="Y"GOTO 20310 ELSE GOTO 20110
20110 CLS:PRINT"SYNTHETIC DIVISION IN FORM OF POLYNOMIAL DIVIDED BY DIVISIOR OF FORM (X-C)."
20120 PRINT
20130 CLEAR 100
20140 DIM A(20),P(20)
20150 INPUT"INPUT THE DEGREE OF THE POLYNOMIAL";N
20160 PRINT"INPUT THE COEFFICIENTS"
20170 FOR I=N+1 TO 1 STEP -1
20180 INPUT A(I)
20190 NEXT I
20200 INPUT"FOR THE DIVISIOR X-C, C IS";C
20210 LET P(N+1)=A(N+1)
20220 FOR M=N TO 1 STEP -1
20230 LET P(M)=P(M+1)*C+A(M)
20240 NEXT M
20250 FOR M=N+1 TO 2 STEP -1
20260 PRINT P(M)
20270 NEXT M
20280 PRINT"THE REMAINDER = "P(1);"/(X - ";C;")"
20290 INPUT"(1)-ENTER NEW DATA (2)-GOTO MENU";V
20300 ON V GOTO 20110,20000
20310 CLS:PRINT"          ***** EXAMPLE *****"
20320 PRINT"POLYNOMIAL IS X^2+6X+9 -- (X+3)^2 AND   IS DIVIDED BY (X+3)."
20330 PRINT"  ENTER THE C AS -3. ANSWER IS 1  3  REMAINDER= 0"
20340 INPUT"PRESS ENTER TO RETURN";E
20350 GOTO 20110
22000 REM MEAN AND STANDARD DEVIATION - BASIC PROGRAMS FOR SCIENTISTS
22010 REM AND ENGINEERS - PAGE 19
22015 CLEAR
22020 CLS:BEEP
22030 M1%=80
22040 DIM X(80)
22050 PRINT TAB(12)"CALCULATION OF MEAN AND STANDARD DEVIATION (RMS DEVIATION)"
22060 GOSUB 22130
22070 GOSUB 22220
22080 PRINT
22090 PRINT "FOR ";N%;" POINTS, MEAN =";M2
22100 PRINT:PRINT TAB(17)"SIGMA =";S1
22110 PRINT:PRINT TAB(16)" VARIANCE =";(S1)^2
22120 GOTO 22060
22130 PRINT:PRINT
22140 INPUT" HOW MANY POINTS (ENTER -0- TO RETURN TO MENU)";N%
22145 IF N%=0 GOTO 20000
22150 IF(N%>M1%)THEN 22140
22160 IF(N%<2)THEN 22320
22170 FOR I%=1 TO N%
22180 PRINT I%;
22190 INPUT X(I%)
22200 NEXT I%
22210 RETURN
22220 REM FIND MEAN AND STANDARD DEVIATION OF N POINTS IN ARRAY X
22230 S3=0
22240 S4=0
22250 FOR I%=1 TO N%
22260 S3=S3+X(I%)
22270 S4=S4+X(I%)*X(I%)
22280 NEXT I%
22290 M2=S3/N%
22300 S1=SQR((S4-S3*S3/N%)/(N%-1))
22310 RETURN
22320 END
25000 GOTO 25630
25010 '  ** QUADRATIC EQUATIONS ** QUAD
25020 ' ** BY DAVID BEARD **
25030 ' ** 27 JULY 87 **
25040 CLS
25050 PRINT"         QUADRATIC EQUATION ROOTS"
25060 PRINT"       ENTER THE VALUES IN THE FORM"
25070 PRINT"           (A*X^2)+(B*X)+C=0"
25080 PRINT
25090 INPUT"INPUT THE VALUE OF 'A'";A
25100 INPUT"INPUT THE VALUE OF 'B'";B
25110 INPUT"INPUT THE VALUE OF 'C'";C
25120 S=-B
25130 T=SQR(ABS((B^2)-(4*A*C)))
25140 U=2*A
25150 V=SGN((B^2)-(4*A*C))
25160 IF V=-1 THEN 25170 ELSE 25220
25170 PRINT
25180 PRINT"          *** NO REAL ROOTS ***"
25185 PRINT
25190 PRINT"   IMAGINARY ROOTS ARE ---> ";S/U;"+- (SQRT ";((B^2)-(4*A*C));")";"/";U
25200 PRINT"   IMAGINARY ROOTS ARE ---> ";S/U;"+-J";T/U
25205 PRINT
25210 GOTO 25290
25220 X1=(S+T)/U
25230 X2=(S-T)/U
25240 PRINT
25250 REM
25260 PRINT"          *** ROOTS ARE REAL ***"
25265 PRINT
25270 PRINT"ROOTS ARE-->   ";X1
25280 PRINT"ROOTS ARE-->   ";X2
25285 PRINT
25290 PRINT"VERTEX IS AT X=";S/U;"Y=";((4*A*C)-(B^2))/(4*A)
25300 PRINT"ROOTS SUM=";-(B/A);"ROOTS PRODUCT=";(C/A)
25310 PRINT"ENTER (1) TO REPEAT (2) GO TO MENU"
25315 A$=INKEY$
25320 IF A$="" GOTO 25315
25322 IF A$="1" GOTO 25040
25325 IF A$="2" GOTO 25630
25330 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 25315
25340 CLEAR 200
25350 N=2
25360 CLS:BEEP:PI=4*ATN(1):RAD=180/PI:E=EXP(1)
25365 PRINT
25370 PRINT TAB(20)"*** INTEGRATE BY SIMPSON'S RULE ***"
25375 PRINT
25380 PRINT TAB(20)"**** PUT Y=F(X) ON LINE 25580 ****"
25382 PRINT
25385 PRINT TAB(15)"INTRINSIC FUNCTIONS ARE  ---->   PI -- RAD -- e"
25387 PRINT
25390 INPUT"INPUT LOWER LIMIT OF X";L
25400 INPUT"INPUT UPPER LIMIT OF X";U
25410 PRINT"# SEGMENTS","INTEGRATE"
25420 DX=(U-L)/N:T=0
25430 X=L:GOSUB 25580:T=T+Y
25440 X=U:GOSUB 25580:T=T+Y
25450 M=N/2:Z=0
25460 FOR J=1 TO M
25470 X=L+DX*(2*J-1):GOSUB 25580
25480 Z=Z+Y:NEXT:T=T+4*Z
25490 M=M-1:IF M=0 THEN 25530
25500 Z=0:FOR J=1 TO M
25510 X=L+DX*2*J:GOSUB 25580:Z=Z+Y
25520 NEXT:T=T+2*Z
25530 A=DX*T/3
25540 PRINT N,A
25550 IF N=256 GOTO 25600
25560 N=N*2
25570 GOTO 25420
25580 Y=1/(X^2*(4-X))
25590 RETURN
25600 PRINT"(1)-REPEAT (2)-GOTO MENU (3) GOTO INTERGAL EVALUATION"
25605 A$=INKEY$
25607 IF A$="" GOTO 25605
25609 IF A$="1" GOTO 25340
25615 IF A$="2" GOTO 25630
25616 IF A$="3" GOTO 52300
25620 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 25605
25630 CLS:SOUND 2000,3:SOUND 1000,3
25640 PRINT TAB(28)"**** MATH3 MENU ****"
25645 PRINT
25650 PRINT"(1)-DERIVATIVES"
25670 PRINT"(2)-SIMULTANEOUS EQUATIONS"
25680 PRINT"(3)-QUADRATIC EQUATIONS"
25690 PRINT"(4)-CHART PLOTTING"
25700 PRINT"(5)-INTEGRATE BY SIMPSON'S RULE"
25702 PRINT"(6)-EVALUATE EQUATION AT TWO POINTS AND DIFFERENCE OF TWO POINTS"
25703 PRINT"(7)-RMS AND MEAN VALUE OF FUNCTIONS"
25705 PRINT"(8)-GOTO MENU"
25706 PRINT:PRINT"INPUT NUMBER OF YOUR CHOICE"
25707 A$=INKEY$
25709 IF A$="" GOTO 25707
25711 IF A$="1" GOTO 25740
25715 IF A$="2" GOTO 26210
25717 IF A$="3" GOTO 25010
25719 IF A$="4" GOTO 27190
25721 IF A$="5" GOTO 25340
25722 IF A$="6" GOTO 52300
25723 IF A$="7" GOTO 53000
25724 IF A$="8" GOTO 100
25725 PRINT"INVALID INPUT" : GOTO 25707
25740 CLS:BEEP
25742 REM CALCULUS AND THE COMPUTER, WSMR 515-G665c (pb) SHELDON P. GORDON
25745 DEFDBL A,H,Q,X
25750 PRINT TAB(23)"PUT EQUATION DEF FNY(X) ON LINE 25780"
25760 PRINT:PRINT
25770 INPUT"WHAT IS THE LIMIT POINT";A
25780 DEF FNY(X)=1/(3*SIN(2*X))
25790 FOR N=1 TO 20
25800 LET H=1/2^N
25810 LET Q=(FNY(A+H)-FNY(A))/H
25815 IF N=15 GOTO 25817 ELSE PRINT N,Q
25817 IF N=15 THEN PRINT TAB(2)"APPROXIMATE VALUE OF F'(X)=";Q
25820 NEXT N
25830 PRINT
25850 PRINT"ENTER (1)-ENTER NEW DATA (2)-GO TO MENU"
25860 A$=INKEY$
25870 IF A$="" GOTO 25860
25875 IF A$="1" GOTO 25740
25880 IF A$="2" GOTO 25630
25890 PRINT"INVALID INPUT":GOTO 25860
26210 CLS
26220 E=2.718281828#
26230 PI=3.141592654#
26240 CLEAR
26250 PRINT"SIMULTANEOUS EQUATIONS"
26260 PRINT"INPUT NUMBER OF EQUATIONS"
26270 INPUT M
26280 DIM A(M,M+2)
26290 FOR Y=1 TO M
26300 FOR X=1 TO M
26310 CLS
26320 PRINT"INPUT COEFFICIENT OF Z";X;" OF EQUATION ";Y
26330 INPUT S
26340 LET A(Y,X)=S
26350 NEXT X
26360 CLS
26370 PRINT"INPUT CONSTANT OF EQUATION ";Y
26380 INPUT S
26390 LET A(Y,M+1)=S
26400 NEXT Y
26410 CLS
26420 PRINT"THE EQUATIONS YOU HAVE ENTERED ARE:"
26430 FOR Y=1 TO M
26440 FOR X=1 TO M
26450 PRINT A(Y,X);" ";
26460 NEXT X
26470 PRINT"=";A(Y,M+1)
26480 NEXT Y
26490 INPUT"(1)-TO PROCEED (2)-MAKE CHANGE";N
26500 ON N GOTO 26600,26520
26510 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 26490
26520 PRINT"INPUT EQUATION NUMBER YOU WISH TO CHANGE"
26530 INPUT Y
26540 PRINT"INPUT COEFFICIENT NUMBER TO CHANGE (";M+1;") FOR CONSTANT"
26550 INPUT X
26560 PRINT"INPUT NEW VALUE FOR COEFFICIENT ";X;", EQUATION ";Y
26570 INPUT S
26580 LET A(Y,X)=S
26590 GOTO 26410
26600 CLS
26602 FOR W=1 TO 12
26604 PRINT
26612 NEXT W
26614 PRINT TAB(34)"COMPUTING"
26620 LET K=0
26630 FOR Y=1 TO M
26640 LET A(Y,M+2)=1
26650 NEXT Y
26660 FOR L=1 TO M
26670 LET K=K+1
26680 IF A(L,K)<>0 OR L<>M THEN GOTO 26710
26690 LET K=K+1
26700 GOTO 26680
26710 IF A(L,K)=0 AND L<>M THEN GOTO 27050
26720 FOR X=M+1 TO 1 STEP -1
26730 LET A(L,X)=A(L,X)/A(L,K)
26740 NEXT X
26750 FOR J=1 TO M
26760 IF J=L THEN GOTO 26810
26770 FOR X=M+1 TO 1 STEP -1
26780 LET A(J,X)=A(J,X)-A(J,K)*A(L,X)
26790 IF ABS(A(J,X))<ABS(A(J,X+1))/10000 THEN LET A(J,X)=0
26800 NEXT X
26810 NEXT J
26820 NEXT L
26830 CLS
26840 FOR Y=1 TO M
26850 LET S=0
26860 FOR X=1 TO M
26870 IF A(Y,X)<>O THEN LET S=1
26880 NEXT X
26890 IF S=0 AND A(Y,M+1)<>0 THEN GOTO 27180
26900 NEXT Y
26910 FOR L=1 TO M
26920 IF A(L,L)<>1 THEN GOTO 27170
26930 NEXT L
26940 FOR Y=1 TO M
26950 FOR X=1 TO M
26960 IF X<>Y AND A(Y,X)<>O THEN GOTO 27170
26970 NEXT X
26980 NEXT Y
26990 FOR Y=1 TO M
27000 PRINT"X";Y;"=";A(Y,M+1)
27010 NEXT Y
27020 PRINT"(1)-REPEAT COMPUTATION (2)-GO TO MENU"
27025 A$=INKEY$
27027 IF A$="" GOTO 27025
27030 IF A$="1" GOTO 26210
27035 IF A$="2" GOTO 25630
27040 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 27025
27050 FOR H=L+1 TO M
27060 IF A(H,K)<>O THEN GOTO 27110
27070 NEXT H
27080 LET K=K+1
27090 IF K>M+1 THEN GOTO 26830
27100 GOTO 26680
27110 FOR G=1 TO M+1
27120 LET T=A(L,G)
27130 LET A(L,G)=A(H,G)
27140 LET A(H,G)=T
27150 NEXT G
27160 GOTO 26680
27170 PRINT"   **  EQUATIONS NOT INDEPENDENT  **":BEEP:BEEP:PRINT:GOTO 27020
27180 PRINT"  **  EQUATIONS CONTRADICTORY  **":BEEP:BEEP:PRINT:GOTO 27020
27190 CLS
27200 ' ** BY DAVID BEARD **
27210 ' ** 1 AUG 1987 **
27220 PI=3.141592654#
27230 E=2.718281828#
27240 PRINT"     PUT EQUATION Y=F(X) ON LINE 27470"
27250 INPUT"INPUT STARTING VALUE FOR X";X
27260 INPUT"INPUT ENDING VALUE FOR X";C
27270 INPUT"INPUT INCREMENT VALUE";I
27280 PRINT"INPUT (1)-SCREEN PRINT (2)-HARD COPY (3)-GOTO MENU (4)-ENTER NEW VALUES"
27285 A$=INKEY$
27287 IF A$="" GOTO 27285
27290 IF A$="1" GOTO 27310
27292 IF A$="2" GOTO 27390
27294 IF A$="3" GOTO 25630
27296 IF A$="4" GOTO 27190
27300 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 27285
27310 CLS:PRINT"       ***** CHART TABLE *****"
27320 PRINT"X","Y"
27330 IF X<=C GOTO 27340 ELSE 27280
27340 GOSUB 27470
27350 PRINT X,Y
27360 X=X+I
27370 GOTO 27330
27380 GOTO 27280
27390 INPUT"TURN ON PRINTER AND PRESS 'ENTER'";V
27400 LPRINT"***** CHART TABLE *****"
27410 LPRINT"X","Y"
27420 IF X<=C GOTO 27430 ELSE 27280
27430 GOSUB 27470
27440 LPRINT X,Y
27450 X=X+I
27460 GOTO 27420
27470 Y=2*X^3-7*X^2-7*X+5
27480 RETURN
30000 REM HEX/DECIMAL/OCTAL/BINARY CONVERT PROGRAM
30010 CLS
30020 PRINT "NUMBER CONVERSION PROGRAM"
30030 PRINT :PRINT "ADD ONE OF THESE PREFIXES TO YOUR NUMBER:"
30040 PRINT "B=BINARY, O=OCTAL, D=DECIMAL, H=HEXADECIMAL"
30050 PRINT "FOR EXAMPLE, HFF = D255"
30055 PRINT"INPUT 0 TO GOTO MENU"
30060 PRINT :INPUT "YOUR NUMBER:"; N$
30065 IF N$="0" GOTO 1000
30070 IF LEN(N$) < 2 THEN GOTO 30060
30080 B$ = LEFT$(N$,1): N$ = RIGHT$(N$,LEN(N$)-1)
30090 IF B$ = "B" OR B$="b" THEN BB = 2
30100 IF B$ = "O" OR B$= "o" THEN BB = 8
30110 IF B$ = "D" OR B$= "d" THEN BB = 10
30120 IF B$ = "H" OR B$= "h" THEN BB = 16
30130 IF BB <> 10 THEN GOSUB 30290 ELSE ND = VAL(N$)
30140 IF BB <> 2 THEN B = 2:GOSUB 30240: NB$ = Y$ ELSE NB$ = N$
30150 IF BB <> 8 THEN B = 8:GOSUB 30240: NO$ = Y$ ELSE NO$ = N$
30160 IF BB <> 16 THEN B = 16:GOSUB 30240:NH$ = Y$ ELSE NH$ = N$
30165 PRINT
30170 PRINT TAB(30) "NUMBER CONVERSION"
30175 PRINT
30180 PRINT "HEX --> ";NH$
30181 PRINT
30182 PRINT "DECIMAL --> ";ND
30183 PRINT
30184 PRINT "OCTAL --> ";NO$
30185 PRINT
30186 PRINT "BINARY --> ";NB$
30187 PRINT
30200 PRINT :PRINT "HIT ANY KEY TO CONTINUE....."
30210 A$=INKEY$:IF A$="" THEN 30210
30220 GOTO 30010
30230 REM SUBROUTINE TO CONVERT DEC TO BASE 'B'
30240 Z = ND:Y$ = ""
30250 Y = Z:Z = INT(Z/B):Y = Y - B * Z
30260 IF Y>9 THEN K = 55 ELSE K = 48
30270 Y$ = CHR$(Y+K)+Y$:IF Z>0 THEN 30250 ELSE RETURN
30280 REM SUBROUTINE TO CONVERT BASE 'B' TO DECIMAL
30290 ND = 0:Z = 0
30300 FOR I = 1 TO LEN(N$)
30310 Z = ASC(MID$(N$,I,1))
30320 IF Z>64 THEN K = 55 ELSE K = 48
30330 ND = ND * BB + Z - K
30340 NEXT I
30350 RETURN
40000 CLEAR
40010 CLS:BEEP
40020 PRINT TAB(33)"STATISTICS"
40030 PRINT
40040 N1=250
40050 DIM S(40),X(N1),Y(N1),Z(N1)
40060 FOR I=1 TO 40
40070 S(I)=0
40080 NEXT I
40090 S1=0
40100 PRINT"ENTER TOTAL POPULATION (0=UNKNOWN)";
40110 INPUT T9
40120 PRINT
40130 PRINT"ARE DATA (G) GROUPED OR (U) UNGROUPED";
40140 INPUT U$
40150 PRINT
40160 IF U$="G" OR U$="g" THEN 40600
40170 J=1
40180 PRINT"TO END, ENTER 9E9"
40190 PRINT"ITEM NO.";J;
40200 INPUT X(J)
40210 IF X(J)<>9.000001E+09 THEN 40240
40220 J=J-1
40230 GOTO 40290
40240 S(1)=S(1)+1
40250 S(2)=S(2)+X(J)
40260 S(4)=S(4)+X(J)*X(J)
40270 J=J+1
40280 IF J<N1 THEN 40190
40290 S(3)=S(2)/S(1)
40300 S(5)=ABS(S(3)-X(J))
40310 S(6)=S(6)+S(5)
40320 S(8)=(X(J)-S(3))^3
40330 S(9)=S(9)+S(8)
40340 S(10)=(X(J)-S(3))^4
40350 S(11)=S(11)+S(10)
40360 S(7)=S(6)/S(1)
40370 M1=S(1)
40380 M1=INT(M1/2)
40390 IF M1=0 THEN 40530
40400 K=S(1)-M1
40410 J=1
40420 I=J
40430 L=I+M1
40440 IF X(I)<=X(L) THEN 40500
40450 W=X(I)
40460 X(I)=X(L)
40470 X(L)=W
40480 I=I-M1
40490 IF I>=M1 THEN 40430
40500 J=J+1
40510 IF J>K THEN 40380
40520 GOTO 40420
40530 IF S(1)/2=INT(S(1)/2) THEN 40570
40540 M=S(1)/2+.5
40550 S(12)=X(M)
40560 GOTO 41110
40570 M=S(1)/2
40580 S(12)=(X(M)+X(M+1))/2
40590 GOTO 41110
40600 PRINT"ENTER FREQUENCY, THEN VALUE (0,0 TO END)"
40610 J=1
40620 PRINT"PAIR NO.";J;
40630 INPUT Y(J),Z(J)
40640 IF Y(J)=0 THEN 40710
40650 S(1)=S(1)+Y(J)
40660 S1=S1+1
40670 S(2)=S(2)+Y(J)*Z(J)
40680 S(4)=S(4)+Y(J)*Z(J)*Z(J)
40690 J=J+1
40700 IF J<=N1 THEN 40620
40710 REM
40720 S(3)=S(2)/S(1)
40730 FOR J=1 TO S(1)
40740 S(5)=Y(J)*ABS(S(3)-Z(J))
40750 S(6)=S(6)+S(5)
40760 S(8)=Y(J)*(Z(J)-S(3))^3
40770 S(9)=S(9)+S(8)
40780 S(10)=Y(J)*(Z(J)-S(3))^4
40790 S(11)=S(11)+S(10)
40800 NEXT J
40810 S(7)=S(6)/S(1)
40820 M1=S1
40830 M1=INT(M1/2)
40840 IF M1=0 THEN 41020
40850 K=S1-M1
40860 J=1
40870 I=J
40880 L=I+M1
40890 IF Z(I)<=Z(L) THEN 40980
40900 V=Y(I)
40910 W=Z(I)
40920 Y(I)=Y(L)
40930 Z(I)=Z(L)
40940 Y(L)=V
40950 Z(L)=W
40960 I=I-M1
40970 IF I>=1 THEN 40880
40980 J=J+1
40990 IF J>K THEN 40830
41000 GOTO 40870
41010 IF C$="S" OR IF C$="s" THEN 41040
41020 T=0
41030 K=1
41040 IF T+Y(K)>S(1)/2 THEN 41090
41050 T=T+Y(K)
41060 K=K+1
41070 GOTO 41040
41080 IF K<=S(1) THEN 41030
41090 P=((Z(K)-Z(K-1))/Y(K))*(S(1)/2-T)
41100 S(12)=(Z(K)+Z(K-1))/2+P
41110 N=S(1)
41120 PRINT"     RESULTS TABULATED AS FOLLOWS:"
41130 PRINT
41140 PRINT"                TOTAL POPULATION:";
41150 IF T9=0 THEN 41180
41160 PRINT T9
41170 GOTO 41200
41180 PRINT" UNKNOWN/NOT INDICATED"
41190 PRINT
41200 PRINT"                        DATA ARE:";
41210 IF U$="G" OR U$="g" THEN 41240
41220 PRINT"UNGROUPED"
41230 GOTO 41250
41240 PRINT" GROUPED"
41250 PRINT"                  NO. OF SAMPLES:";S(1)
41260 PRINT"                  SUM OF SAMPLES:";S(2)
41270 PRINT"                            MEAN:";S(3)
41280 PRINT"                  SUM OF SQUARES:";S(4)
41290 PRINT"                  MEAN DEVIATION:";S(7)
41300 PRINT"                          MEDIAN:";S(12)
41310 S(13)=S(4)/N-S(3)^2
41320 PRINT"                        VARIANCE:";S(13)
41330 IF U$="G" OR U$="g" THEN 41360
41340 S(14)=S(13)-(1/12)*(Z(2)-Z(1))^2
41350 PRINT"        VARIANCE WITH SHEP. CORR.:";S(14)
41360 S(15)=SQR(S(13))
41370 PRINT"              STANDARD DEVIATION:";S(15)
41380 IF U$="G" OR U$="g" THEN 41410
41390 S(16)=SQR(S(14))
41400 PRINT"STD. DEVIATION WITH SHEP. CORR.:";S(16)
41410 S(17)=S(13)*N/(N-1)
41420 PRINT"   UNBIASED ESTIMATE OF VARIANCE:";S(17)
41430 S(18)=SQR(S(17))
41440 PRINT
41450 INPUT"PRESS RETURN TO CONTINUE",QQ$
41460 PRINT"    STD. DEV. USING THAT VARIANCE:";S(18)
41470 S(19)=.67449*S(15)
41480 PRINT"                   PROPABLE ERROR:";S(19)
41490 S(20)=SQR(S(17)/N)
41500 PRINT"           STANDARD ERROR OF MEAN:";S(20)
41510 S(21)=S(15)/S(3)
41520 PRINT"              COEFF. OF VARIATION:";100*S(21);"%"
41530 S(22)=S(9)/N
41540 PRINT"            3rd MOMENT ABOUT MEAN:";S(22)
41550 S(23)=S(11)/N
41560 PRINT"            4th MOMENT ABOUT MEAN:";S(23)
41570 IF U$="G" OR U$="g" THEN 41610
41580 R=Z(2)-Z(1)
41590 S(24)=S(23)-.5*(R^2)*S(17)+(7/240)*R^4
41600 PRINT"      4th MOMENT WITH SHEP. CORR.:";S(24)
41610 S(25)=S(22)/(S(15)^3)
41620 PRINT"           MOMENT COEFF. SKEWNESS:";S(25)
41630 S(26)=S(23)/(S(13)^2)
41640 PRINT"           MOMENT COEFF. KURTOSIS:";S(26)
41650 S(27)=(S(22)*N^2)/((N-1)*(N-2))
41660 PRINT" UNBIASED ESTIM. 3rd CENT. MOMENT:";S(27)
41670 IF T9=0 THEN 41730
41680 IF N<=.05*T9 THEN 41730
41690 S(28)=S(20)*SQR((T9-N)/(T9-1))
41700 PRINT" STANDARD ERROR MEAN WITH FINITE"
41710 PRINT"     POPULATION CORRECTION FACTOR:";S(28)
41720 GOTO 41740
41730 PRINT"FINITE POPULATION CORRECTION FACTOR N/A"
41740 S(29)=3*(S(3)-S(12))/S(15)
41750 PRINT"    PEARSON'S 2nd COEFF. SKEWNESS:";S(29)
41760 IF U$="G" OR U$="g" THEN 41790
41770 S(30)=X(N)-X(1)
41780 GOTO 41800
41790 S(30)=Z(S1)-Z(1)
41800 PRINT"                            RANGE:";S(30)
41810 S(31)=S(7)/(.7978845608#*S(15))
41820 PRINT" INDEX OF MEAN DEVIATION TO"
41830 PRINT"  PRODUCT OF M.A.E. AND STD. DEV.:";S(31)
41840 PRINT
41850 INPUT"INPUT (1)-REPEAT (2)-GOTO MENU";F
41860 ON F GOTO 40000,20000
41870 PRINT"INVALID INPUT":GOTO 41850
41895 CLS:SOUND 2000,3:SOUND 1000,3
41897 PRINT TAB(14)"COMBINATIONS, PERMUTATIONS, ALGEBRAIC FACTORING MENU"
41898 PRINT:PRINT:PRINT
41900 PRINT TAB(20)"(1)-COMBINATIONS"
41905 PRINT TAB(20)"(2)-PERMUTATIONS"
41910 PRINT TAB(20)"(3)-HORNER'S METHOD TO EVALUATE AN ORDER-N POLYNOMIAL"
41914 PRINT TAB(20)"(4)-INTERSECTION OF NON-LINEAR EQUATIONS"
41915 PRINT TAB(20)"(5)-GOT0 MENU"
41918 PRINT
41920 PRINT"INPUT NUMBER OF YOUR CHOICE"
41925 A$=INKEY$
41927 IF A$="" GOTO 41925
41930 IF A$="1" GOTO 42000
41935 IF A$="2" GOTO 43000
41940 IF A$="3" GOTO 44000
41945 IF A$="4" GOTO 51000
41950 IF A$="5" GOTO 100
41970 PRINT"INVALID INPUT":GOTO 41925
42000 KEY OFF
42010 REM
42020 REM
42030 CLS:BEEP
42040 PRINT TAB(32)"COMBINATIONS"
42042 PRINT:PRINT TAB(25)"ENTER '0' TO RETURN TO MENU"
42050 PRINT:PRINT TAB(30)"HOW MANY 'THINGS'?"
42060 INPUT N
42070 IF N<=0 THEN GOTO 41895
42080 PRINT TAB(25)"HOW MANY 'THINGS' AT A TIME ?"
42090 INPUT M
42100 GOSUB 42140
42110 PRINT
42120 PRINT"COMBINATIONS OF ";N;"THINGS TAKEN ";M;" AT A TIME IS ";C
42130 PRINT:PRINT:GOTO 42050
42140 REM
42150 X=N
42160 GOSUB 42260
42170 N8=S
42180 X=M
42190 GOSUB 42260
42200 M8=S
42210 X=N-M
42220 GOSUB 42260
42230 D8=S
42240 C=INT(EXP(N8-(M8+D8))+.5)
42250 RETURN
42260 REM
42270 S=1
42280 IF X<=O THEN S=0:RETURN
42290 FOR I8=1 TO 10
42300 S=S*I8
42310 IF X=I8 THEN S=LOG(S):RETURN
42320 NEXT I8
42330 S=LOG(6.283186)/2+LOG(X)*(X+.5)-X+1/(12*X)
42340 RETURN
42350 END
43000 KEY OFF
43010 REM
43020 REM
43030 CLS:BEEP
43040 PRINT TAB(32)"PERMUTATIONS"
43042 PRINT:PRINT TAB(25)"ENTER '0' TO RETURN TO MENU"
43050 PRINT:PRINT TAB(30)"HOW MANY 'THINGS'?"
43060 INPUT N
43070 IF N<=0 THEN GOTO 41895
43080 PRINT TAB(25)"HOW MANY 'THINGS' AT A TIME ?"
43090 INPUT M
43100 GOSUB 43140
43110 PRINT
43120 PRINT"PERMUTATIONS OF ";N;"THINGS TAKEN ";M;" AT A TIME IS ";P
43130 PRINT:PRINT:GOTO 43050
43140 REM
43150 X=N
43160 GOSUB 43250
43170 N8=S
43180 X=N-M
43190 GOSUB 43250
43200 M8=S
43210 P=INT(EXP(N8-M8)+.5)
43220 RETURN
43230 C=INT(EXP(N8-(M8+D8))+.5)
43240 RETURN
43250 REM
43260 S=1
43270 IF X<=O THEN S=0:RETURN
43280 FOR I8=1 TO 10
43290 S=S*I8
43300 IF X=I8 THEN S=LOG(S):RETURN
43310 NEXT I8
43320 S=LOG(6.283186)/2+LOG(X)*(X+.5)-X+1/(12*X)
43330 RETURN
43340 END
44000 CLEAR
44010 CLS:BEEP
44020 PRINT TAB(16)"HORNER'S METHOD TO EVALUATE AN ORDER-N POLYNOMIAL"
44025 PRINT
44030 DIM A(10)
44040 INPUT"INPUT DEGREE OF POLYNOMIAL < 10";N
44050 FOR I=1 TO N+1
44060 PRINT"INPUT COEFFICIENT OF  X ^";I-1
44070 INPUT A(I)
44080 NEXT I
44090 PRINT
44100 PRINT"p(X) = ";A(1);
44110 FOR I=1 TO N
44120 IF A(I+1)<0 THEN PRINT "-"; ELSE PRINT "+";
44130 PRINT " X *(";ABS(A(I+1));
44140 NEXT I
44150 PRINT STRING$(N,")")
44160 PRINT
44170 INPUT"POLYNOMIAL IS TO BE EVALUATED WITH WHAT AGRUMENT X ";X
44180 GOSUB 44290
44190 PRINT"p(";X;") = ";P;
44200 PRINT
44210 PRINT "CONTINUE ... "
44220 PRINT
44230 FOR I=1 TO 200
44240 IF INKEY$<>"" THEN 44090
44250 NEXT I
44260 PRINT"(1)-EVALUATE POLYNOMIAL WITH DIFFERENT ARGUMENT (2)-INPUT NEW POLYNOMIAL        (3)-GOTO MENU"
44270 A$=INKEY$
44272 IF A$="" GOTO 44270
44274 IF A$="1" GOTO 44170
44275 IF A$="2" GOTO 44000
44276 IF A$="3" GOTO 41895
44277 PRINT"INVALID INPUT":GOTO 44270
44280 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 44260
44290 REM
44300 P=A(N+1)
44310 IF N=0 THEN RETURN
44320 FOR I8=N TO 1 STEP -1
44330 P=A(I8)+X*P
44340 NEXT I8
44350 RETURN
44360 END
45000 CLEAR:KEY OFF
45010 CLS:BEEP
45020 PRINT TAB(25)"UNIT DATA NORMALIZATION"
45030 PRINT:PRINT
45040 DIM X(90),Y(90)
45050 REM
45060 INPUT"HOW MANY NUMBERS TO BE INPUT ";N
45070 FOR I=1 TO N
45080 INPUT"INPUT NUMBER";X(I)
45090 Y(I)=X(I)
45100 NEXT I
45110 GOSUB 45190
45120 PRINT:PRINT
45130 PRINT " #              DATA             NORMALIZED DATA"
45140 PRINT
45150 FOR I=1 TO N
45160 PRINT USING"##         #####.####           #####.######";I,Y(I),X(I)
45170 NEXT I
45180 GOTO 45310
45190 REM
45200 GOSUB 45250
45210 FOR I8=1 TO N
45220 X(I8)=X(I8)/M
45230 NEXT I8
45240 RETURN
45250 REM
45260 M=X(I)
45270 FOR I8=2 TO N
45280 IF M<X(I8) THEN M=X(I8)
45290 NEXT I8
45300 RETURN
45310 PRINT
45315 PRINT"ENTER (1)-ENTER NEW DATA (2)-GOTO MENU"
45320 A$=INKEY$
45330 IF A$="" GOTO 45320
45340 IF A$="1" GOTO 45000
45350 IF A$="2" GOTO 100
45360 PRINT"INVALID INPUT":GOTO 45315
45370 END
50000 CLS:CLEAR
50005 BEEP
50010 PRINT TAB(23)"EVALUATES A POLYNOMIAL AND
50020 PRINT TAB(20)"ITS FIRST AND SECOND DERIVATIVE."
50030 PRINT:PRINT
50040 DIM A(20) 'COEFFICIENTS
50050 DIM X(100) 'ARGUMENTS FOR EVALUATION
50060 DIM T(100,2) 'VALUES OF POLYNOMIAL AND DERIVATIVES
50070 REM
50080 REM INPUT DEGREE AND COEFFICIENTS
50090 REM
50100 INPUT"INPUT DEGREE OF POLYNOMIAL F(X)";N
50110 REM
50120 FOR I=0 TO N
50130 INPUT"INPUT VALUES OF COEFFICIENTS, HIGHEST POWER TO CONSTANT";A(I)
50140 NEXT I
50150 REM
50160 REM PRINT THE POLYNOMIAL
50170 REM
50180 PRINT"POLYNOMIAL =";
50190 FOR I=0 TO N-1
50200 PRINT A(I);"X^";N-I; "+ ";
50210 NEXT I
50220 PRINT A(N)
50230 PRINT
50240 REM
50250 REM READ X-VALUES
50260 REM
50270 INPUT"HOW MANY X-VALUES DO YOU WANT EVALUATED ?";X9
50280 FOR J=1 TO X9
50290 INPUT"ENTER VALUES TO EVALUATE F(X), F'(X) AND F''(X)";X(J)
50300 NEXT J
50310 REM
50320 REM MAIN ROUTINE.
50330 REM
50340 REM K INDICATES WHICH DERIVATIVE.
50350 REM
50360 REM
50370 LET K=0
50380 GOSUB 50610 'RUN THROUGH X(J)
50390 GOSUB 50700 'DIFFERENTIATE
50400 LET K=1
50410 GOSUB 50610 'RUN THROUGH X(J)
50420 GOSUB 50700 'DIFFERENTIATE
50430 LET K=2
50440 GOSUB 50610 'RUN THROUGH X(J)
50450 REM
50460 REM PRINT VALUES.
50470 REM
50480 PRINT:PRINT
50490 PRINT " X", "POLY.", "FIRST DER.", "SECOND DER."
50500 PRINT
50510 FOR J=1 TO X9
50520 PRINT X(J),T(J,0),T(J,1),T(J,2)
50530 NEXT J
50535 PRINT
50540 PRINT"INPUT (1)-REPEAT (2)-GOTO MENU"
50550 A$=INKEY$
50560 IF A$="" GOTO 50550
50570 IF A$="1" GOTO 50000
50580 IF A$="2" GOTO 4000
50590 PRINT"INVALID INPUT":GOTO 50550
50600 REM
50610 REM SUBROUTINE -- RUN THROUGH X(J)
50620 REM
50630 FOR J=1 TO X9
50640 LET X=X(J)
50650 GOSUB 50780 'EVALUATE
50660 LET T(J,K)=Y
50670 NEXT J
50680 RETURN
50690 REM
50700 REM SUBROUTINE -- DIFFERENTIATE
50710 REM
50720 FOR I=0 TO N
50730 LET A(I)=A(I)*(N-I)
50740 NEXT I
50750 LET N=N-1
50760 RETURN
50770 REM
50780 REM SUBROUTINE -- EVALUATE
50790 REM
50800 LET Y=0
50810 FOR I=0 TO N
50820 LET Y=Y*X+A(I)
50830 NEXT I
50840 RETURN
50850 REM
50860 END
51000 REM -----------------------------------------------------------------------
51010 REM                       PROGRAM 18-3
51020 REM -----------------------------------------------------------------------
51030 REM           SOLVING TWO NON-SINUSOIDAL EQUATIONS
51040 REM -----------------------------------------------------------------------
51050 REM
51060 DEF FNF(X)=X^2-4*X+6
51070 F$="f(x) ENTERED ON LINE 51060"
51080 DEF FNG(X)=3
51090 G$="g(x) ENTERED ON LINE 51080"
51100 CLS:BEEP
51110 PRINT:PRINT" THIS PROGRAM USES A BRUTE FORCE TEST OF MANY POINTS"
51120 PRINT:PRINT" TO SOLVE FOR THE SOLUTION (INTERSECTION )FOR"
51130 PRINT:PRINT" TWO NON-LINEAR FUNCTIONS"
51140 PRINT
51150 PRINT TAB(23)"FOR FUNCTIONS ";F$
51160 PRINT:PRINT TAB(27)"AND ";G$
51170 PRINT
51180 INPUT"INITIAL VALUE OF X=";XI
51190 PRINT
51200 INPUT"AND FINAL VALUE OF X=";XF
51210 PRINT
51220 INPUT"FOR WHAT INCREMENT VALUE";XS
51230 PRINT:PRINT
51240 FOR X=XI TO XF STEP XS
51250 Y1=FNF(X)
51260 Y2=FNG(X)
51270 IF ABS(Y1-Y2)<=.01*ABS(Y1) THEN 51310
51280 NEXT X
51290 PRINT"NO SOLUTION OBTAINED"
51300 STOP
51310 PRINT USING"SOLUTION IS X=###.###";X
51320 PRINT:PRINT"INPUT (1)-ENTER NEW VALUES (2)-GOTO MENU"
51330 A$=INKEY$
51340 IF A$="" GOTO 51330
51350 IF A$="1" GOTO 51100
51360 IF A$="2" GOTO 41895
51370 PRINT"INVALID INPUT":GOTO 51330
51500  CLEAR:DEF  FNA(X) =  ATN (X /  SQR ( - X * X + 1))
51510  DEF  FNB(X) =  -  FNA(X) + 1.5708
51520  DIM A(3),S(3)
51530 P = 3.1415927#
51540 C = .0174532927#
51550 CLS
51560 PRINT:PRINT :PRINT :PRINT
51570 PRINT "PARTS OF A TRIANGLE"
51580 PRINT
51590 PRINT "PROBLEM TYPES:": PRINT "1. ASA": PRINT "2. SAS": PRINT "3. AAS": PRINT "4. SSA": PRINT "5. SSS": PRINT "6. END PROGRAM": PRINT
51600 PRINT "ENTER PROBLEM TYPE BY NUMBER";
51610  INPUT X
51620  IF X = 6 THEN 52170
51630  IF X = 5 THEN 52000
51640  IF X = 4 THEN 51900
51650  IF X = 3 THEN 51840
51660  IF X = 2 THEN 51750
51670  PRINT "ENTER ANGLE, SIDE, ANGLE";
51680  INPUT A(1),S(3),A(2)
51690 A(1) = A(1) * C
51700 A(2) = A(2) * C
51710 A(3) = P - A(1) - A(2)
51720 S(1) = S(3) *  SIN (A(1)) /  SIN (A(3))
51730 S(2) = S(3) *  SIN (A(2)) /  SIN (A(3))
51740  GOTO 52050
51750  PRINT "ENTER SIDE, ANGLE, SIDE";
51760  INPUT S(3),A(1),S(2)
51770 A(1) = A(1) * C
51780 S(1) =  SQR (S(3) ^ 2 + S(2) ^ 2 - 2 * S(3) * S(2) *  COS (A(1)))
51790 A(2) =  SIN (A(1)) / S(1) * S(2)
51800  REM  - ARCSIN FUNCTION
51810 A(2) =  FNA(A(2))
51820 A(3) = P - A(1) - A(2)
51830  GOTO 52050
51840  PRINT "ENTER ANGLE, ANGLE, SIDE";
51850  INPUT A(3),A(2),S(3)
51860 A(3) = A(3) * C
51870 A(2) = A(2) * C
51880 A(1) = P - A(2) - A(3)
51890  GOTO 51720
51900  PRINT "ENTER SIDE, SIDE, ANGLE";
51910  INPUT S(1),S(2),A(1)
51920 A(1) = A(1) * C
51930 T = S(2) *  SIN (A(1))
51940  IF S(1) < T THEN 52130
51950 S(3) = S(3) + Y
51960  IF S(1) <  = T THEN 51990
51970 Y =  SQR (S(1) ^ 2 - T ^ 2)
51980 S(3) = S(3) + Y
51990  GOTO 51790
52000  PRINT "ENTER SIDE, SIDE, SIDE";
52010  INPUT S(1),S(2),S(3)
52020 A(1) = (S(2) ^ 2 + S(3) ^ 2 - S(1) ^ 2) / 2 / S(2) / S(3)
52030 A(1) =  FNB(A(1))
52040  GOTO 51790
52050  PRINT 
52060  FOR I = 1 TO 3
52070  IF A(I) < 0 THEN 52130
52080  PRINT "SIDE ";I;" = "; INT (S(I) * 1000 + .5) / 1000
52090  PRINT "OPPOSITE ANGLE = "; INT (A(I) / C * 1000 + .5) / 1000;" DEGREES"
52100  NEXT I
52110  PRINT 
52120  GOTO 51560
52130  PRINT 
52140  PRINT "NO SOLUTION"
52150  PRINT 
52160  GOTO 51560
52170  GOTO 13000
52300 CLS:BEEP
52310 PI=4*ATN(1):E=EXP(1):RAD=180/PI
52320 REM PROGRAM TO EVALUATE EQUATION AT TWO LIMITS AND AT DIFFERENCE
52330 REM OF UPPER AND LOWER LIMIT FOR CHECK OF INTEGRATION.
52340 PRINT
52350 PRINT TAB(17)"PROGRAM TO EVALUATE EQUATION AT TWO POINTS AND"
52360 PRINT TAB(17)"DIFFERENCE OF TWO POINTS FOR INTEGRATION CHECKS"
52370 PRINT TAB(17)"INTRINSIC FUNCTIONS ARE  ---->  PI -- RAD -- e"
52375 PRINT
52380 PRINT TAB(25)"ENTER EQUATION ON LINE 52440"
52390 PRINT
52400 INPUT"INPUT LOWER LIMIT";L
52410 PRINT
52420 INPUT"INPUT UPPER LIMIT";U
52430 X=L
52440 Y=((-1/4)*X)-((1/16)*LOG((4-X)/X))
52450 IF X=U GOTO 52490
52460 Y1=Y
52470 X=U
52480 GOTO 52440
52490 Y2=Y
52500 Z=Y2-Y1
52510 PRINT
52520 PRINT"LOWER LIMIT =";L;" UPPER LIMIT =";U
52530 PRINT
52540 PRINT"LOWER VALUE =";Y1;" UPPER VALUE =";Y2
52550 PRINT:PRINT
52555 PRINT"**********************************************
52560 PRINT TAB(10)"DIFFERENCE VALUE =  ";Z
52565 PRINT"**********************************************
52570 PRINT:PRINT
52580 PRINT"INPUT (1)-TO ENTER NEW VALUES (2)-GOTO MENU"
52590 A$=INKEY$
52591 IF A$="" GOTO 52590
52592 IF A$="1" GOTO 52300
52594 IF A$="2" GOTO 25630
52600 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 52580
53000 CLEAR 200
53010 N=2:PI=4*ATN(1):E=EXP(1):RAD=180/PI
53020 CLS:BEEP
53030 PRINT:PRINT
53040 PRINT TAB(16)"*** CALCULATE MEAN AND RMS VALUE OF FUNCTION ***"
53050 PRINT
53060 PRINT TAB(10)"**** PUT Y=F(X) ON LINE 53300 AND F^2(X) ON LINE 53520 ****"
53070 PRINT
53080 PRINT TAB(17)"NOTE - BEST TO PUT EQUATION IN FORM OF THETA"
53090 PRINT TAB(17)"INTRINSIC FUNCTIONS ARE ---->  PI -- RAD -- e"
53095 PRINT
53100 INPUT"INPUT LOWER LIMIT OF X";L
53110 INPUT"INPUT UPPER LIMIT OF X";U
53120 PRINT
53130 PRINT"# SEGMENTS","MEAN VALUE"
53140 DX=(U-L)/N:T=0
53150 X=L:GOSUB 53300:T=T+Y
53160 X=U:GOSUB 53300:T=T+Y
53170 M=N/2:Z=0
53180 FOR J=1 TO M
53190 X=L+DX*(2*J-1):GOSUB 53300
53200 Z=Z+Y:NEXT:T=T+4*Z
53210 M=M-1:IF M=0 THEN 53250
53220 Z=0:FOR J=1 TO M
53230 X=L+DX*2*J:GOSUB 53300:Z=Z+Y
53240 NEXT:T=T+2*Z
53250 A=DX*T/3
53260 PRINT N,A/(U-L)
53270 IF N=128 GOTO 53320
53280 N=N*2
53290 GOTO 53140
53300 Y=1-EXP(-.5*X)
53310 RETURN
53320 N=2:PI=4*ATN(1):E=EXP(1)
53330 BEEP
53340 PRINT
53345 PRINT TAB(15)" NOTE -- Equation must be SQUARED on line 53520 for RMS"
53348 PRINT
53350 PRINT"# SEGMENTS","RMS VALUE"
53360 DX=(U-L)/N:T=0
53370 X=L:GOSUB 53520:T=T+Y
53380 X=U:GOSUB 53520:T=T+Y
53390 M=N/2:Z=0
53400 FOR J=1 TO M
53410 X=L+DX*(2*J-1):GOSUB 53520
53420 Z=Z+Y:NEXT:T=T+4*Z
53430 M=M-1:IF M=0 THEN 53470
53440 Z=0:FOR J=1 TO M
53450 X=L+DX*2*J:GOSUB 53520:Z=Z+Y
53460 NEXT:T=T+2*Z
53470 A=DX*T/3
53480 PRINT N,SQR(A/(U-L))
53490 IF N=128 GOTO 53540
53500 N=N*2
53510 GOTO 53360
53520 Y=(X/(SQR(4-X)))^2
53530 RETURN
53540 PRINT:BEEP:PRINT"(1)-REPEAT (2)-GOTO MENU"
53550 A$=INKEY$
53560 IF A$="" GOTO 53550
53570 IF A$="1" GOTO 53000
53580 IF A$="2" GOTO 25630
53590 PRINT"INVALID INPUT":BEEP:BEEP:GOTO 53550
