ZXASCII
AUTO -1
PROG globe3d
10 REM Blocky World Map
20 PAPER 0: CLS 
30 DATA "eEEDDNPPLcIbJbCIh"
40 DATA "dGEaDaBPPKbBcJbPEMaEe"
50 DATA "MMEJPMIMKBPLCbEMIAON5MPOMMA"
60 DATA "3M2CEDFLAMbNHON66C"
70 DATA "LHPOPOAPOACbEBHB55LDFb"
80 DATA "AB4O2IcGOON55OABb"
90 DATA "b4JPDBcMLLPDHHH6LIc"
100 DATA "b5CeDABBDPN6HAKc"
110 DATA "bF2LLfNPOMMPH6IBd"
120 DATA "cHPAEeE5HOJH3LCe"
130 DATA "dBHADdF5ODABLBPIEe"
140 DATA "fGMIcBHL4CbGAKAICd"
150 DATA "fN2MeHPOLeGFKAMc"
160 DATA "f5dF2KfCbBDb"
170 DATA "fB3KdF2CIgNNIb"
180 DATA "g2LfPLACeE3Ob"
190 DATA "gPLCfPCgBPHPLb"
200 DATA "gPCsDCAF"
210 DATA "gLvEC"
220 DATA "gGx"
230 LOAD "fonts:default_16x16.font" BANK 5
240 FONT 5
250 FOR f=0 TO 19
260 READ a$
270 LET z$=""
280 FOR i=1 TO LEN a$
290 IF a$(i)<"A" THEN FOR b=1 TO CODE a$(i)-48: LET z$=z$+"p": NEXT b: GO TO 320
300 IF a$(i)<"a" THEN LET z$=z$+#1+CHR$(CODE a$(i)+32): GO TO 320
310 FOR b=1 TO CODE a$(i)-96: LET z$=z$+" ": NEXT b
320 NEXT i
330 PRINT INK 4;PAPER 1;AT 4+f,9;z$
340 NEXT f
350 REM 3D Sphere
360 OPTION "fdist",2000: RADIANS: LET np=100000: LET hlines=INT SQR np: LET vlines=hlines: LET r=175: LET yc=4*FONTw: LET xc=9*FONTh: LET xs=(31.5*FONTw)/hlines: LET ys=(20*FONTh)/vlines
370 DIM pts(hlines*vlines,4)
380 LET i=1: FOR m=1 TO vlines-1: FOR n=0 TO hlines: LET pts(i,1)=r*(SIN(PI*m/vlines)*COS(2*PI*n/hlines)): LET pts(i,2)=r*(SIN(PI*m/vlines)*SIN(2*PI*n/hlines)): LET pts(i,3)=r*(COS(PI*m/vlines)): LET pts(i,4)=POINT(xc+(n*xs),yc+(m*ys)): INC i: NEXT n: NEXT m
390 CLS : SCREEN LOCK: LET c=(2*PI)/360: LET angle=0: TRANSFORM3D pts() ROTATE DEGTORAD 90,0,0
400 PROJECT3D pts() TO dst() ROTATE 0,angle,0
410 PLOT dst()
420 INC angle,c,0 TO 2*PI
430 WAIT SCREEN : CLS: GO TO 400
