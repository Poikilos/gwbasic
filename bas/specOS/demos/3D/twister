ZXASCII
AUTO -1
PROG twister
10 REM Twister demo effect
20 PAPER 0: MOUSE HIDE: CLS: SCREEN LOCK: GO SUB 120
30 DEGREES: LET sw=SCRw: LET sh=SCRh: LET ang=0: LET xoff=Sw/2-20: LET aa=1: LET t=mSECS
40 LET ys=0: LET amp=6+SIN(COS(aa)*17)*16: FOR y=0 TO Sh-1: GLOBAL ym=y MOD gw+1,yamp=(y/amp)+ang
50 GLOBAL x1=(SIN yamp*100)+xoff,x2=(SIN(yamp+90)*100)+xoff,x3=(SIN(yamp+180)*100)+xoff,x4=(SIN(yamp+270)*100)+xoff
70 IF x2-x1>=1 THEN POKE$ 0,ys+x1,tex$(1,ym,x2-x1)
80 IF x3-x2>=1 THEN POKE$ 0,ys+x2,tex$(2,ym,x3-x2)
90 IF x4-x3>=1 THEN POKE$ 0,ys+x3,tex$(3,ym,x4-x3)
100 IF x1-x4>=1 THEN POKE$ 0,ys+x4,tex$(4,ym,x1-x4)
110 INC ys,SCRw: NEXT y: LET t1=mSECS: INC aa,(t1-t)/20,0 TO 359: LET t=t1: LET ang=-180+SIN aa*180: WAIT SCREEN: CLS: GO TO 40
120 REM Prepare texture
130 PALETTE DEFAULT: INK 15: PRINT MOVE (SCRw-(FONTw*32))/2,(SCRh/2)-FONTh*2;"Please wait - preparing textures"
140 GRAPHIC NEW tex LOAD "twist_tex.bmp": LET gw=GFXw tex/4
150 IF FILEEXISTS "twister_dat" THEN LOAD "twister_dat" DATA : GO TO 210
160 DIM tex$(4,gw,gw): WINDOW GRAPHIC tex
170 LET pc=gw: FOR y=0 TO gw-1: LET y1=y+1: FOR x=1 TO gw: LET s=gw/x: GLOBAL t1$="",t2$="",t3$="",t4$="",sx=0
180 IF LEN t1$<x THEN GLOBAL t1$=t1$+CHR$(POINT(sx,y)),t2$=t2$+CHR$(POINT(sx+gw,y)),t3$=t3$+CHR$(POINT(sx+gw+gw,y)),t4$=t4$+CHR$(POINT(sx+gw+gw+gw,y)): INC sx,s: GO TO 180
190 LET tex$(1,y1,x)=t1$: LET tex$(2,y1,x)=t2$: LET tex$(3,y1,x)=t3$: LET tex$(4,y1,x)=t4$: NEXT x: WINDOW 0: PRINT MOVE SCRw/2,SCRh/2;100-INT(pc/gw*100);"%     ": SCREEN UPDATE: WINDOW GRAPHIC tex: DEC pc: NEXT y
200 SAVE "twister_dat" DATA tex$()
210 WINDOW 0: PALETTE COPY tex,0,256 TO 0
220 RETURN 
