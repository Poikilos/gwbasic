ZXASCII
AUTO -1
PROG fern
10 REM Fractal Fern
20 PALETTE 0,128,128,128: PALETTE 1,0,255,0: PALETTE 30,0,23,0: RAINBOW 1 TO 30: PALETTE 31,0,32,0: PALETTE 255,0,32,0: RAINBOW 31 TO 255: PAPER 0: CLS 
30 LET maxpoints=1000000: LET x=0: LET y=0
40 FOR n=1 TO maxpoints
50 LET p=RND*100: LET xt=x
60 CASE 1
70 WHEN p<77: LET x=0.85*x+0.04*y+0.075: LET y=-0.04*xt+0.85*y+0.18
80 WHEN p<89: LET x=0.20*x-0.26*y+0.4: LET y=0.23*xt+0.22*y+0.045
90 WHEN p<99: LET x=-0.15*x+0.28*y+0.575: LET y=0.26*xt+0.24*y-0.086
100 OTHERWISE: LET x=0.5: LET y=0.26*y
110 END CASE
120 IF n>100 THEN LET xn=x*SCRw: LET yn=SCRh-y*SCRh: PLOT INK MIN(POINT(xn,yn)+1,255);xn,yn
130 NEXT n
140 PAUSE 0
