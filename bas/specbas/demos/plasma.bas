ZXASCII
AUTO -1
1 ON ERROR GO TO 240
2 REM SCREEN FULL 640,480
10 LET BSIZE=1 SHL INT(RND*4)
20 LET PLASMAX=(RND*1.8)+0.5
30 LET PLASMAY=(RND*1.8)+1.25+(INT(RND*2)*180)
40 LET XSIZE=(RND*30)+40
50 LET YSIZE=(RND*50)+20
60 LET XSTART=RND*400
70 LET YSTART=RND*200
80 LET XINC=RND*3
90 LET YINC=RND*3
100 DEGREES: FOR i=0 TO 7
110 LET r1=MIN(INT(RND*5)*64,255): LET r2=0: LET g1=MIN(INT(RND*5)*64,255): LET g2=0: LET b1=MIN(INT(RND*5)*64,255): LET b2=0
120 LET k=0: FOR j=0 TO 31: LET r=r1+(k*((r2-r1)/32)): LET g=g1+(k*((g2-g1)/32)): LET b=b1+(k*((b2-b1)/32)): PALETTE j+(i*32),r,g,b: LET k=k+1: NEXT j
130 NEXT i
140 WINDOW NEW PLASMA,0,0,1400,1400
150 WINDOW PLASMA: LET d=0: FOR F=0 TO 1400 STEP BSIZE: INK d MOD 255: RECTANGLE 0,F TO 700,F+BSIZE,"": INC d: NEXT F
160 LET x=1: LET y=1
170 FOR F=1 TO 1397: INC X,PLASMAX: WINDOW COPY PLASMA,0,0,1,1400 TO PLASMA,F,(SIN(X)*XSIZE)-XSIZE: NEXT F
180 FOR F=0 TO 1398: INC Y,PLASMAY: WINDOW COPY PLASMA,0,F,1400,1 TO PLASMA,(SIN(Y)*YSIZE)-YSIZE,F: NEXT F
190 INC XSTART,XINC: IF XSTART>360 THEN DEC XSTART,360
200 INC YSTART,YINC: IF YSTART>360 THEN DEC YSTART,360
210 WINDOW MOVE PLASMA,(SIN(XSTART)*140)-140,(SIN(YSTART)*100)-100
220 PALETTE SHL 1: WAIT SCREEN 20: IF MOUSEBTN=0 THEN GO TO 190
230 DO WHILE MOUSEBTN<>0: LOOP: RUN 
240 INK RGBc 0: WINDOW ERASE PLASMA: PALETTE 0,0,0,0
