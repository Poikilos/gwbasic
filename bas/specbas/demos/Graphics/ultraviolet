ZXASCII
AUTO -1
PROG ultraviolet
10 REM Ultraviolet
20 RADIANS: PAPER 0: CLS: SCREEN LOCK 
30 LET sz=50,ns=30,fr=.5,tr=25,dm=.25,tn=.98,ph=RND*2*PI,amp=85,frq=.015,ofs=285,md=6
40 DIM sp(tr),fr(tr),nd(tr,sz,4): LET mx=MOUSEx,my=MOUSEy
50 FOR i=1 TO tr: LET sp(i)=(.45+.025*(i/tr))+(RND*.1)-.05,fr(i)=fr+(RND*.01)-.005: FOR j=1 TO sz: LET nd(i,j,1)=mx,nd(i,j,2)=my: NEXT j: NEXT i
60 LET mx=(MOUSEx+(mx*(md-1)))/md,my=(MOUSEy+(my*(md-1)))/md
70 FOR i=1 TO tr: INK i: LET s=sp(i): INC nd(i,1,3),(mx-nd(i,1,1))*s: INC nd(i,1,4),(my-nd(i,1,2))*s
80 FOR j=1 TO sz: IF j>1 THEN INC nd(i,j,3),((nd(i,j-1,1)-nd(i,j,1))*s)+(nd(i,j-1,3)*dm): INC nd(i,j,4),((nd(i,j-1,2)-nd(i,j,2))*s)+(nd(i,j-1,4)*dm)
90 LET nd(i,j,3)=nd(i,j,3)*fr,nd(i,j,4)=nd(i,j,4)*fr: INC nd(i,j,1),nd(i,j,3): INC nd(i,j,2),nd(i,j,4): LET s=s*tn: NEXT j
100 MOVE TO nd(i,1,1),nd(i,1,2): FOR j=1 TO sz-2: CURVE nd(i,j,1),nd(i,j,2) TO (nd(i,j,1)+nd(i,j+1,1))*.5,(nd(i,j,2)+nd(i,j+1,2))*.5,ns: NEXT j
110 CURVE nd(i,j,1),nd(i,j,2) TO nd(i,j+1,1),nd(i,j+1,2),ns: NEXT i
120 INC ph,frq: LET h=(ofs+SIN ph*amp) MOD 360: FOR f=1 TO tr: PALETTE HSV f,h,255,((tr-f)*(191/tr))+64: NEXT f
130 WAIT SCREEN : CLS: GO TO 60
