100 :REM'

****************************************
ROBOTS BY LANCE MICKLUS, COPYRIGHT 1978
VERSION 1.1
****************************************


120 DEFINT A-Z
140 DIM PO(50)
160 CLS
180 PRINT@320,"USING THE UP, DOWN, LEFT, & RIGHT ARROW KEYS, SEE IF YOU"
200 PRINT"CAN KEEP YOUR MAN ''X'' FROM GETTING ZAPPED BY THE ROBOTS."
220 PRINT:INPUT"HIT ''ENTER'' TO BEGIN";A$
240 FE=5 : RT=20
260 :REM'

GET INITIAL CONDITIONS
280 CLS : PRINT TAB(20) "ROBOT BY LANCE MICKLUS" : PRINT : PRINT
300 PRINT"FENCES =";STR$(FE)
320 PRINT"ROBOTS =";STR$(RT)
340 PRINT
360 PRINT"TYPE FENCES TO CHANGE NUMBER OF FENCES"
380 PRINT"     ROBOTS TO CHANGE NUMBER OF ROBOTS"
400 PRINT"     PLAY IF READY TO PLAY THE GAME"
420 PRINT"     QUIT TO END THE GAME"
440 PRINT
460 INPUT"OPTION";A$
480 A$=LEFT$(A$,1)
500 IF A$="F" THEN 620
520 IF A$="R" THEN 680
540 IF A$="P" THEN 740
560 IF A$="Q" THEN 2600
580 GOTO 280
600 REM ** CHANGE FENCES **
620 INPUT"HOW MANY FENCES (1-15)";FENCES
640 IF FE<1 OR FE>15 THEN 620 :ELSE GOTO 280
660 REM ** CHANGE ROBOTS **
680 INPUT"HOW MANY ROBOTS (1-50)";RT
700 IF RT<1 OR RT>50 THEN 680 :ELSE GOTO 280
720 :REM'

DRAW PLAYING FIELD
740 CLS
760 FOR Y=0 TO 47
780 SET(0,Y)
800 SET(1,Y)
820 SET(126,Y)
840 SET(127,Y)
860 NEXT
880 FOR X=2 TO 125
900 SET(X,0)
920 SET(X,47)
940 NEXT
960 :REM'
980 REM ** DRAW FENCES ***
1000 :REM'
1020 FOR I=1 TO FE
1040 ON RND(4) GOSUB 1100, 1200, 1300, 1400, 1500
1060 NEXT
1080 GOTO 1540
1100 Y=RND(43)+2 : IF POINT(2,Y) THEN 1100
1120 FOR X=2 TO 5+RND(40)
1140 IF POINT(X+1,Y) OR POINT(X+1,Y+1) OR POINT(X+1,Y-1) RETURN
1160 SET(X,Y)
1180 NEXT : RETURN
1200 Y=RND(43)+2 : IF POINT(2,Y) THEN 1200
1220 FOR X=125 TO 120-RND(40) STEP -1
1240 IF POINT(X-1,Y) OR POINT(X-1,Y+1) OR POINT(X-1,Y-1) RETURN
1260 SET(X,Y)
1280 NEXT : RETURN
1300 X=RND(122)+2 : IF POINT(X,1) THEN 1300
1320 FOR Y=1 TO 3+RND(12)
1340 IF POINT(X,Y+1) OR POINT(X-1,Y+1) OR POINT(X+1,Y+1) OR POINT(X+2,Y+1) RETURN
1360 SET(X,Y) : SET(X+1,Y)
1380 NEXT : RETURN
1400 X=RND(122)+2 : IF POINT(X,1) THEN 1300
1420 FOR Y=46 TO 44-RND(12) STEP -1
1440 IF POINT(X,Y-1) OR POINT(X-1,Y-1) OR POINT(X+1,Y-1) OR POINT(X+2,Y-1) RETURN
1460 SET(X,Y) : SET(X+1,Y)
1480 NEXT : RETURN
1500 GOTO 1100
1520 REM ** POSITION PIECES **
1540 HU=64+RND(893)
1560 IF PEEK(15360+HU)<>32 THEN 1540 :ELSE POKE 15360+HU,88
1580 RO=RT
1600 FOR I=1 TO RO
1620 J=64+RND(893)
1640 IF PEEK(15360+J)<>32 THEN 1620
1660 PO(I)=J : POKE 15360+J,79
1680 NEXT
1700 IF INKEY$="" THEN 1700
1720 :REM'

GAME LOOP
1740 :REM'
1760 REM ** PLAYER'S MOVE **
1780 :REM'
1800 A$=INKEY$
1820 IF A$=CHR$(8) POKE 15360+HU,32 : HU=HU-1 : GOTO 1920
1840 IF A$=CHR$(9) POKE 15360+HU,32 : HU=HU+1 : GOTO 1920
1860 IF A$=CHR$(91) POKE 15360+HU,32 : HU=HU-64 : GOTO 1920
1880 IF A$=CHR$(10) POKE 15360+HU,32 : HU=HU+64 : GOTO 1920
1900 GOTO 2080
1920 IF PEEK(15360+HU)=32 POKE 15360+HU,88 : GOTO 1800
1940 IF PEEK(15360+HU)<>79 THEN 2000
1960 CLS : PRINT CHR$(23) : PRINT@320,"ZAPPED BY A ROBOT"
1980 FOR X=1 TO 6000 : NEXT : GOTO 260
2000 CLS : PRINT CHR$(23) : PRINT@ 320,"ZAPPED BY THE FENCE" : GOTO 1980
2020 :REM'
2040 REM ** ROBOTS MAKE A MOVE **
2060 :REM'
2080 I = RND(10+RO)
2100 IF I > RO THEN 1800
2120 J = PO(I)
2140 POKE 15360+J,32
2160 Y1=INT(J/64) : X1=J-64*Y1
2180 Y2=INT(HU/64) : X2=HU-64*Y2
2200 J1=J+64*SGN(Y2-Y1)+SGN(X2-X1)
2220 IF PEEK(15360+J1)=32 POKE 15360+J1,79 : PO(I)=J1 : GOTO 1800
2240 IF PEEK(15360+J1)=88 THEN 1960
2260 GOSUB 2460
2280 IF RO>0 THEN 1800
2300 CLS : PRINT CHR$(23)
2320 PRINT@ 320,"YOU WIN!!!"
2340 GOTO 1980
2360 END
2380 :REM'

SUBROUTINES
2400 :REM'
2420 REM ** DELETE A ROBOT FROM LIST **
2440 :REM'
2460 FOR I1=I+1 TO RO
2480 PO(I1-1)=PO(I1)
2500 NEXT
2520 RO=RO-1
2540 RETURN
2560 :REM'
2580 REM
2600 END
