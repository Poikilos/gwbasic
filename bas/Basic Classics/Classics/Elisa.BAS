1 REM
2 REM  ELISA DOCTOR
3 REM  CREATED BY JOSEPH WEIZENBAUM
4 REM  THIS VERSION BY JEFF SHRAGER
5 REM  EDITED AND MODIFIED FOR MITS 8K BASIC 4.0 BY 
6 REM  STEVE NORTH -- CREATIVE COMPUTING
7 REM  PO BOX 789-M MORRISTOWN NJ  07960
8 REM
9 REM  -INITIALIZATION-
10 CLEAR1000:DIMC$(72),I$(72),K$(72),F$(72),S$(72),R$(72),P$(72),Z$(72):DIMS(36),R(36),N(36):N1=36
11 N2=12:N3=112:FORX=1TON1+N2+N3:READZ$
12 NEXTX:REM SAME AS RESTORE
13 FORX=1TON1:READS(X),L:R(X)=S(X)
14 N(X)=S(X)+L-1:NEXTX
15 PRINT"HI!  I'M ELIZA.  WHAT'S YOUR PROBLEM?"
16 REM
17 REM -USER INPUT SECTION-
18 REM
19 INPUTI$
20 I$=" "+I$+" "
21 REM GET RID OF APOSTROPHES
22 FORL=1TOLEN(I$)
23 IFMID$(I$,L,1)="'"THENI$=LEFT$(I$,L-1)+RIGHT$(I$,LEN(I$)-L):GOTO23
24 IFL+4<=LEN(I$)THEN25:ELSE26
25 IFMID$(I$,L,4)="SHUT"THENPRINT"SHUT UP...":GOTO100
26 NEXTL:IFI$=P$THENPRINT"PLEASE DON'T REPEAT YOURSELF!":GOTO16
27 REM
28 REM -FIND KEYWORD IN I$-
29 REM
30 RESTORE:S=0:FORK=1TON1:READK$
31 IFS>0THEN34
32 FORL=1TOLEN(I$)-LEN(K$)+1:IFMID$(I$,L,LEN(K$))=K$THENS=K:T=L:F$=K$
33 NEXTL
34 NEXTK:IFS>0THENK=S:L=T:GOTO36:ELSEK=36:GOTO53:REM  WE DIDN'T FIND ANY KEYWORDS
35 REM
36 REM TAKE RIGHT PART OF STRING AND CONJUGATE IT
37 REM USING THE LIST OF STRINGS TO BE SWAPPED
38 REM
39 RESTORE:FORX=1TON1:READZ$
40 NEXTX:REM SKIP OVER KEYWORDS
41 C$=" "+RIGHT$(I$,LEN(I$)-LEN(F$)-L+1)
42 FORX=1TON2/2:READS$,R$
43 FORL=1TOLEN(C$)
44 IFL+LEN(S$)>LEN(C$)THEN47:ELSEIFMID$(C$,L,LEN(S$))<>S$THEN47
45 C$=LEFT$(C$,L-1)+R$+RIGHT$(C$,LEN(C$)-L-LEN(S$)+1)
46 L=L+LEN(R$):GOTO50
47 IFL+LEN(R$)>LEN(C$)THEN50:ELSEIFMID$(C$,L,LEN(R$))<>R$THEN50
48 C$=LEFT$(C$,L-1)+S$+RIGHT$(C$,LEN(C$)-L-LEN(R$)+1)
49 L=L+LEN(S$)
50 NEXTL:NEXTX
51 IFMID$(C$,2,1)=" "THENC$=RIGHT$(C$,LEN(C$)-1):REM ONLY ONE SPACE
52 REM
53 REM NOW USING THE KEYWORD NUMBER (K) GET REPLY
54 REM
55 RESTORE
56 FORX=1TON1+N2
57 READZ$:NEXTX:FORX=1TOR(K):READF$
58 NEXTX:REM  READ RIGHT REPLY
59 R(K)=R(K)+1
60 IFR(K)>N(K)THENR(K)=S(K)
61 IFRIGHT$(F$,1)<>"*"THENPRINTF$:P$=I$:GOTO16
62 PRINTLEFT$(F$,LEN(F$)-1);C$
63 P$=I$:GOTO16
64 REM
65 REM -PROGRAM DATA FOLLOWS-
66 REM
67 REM KEYWORDS
68 REM
69 DATA"CAN YOU","CAN I","YOU ARE","YOURE","I DONT","I FEEL":DATA"WHY DONT YOU","WHY CANT I","ARE YOU","I CANT","I AM","IM ":DATA"YOU ","I WANT","WHAT","HOW","WHO","WHERE","WHEN","WHY":DATA"NAME","CAUSE","SORRY","DREAM","HELLO","HI ","MAYBE"
70 DATA" NO","YOUR","ALWAYS","THINK","ALIKE","YES","FRIEND":DATA"COMPUTER","NOKEYFOUND"
71 REM
72 REM STRING DATA FOR CONJUGATIONS
73 REM
74 DATA" ARE "," AM ","WERE ","WAS "," YOU "," I ","YOUR ","MY ":DATA" IVE "," YOUVE "," IM "," YOURE "
75 REM
76 REM REPLIES
77 REM
78 DATA"DON'T YOU BELIEVE THAT I CAN*":DATA"PERHAPS YOU WOULD LIKE TO BE ABLE TO*":DATA"YOU WANT ME TO BE ABLE TO*":DATA"PERHAPS YOU DON'T WANT TO*":DATA"DO YOU WANT TO BE ABLE TO*":DATA"WHAT MAKES YOU THINK I AM*"
79 DATA"DOES IT PLEASE YOU TO BELIEVE I AM*":DATA"PERHAPS YOU WOULD LIKE TO BE*":DATA"DO YOU SOMETIMES WISH YOU WERE*":DATA"DON'T YOU REALLY*":DATA"WHY DON'T YOU*":DATA"DO YOU WISH TO BE ABLE TO*":DATA"DOES THAT TROUBLE YOU?"
80 DATA"TELL ME MORE ABOUT SUCH FEELINGS.":DATA"DO YOU OFTEN FEEL*":DATA"DO YOU ENJOY FEELING*":DATA"DO YOU REALLY BELIEVE I DON'T*":DATA"PERHAPS IN GOOD TIME I WILL*":DATA"DO YOU WANT ME TO*":DATA"DO YOU THINK YOU SHOULD BE ABLE TO*"
81 DATA"WHY CAN'T YOU*":DATA"WHY ARE YOU INTERESTED IN WHETHER OR NOT I AM*":DATA"WOULD YOU PREFER IF I WERE NOT*":DATA"PERHAPS IN YOUR FANTASIES I AM*":DATA"HOW DO YOU KNOW YOU CAN'T*":DATA"HAVE YOU TRIED?":DATA"PERHAPS YOU CAN NOW*"
82 DATA"DID YOU COME TO ME BECAUSE YOU ARE*":DATA"HOW LONG HAVE YOU BEEN*":DATA"DO YOU BELIEVE IT IS NORMAL TO BE*":DATA"DO YOU ENJOY BEING*":DATA"WE WERE DISCUSSING YOU-- NOT ME.":DATA"OH, I*"
83 DATA"YOU'RE NOT REALLY TALKING ABOUT ME, ARE YOU?":DATA"WHAT WOULD IT MEAN TO YOU IF YOU GOT*":DATA"WHY DO YOU WANT*":DATA"SUPPOSE YOU SOON GOT*":DATA"WHAT IF YOU NEVER GOT*":DATA"I SOMETIMES ALSO WANT*":DATA"WHY DO YOU ASK?"
84 DATA"DOES THAT QUESTION INTEREST YOU?":DATA"WHAT ANSWER WOULD PLEASE YOU THE MOST?":DATA"WHAT DO YOU THINK?":DATA"ARE SUCH QUESTIONS ON YOUR MIND OFTEN?":DATA"WHAT IS IT THAT YOU REALLY WANT TO KNOW?":DATA"HAVE YOU ASKED ANYONE ELSE?"
85 DATA"HAVE YOU ASKED SUCH QUESTIONS BEFORE?":DATA"WHAT ELSE COME TO MIND WHEN YOU ASK THAT?":DATA"NAMES DON'T INTEREST ME.":DATA"I DON'T CARE ABOUT NAMES-- PLEASE GO ON.":DATA"IS THAT THE REAL REASON?"
86 DATA"DON'T ANY OTHER REASONS COME TO MIND?":DATA"DOES THAT REASON EXPLAIN ANYTHING ELSE?":DATA"WHAT OTHER REASONS MIGHT THERE BE?":DATA"PLEASE DON'T APOLOGIZE!":DATA"APOLOGIES ARE NOT NECESSARY."
87 DATA"WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE.":DATA"DON'T BE SO DEFENSIVE!":DATA"WHAT DOES THAT DREAM SUGGEST TO YOU?":DATA"DO YOU DREAM OFTEN?":DATA"WHAT PERSONS APPEAR IN YOUR DREAMS?":DATA"ARE YOU DISTURBED BY YOUR DREAMS?"
88 DATA"HOW DO YOU DO ... PLEASE STATE YOUR PROBLEM.":DATA"YOU DON'T SEEM QUITE CERTAIN.":DATA"WHY THE UNCERTAIN TONE?":DATA"CAN'T YOU BE MORE POSITIVE?":DATA"YOU AREN'T SURE?":DATA"DON'T YOU KNOW?"
89 DATA"ARE YOU SAYING NO JUST TO BE NEGATIVE?":DATA"YOU ARE BEING A BIT NEGATIVE.":DATA"WHY NOT?":DATA"ARE YOU SURE?":DATA"WHY NO?":DATA"WHY ARE YOU CONCERNED ABOUT MY*":DATA"WHAT ABOUT YOUR OWN*":DATA"CAN YOU THINK OF A SPECIFIC EXAMPLE?"
90 DATA"WHEN?":DATA"WHAT ARE YOU THINKING OF?":DATA"REALLY, ALWAYS?":DATA"DO YOU REALLY THINK SO?":DATA"BUT YOU ARE NOT SURE YOU*":DATA"DO YOU DOUBT YOU*":DATA"IN WHAT WAY?":DATA"WHAT RESEMBLANCE DO YOU SEE?"
91 DATA"WHAT DOES THE SIMILARITY SUGGEST TO YOU?":DATA"WHAT OTHER CONNECTIONS DO YOU SEE?":DATA"COULD THERE REALLY BE SOME CONNECTION?":DATA"HOW?":DATA"YOU SEEM QUITE POSITIVE.":DATA"ARE YOU SURE?":DATA"I SEE.":DATA"I UNDERSTAND."
92 DATA"WHY DO YOU BRING UP THE TOPIC OF FRIENDS?":DATA"DO YOUR FRIENDS WORRY YOU?":DATA"DO YOUR FRIENDS PICK ON YOU?":DATA"ARE YOU SURE YOU HAVE ANY FRIENDS?":DATA"DO YOU IMPOSE ON YOUR FRIENDS?"
93 DATA"PERHAPS YOUR LOVE FOR FRIENDS WORRIES YOU.":DATA"DO COMPUTERS WORRY YOU?":DATA"ARE YOU TALKING ABOUT ME IN PARTICULAR?":DATA"ARE YOU FRIGHTENED BY MACHINES?":DATA"WHY DO YOU MENTION COMPUTERS?"
94 DATA"WHAT DO YOU THINK MACHINES HAVE TO DO WITH YOUR PROBLEM?":DATA"DON'T YOU THINK COMPUTERS CAN HELP PEOPLE?":DATA"WHAT IS IT ABOUT MACHINES THAT WORRIES YOU?":DATA"SAY, DO YOU HAVE ANY PSYCHOLOGICAL PROBLEMS?"
95 DATA"WHAT DOES THAT SUGGEST TO YOU?":DATA"I SEE.":DATA"I'M NOT SURE I UNDERSTAND YOU FULLY.":DATA"COME COME ELUCIDATE YOUR THOUGHTS.":DATA"CAN YOU ELABORATE ON THAT?":DATA"THAT IS QUITE INTERESTING"
96 REM
97 REM        DATA FOR FINDING REPLIES
98 REM
99 DATA1,3,4,2,6,4,6,4,10,4,14,3,17,3,20,2,22,3,25,3:DATA28,4,28,4,32,3,35,5,40,9,40,9,40,9,40,9,40,9,40,9:DATA49,2,51,4,55,4,59,4,63,1,63,1,64,5,69,5,74,2,76,4:DATA80,3,83,7,90,3,93,6,99,7,106,6
100 END
