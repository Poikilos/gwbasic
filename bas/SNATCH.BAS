10 HISC = 2000
20 REM BEANS,CORN
30 REM SQUIRREL(14),RABBIT(41),CHIPMUNK(21),GROUNDHOG(87)
40 DIM V(3), R(4), RR(4)
50 V(1) = 100: V(2) = 100
60 R(1) = 9: R(2) = 6: R(3) = 8: R(4) = 3: RR(1) = 9: RR(2) = 6: RR(3) = 8: RR(4) = 3
65 CLS : D = 0
70 LOCATE 4, 50: PRINT "BEANS     CORN"
80 LOCATE 5, 50: PRINT "+ 0 %"
90 LOCATE 5, 60: PRINT "+ 0 %"
100 LOCATE 7, 50: PRINT "- 0 %"
110 LOCATE 7, 60: PRINT "- 0 %"
120 LOCATE 8, 50: PRINT "- 0 %"
130 LOCATE 8, 60: PRINT "- 0 %"
140 LOCATE 9, 50: PRINT "- 0 %"
150 LOCATE 9, 60: PRINT "- 0 %"
160 LOCATE 10, 50: PRINT "- 0 %"
170 LOCATE 10, 60: PRINT "- 0 %"
180 LOCATE 1, 30: PRINT " GARDEN SNATCH!"
185 IF HISC <> 2000 THEN LOCATE 1, 60: PRINT "LOW SCORE -"; HISC
190 D = D + 1: LOCATE 2, 1: PRINT "DAY"; D
200 PRINT
210 IF V(1) < 0 THEN V(1) = 0
220 IF V(2) < 0 THEN V(2) = 0
225 IF V(1) < 20 THEN COLOR 12, 0 ELSE COLOR 7, 0
230 PRINT "YOU HAVE"; V(1); "% BEANS LEFT          "
235 IF V(2) < 20 THEN COLOR 12, 0 ELSE COLOR 7, 0
240 PRINT "YOU HAVE"; V(2); "% CORN LEFT          "
245 COLOR 7, 0
250 PRINT
255 IF R(1) > 9 THEN COLOR 12, 0 ELSE COLOR 7, 0
260 IF R(1) <> 1 THEN PRINT "THERE ARE"; R(1); "SQUIRRELS PRESENT          "
270 IF R(1) = 1 THEN PRINT "THERE IS 1 SQUIRREL PRESENT          "
275 IF R(2) > 9 THEN COLOR 12, 0 ELSE COLOR 7, 0
280 IF R(2) <> 1 THEN PRINT "THERE ARE"; R(2); "RABBITS PRESENT          "
290 IF R(2) = 1 THEN PRINT "THERE IS 1 RABBIT PRESENT          "
295 IF R(3) > 9 THEN COLOR 12, 0 ELSE COLOR 7, 0
300 IF R(3) <> 1 THEN PRINT "THERE ARE"; R(3); "CHIPMUNKS PRESENT          "
310 IF R(3) = 1 THEN PRINT "THERE IS 1 CHIPMUNK PRESENT          "
315 IF R(4) > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
320 IF R(4) <> 1 THEN PRINT "THERE ARE"; R(4); "GROUNDHOGS PRESENT          "
330 IF R(4) = 1 THEN PRINT "THERE IS 1 GROUNDHOG PRESENT          "
335 COLOR 7, 0
340 PRINT
350 PRINT "(S)HOOT RODENTS, (T)RAP RODENTS, (H)ELP PLANTS"
360 IF V(1) = 0 AND V(2) = 0 THEN 1170
365 IF V(1) = 100 AND V(2) = 100 AND R(1) = 0 AND R(2) = 0 AND R(3) = 0 AND R(4) = 0 THEN 1310
370 I$ = INKEY$: IF I$ = "" THEN 370
380 IF I$ <> "S" AND I$ <> "T" AND I$ <> "H" AND I$ <> "s" AND I$ <> "t" AND I$ <> "h" THEN 370
390 IF I$ = "H" OR I$ = "h" THEN
400 LOCATE 13, 1: PRINT "HELP (B)EANS OR (C)ORN?"
410 I$ = INKEY$: IF I$ = "" THEN 410
420 IF I$ <> "B" AND I$ <> "b" AND I$ <> "C" AND I$ <> "c" THEN 410
430 LOCATE 13, 1: PRINT "                          "
440 IF I$ = "B" OR I$ = "b" THEN V(1) = INT(V(1) * 1.3): IF V(1) > 100 THEN V(1) = 100
450 IF I$ = "C" OR I$ = "c" THEN V(2) = INT(V(2) * 1.3): IF V(2) > 100 THEN V(2) = 100
460 END IF
470 IF I$ = "S" OR I$ = "s" THEN
480 LOCATE 13, 1: PRINT "SHOOT (S)QUIRREL,(R)ABBIT,(C)HIPMUNK OR (G)ROUNDHOG?"
490 I$ = INKEY$: IF I$ = "" THEN 490
500 IF I$ <> "S" AND I$ <> "R" AND I$ <> "C" AND I$ <> "G" AND I$ <> "s" AND I$ <> "r" AND I$ <> "c" AND I$ <> "g" THEN 490
510 IF I$ = "S" OR I$ = "s" THEN Z = INT((RR(1) * RND + 2) / 2)
520 IF I$ = "R" OR I$ = "r" THEN Z = INT((RR(2) * RND + 2) / 2)
530 IF I$ = "C" OR I$ = "c" THEN Z = INT((RR(3) * RND + 2) / 5)
540 IF I$ = "G" OR I$ = "g" THEN Z = INT((RR(4) * RND + 2) / 4)
550 IF I$ = "S" OR I$ = "s" THEN LOCATE 15, 1: PRINT "YOU HAVE"; Z; "LESS SQUIRRELS TO WORRY ABOUT": RR(1) = RR(1) - Z: IF RR(1) < 0 THEN RR(1) = 0
560 IF I$ = "R" OR I$ = "r" THEN LOCATE 15, 1: PRINT "YOU HAVE"; Z; "LESS RABBITS TO WORRY ABOUT": RR(2) = RR(2) - Z: IF RR(2) < 0 THEN RR(2) = 0
570 IF I$ = "C" OR I$ = "c" THEN LOCATE 15, 1: PRINT "YOU HAVE"; Z; "LESS CHIPMUNKS TO WORRY ABOUT": RR(3) = RR(3) - Z: IF RR(3) < 0 THEN RR(3) = 0
580 IF I$ = "G" OR I$ = "g" THEN LOCATE 15, 1: PRINT "YOU HAVE"; Z; "LESS GROUNDHOGS TO WORRY ABOUT": RR(4) = RR(4) - Z: IF RR(4) < 0 THEN RR(4) = 0
590 FOR ZZ = 1 TO Z
600 FOR ZZZ = 600 TO 100 STEP -20
610 SOUND ZZZ, .1
620 NEXT ZZZ
630 NEXT ZZ
640 LOCATE 13, 1: PRINT "                                                         "
650 LOCATE 15, 1: PRINT "                                                  "
660 END IF
670 IF I$ = "T" OR I$ = "t" THEN
680 LOCATE 13, 1: PRINT "TRAP (S)QUIRREL,(R)ABBIT,(C)HIPMUNK OR (G)ROUNDHOG?"
690 I$ = INKEY$: IF I$ = "" THEN 690
700 IF I$ <> "S" AND I$ <> "R" AND I$ <> "C" AND I$ <> "G" AND I$ <> "s" AND I$ <> "r" AND I$ <> "c" AND I$ <> "g" THEN 690
710 IF I$ = "S" OR I$ = "s" THEN Z = INT((RR(1) * RND + 2) / 3)
720 IF I$ = "R" OR I$ = "r" THEN Z = INT((RR(2) * RND + 2) / 5)
730 IF I$ = "C" OR I$ = "c" THEN Z = INT((RR(3) * RND + 2) / 2)
740 IF I$ = "G" OR I$ = "g" THEN Z = INT((RR(4) * RND + 2) / 3)
750 IF I$ = "S" OR I$ = "s" THEN LOCATE 15, 1: PRINT "YOU CAUGHT"; Z; "SQUIRRELS": RR(1) = RR(1) - Z: IF RR(1) < 0 THEN RR(1) = 0
760 IF I$ = "R" OR I$ = "r" THEN LOCATE 15, 1: PRINT "YOU CAUGHT"; Z; "RABBITS": RR(2) = RR(2) - Z: IF RR(2) < 0 THEN RR(2) = 0
770 IF I$ = "C" OR I$ = "c" THEN LOCATE 15, 1: PRINT "YOU CAUGHT"; Z; "CHIPMUNKS": RR(3) = RR(3) - Z: IF RR(3) < 0 THEN RR(3) = 0
780 IF I$ = "G" OR I$ = "g" THEN LOCATE 15, 1: PRINT "YOU CAUGHT"; Z; "GROUNDHOGS": RR(4) = RR(4) - Z: IF RR(4) < 0 THEN RR(4) = 0
790 FOR ZZ = 1 TO Z
800 FOR ZZZ = 600 TO 100 STEP -20
810 SOUND ZZZ, .1
820 NEXT ZZZ
830 NEXT ZZ
840 LOCATE 13, 1: PRINT "                                                         "
850 LOCATE 15, 1: PRINT "                                                  "
860 END IF
870 FOR Z = 1 TO 4
880 RR(Z) = RR(Z) * 1.05
890 IF INT(RR(Z)) <> R(Z) THEN R(Z) = INT(RR(Z))
900 NEXT Z
910 S1 = INT(1 / 4 * R(1) * RND)
920 S2 = INT(4 / 4 * R(1) * RND)
930 R1 = INT(4 / 4 * R(2) * RND)
940 R2 = INT(1 / 4 * R(2) * RND)
950 C1 = INT(2 / 4 * R(3) * RND)
960 C2 = INT(1 / 4 * R(3) * RND)
970 G1 = INT(8 / 4 * R(4) * RND)
980 G2 = INT(7 / 4 * R(4) * RND)
990 V(1) = V(1) - (S1 + R1 + C1 + G1)
1000 V(2) = V(2) - (S2 + R2 + C2 + G2)
1010 LOCATE 4, 50: PRINT "BEANS     CORN"
1015 IF S1 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1020 LOCATE 7, 50: PRINT "-"; S1; "% "
1025 IF S2 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1030 LOCATE 7, 60: PRINT "-"; S2; "% "
1035 IF R1 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1040 LOCATE 8, 50: PRINT "-"; R1; "% "
1045 IF R2 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1050 LOCATE 8, 60: PRINT "-"; R2; "% "
1055 IF C1 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1060 LOCATE 9, 50: PRINT "-"; C1; "% "
1065 IF C2 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1070 LOCATE 9, 60: PRINT "-"; C2; "% "
1075 IF G1 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1080 LOCATE 10, 50: PRINT "-"; G1; "% "
1085 IF G2 > 3 THEN COLOR 12, 0 ELSE COLOR 7, 0
1090 LOCATE 10, 60: PRINT "-"; G2; "% "
1095 COLOR 7, 0
1100 V1 = INT(3 * RND)
1110 V2 = INT(3 * RND)
1120 V(1) = V(1) + V1: IF V(1) > 100 THEN V(1) = 100
1130 V(2) = V(2) + V2: IF V(2) > 100 THEN V(2) = 100
1140 LOCATE 5, 50: PRINT "+"; V1; "% "
1150 LOCATE 5, 60: PRINT "+"; V2; "% "
1160 GOTO 180
1170 LOCATE 15, 1: PRINT "YOU STARVED TO DEATH"
1180 FOR Z = 1000 TO 100 STEP -10
1190 SOUND Z, .1
1200 NEXT Z
1210 FOR Z = 40 TO 1 STEP -1
1220 SOUND INT(60 * RND) + 60 + Z, .2
1230 NEXT Z
1240 PRINT
1250 PRINT "DO YOU WANT TO PLAY AGAIN?"
1260 I$ = INKEY$: IF I$ = "" THEN 1260
1270 IF I$ <> "Y" AND I$ <> "N" AND I$ <> "y" AND I$ <> "n" THEN 1260
1280 IF I$ = "Y" OR I$ = "y" THEN 50
1290 IF I$ = "N" OR I$ = "n" THEN END
1300 END
1310 LOCATE 15, 1: PRINT "YOU LIVE HAPPILY EVER AFTER WITH YOUR NICE GARDEN": PRINT
1320 SOUND 523, 5: SOUND 30000, 2: SOUND 523, 5: SOUND 30000, 2: SOUND 523, 5
1325 IF D < HISC THEN
1326 HISC = D
1327 PLAY "<<<L16GL8AL16GL8A>C>>"
1328 LOCATE 17, 1: PRINT "IT'S A NEW LOW SCORE!": PRINT
1329 END IF
1330 GOTO 1250

