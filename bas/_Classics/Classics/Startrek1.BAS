10 CLS
20 PRINT@210,"S U P E R  S T A R  T R E K"
30 PRINT:PRINT
100 CLEAR 600
110 Z$="                         "
120 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8):RANDOM
130 T=INT(RND(0)*20+20)*100:T0=T:T9=25+INT(RND(0)*10):D0=0:E=3000:E0=E
140 P=10:PO=P:S9=200:S=0:B9=0:K9=0:X$="":X0$=" IS "
170 Q1=RND(8):Q2=RND(8):S1=RND(8):S2=RND(8)
180 FORI=1TO9:C(I,1)=0:C(I,2)=0:NEXTI
190 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
200 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
210 FORI=1TO8:D(I)=0:NEXTI
220 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
230 FORI=1TO8:FORJ=1TO8:K3=0:Z(I,J)=0:R1=RND(0)
240 IFR1>.98THENK3=3:K9=K9+3:GOTO270
250 IFR1>.95THENK3=2:K9=K9+2:GOTO270
260 IFR1>.8THENK3=1:K9=K9+1
270 B3=0:IFRND(0)>.96THENB3=1:B9=B9+1
280 G(I,J)=K3*100+B3*10+RND(8):NEXTJ:NEXTI:IFK9>T9THENT9=K9+1
290 IFB9<>0THEN320
300 IF G(Q1,Q2)<200THENG(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1
310 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=RND(8):Q2=RND(8)
320 K7=K9:IFB9<>1THENX$="S":X0$=" ARE "
330 PRINT"YOUR ORDERS ARE AS FOLLOWES:"
340 PRINT"   DESTROY THE";K9;"KLINGON WARSHIPS WHICH HAVE INVADED"
350 PRINT"   THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
360 PRINT"   ON STARDATE";T0+T9;" THIS GIVES YOU";T9;"DAYS. THERE";X0$
370 PRINT"  ";B9;"STARBASE";X$" IN THE GALAXY FOR RESUPLYING YOUR SHIP."
380 PRINT:INPUT"ENTER YOUR NAME IN THE CAPTAIN'S LOG";NA$
400 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(0):Z(Q1,Q2)=G(Q1,Q2)
410 IFQ1<1ORQ1>8ORQ2<1ORQ2>8THEN510
450 REM
460 K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3
470 S3=G(Q1,Q2)-100*K3-10*B3:IFK3=0THEN500
480 CLS:PRINT@396,"COMBAT AREA      CONDITION RED":IFS>200THEN500
490 PRINT@463,"SHIELDS DANGEROUSLY LOW"
500 FORI=1TO3:K(I,1)=0:K(I,2)=0:NEXTI
510 FORI=1TO3:K(I,3)=0:NEXTI:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
520 A$="<*>":Z1=S1:Z2=S2:GOSUB3320:IFK3<1THEN550
530 FORI=1TOK3:GOSUB3300:A$="+K+":Z1=R1:Z2=R2
540 GOSUB3320:K(I,1)=R1:K(I,1)=R2:K(I,3)=S9*(.5+RND(0)):NEXTI
550 IFB3<1THEN570
560 GOSUB3300:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB3320
570 FORI=1TOS3:GOSUB3300:A$=" * ":Z1=R1:Z2=R2:GOSUB3320:NEXTI
580 GOSUB2460
590 IFS+E>10THENIFE>10ORD(7)=0THEN640
600 CLS:PRINT@320,"CAPTAIN ";NA$;"!  YOU'VE JUST STRANDED YOUR SHIP IN SPACE!"
610 PRINT"YOU HAVE INSUFFICIENT MANUVERING ENERGY,";
620 PRINT" AND SHIELD CONTROL.":PRINT"IS PRESENTLY INCAPABLE OF CROSS";
630 PRINT"-CIRUITING TO ENGINE ROOM!!":GOTO2330
640 PRINT"YOUR ORDERS, CAPTAIN ";NA$;:INPUTA$
650 FORI=1TO9:IFLEFT$(A$,3)<>MID$(A1$,3*I-2,3)THEN670
660 ONIGOTO770,580,1410,1490,1680,1990,2070,2680,2360
670 NEXTI:CLS:PRINT"ENTER ONE OF THE FOLOWING:"
680 PRINT" NAV  (TO SET COURSE)"
690 PRINT" SRS  (FOR SHORT RANGE SENSOR SCAN)"
700 PRINT" LRS  (FOR LONG RANGE SENSOR SCAN)"
710 PRINT" PHA  (TO FIRE PHASERS)"
720 PRINT" TOR  (TO FIRE PHOTON TORPEDOES)"
730 PRINT" SHE  (TO RAISE OR LOWER SHIELDS)"
740 PRINT" DAM  (FOR DAMAGE CONTROAL REPORTS)"
750 PRINT" COM  (TO CALL ON LIBRARY-COMPUTER)"
760 PRINT" XXX  (TO RESIGN YOUR COMAND)":PRINT:GOTO590
770 INPUT"COURSE (0-9)";C1:IFC1=9THENC1=1
780 IFC1>=1ANDC1<9THEN800
790 PRINT"  LT. SULU REPORTS, 'INCORECT COURSE DATA, SIR!'":GOTO590
800 X$="8":IFD(1)<0THENX$="0.2"
810 PRINT"WARP FACTOR (0-";X$;")";:INPUTW1:IFD(1)<0ANDW1>.2THEN860
820 IFW1>0ANDW1<=8THEN870
830 IFW1=0THEN590
840 PRINT"  CHIEF ENGINEER SCOTT REPORTS 'THE ENGINES WON'T TAKEWARP";W1;"!":GOTO590
860 PRINT"WARP ENGINES ARE DAMMAGED.  MAXIMUM SPEED=WARP 0.2":GOTO590
870 N=INT(W1*8+.5):IFE-N>=0THEN940
880 PRINT"ENGINEERING REPORTS  'INSUFFICIENT ENERGY AVAILABLE"
890 PRINTTAB(23)"FOR MANUVERING AT WARP";W1;"!"
900 IFS<N-EORD(7)<0THEN590
910 PRINT"DEFLECTOR CONTROL ROOM ACKNOWLEDGES";S;"UNITS OF ENERGY"
920 PRINTTAB(23)"PRESENTLY DEPLOYED TO SHIELDS."
930 GOTO590
940 FORI=1TOK3:IFK(I,3)=0THEN970
950 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB3320:GOSUB3300
960 K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB3320
970 NEXT I:GOSUB2220:D1=0:D6=W1:IFW1>=1THEND6=1
980 FORI=1TO8:IFD(I)>=0THEN1030
990 D(I)=D(I)+D6:IFD(I)>-.1ANDD(I)<0THEND(I)=-.1:GOTO1030
1000 IFD(I)<0THEN1030
1010 IFD1<>1THEND1=1:PRINT"DAMAGE CONTROL REPORT"
1020 PRINTTAB(8);:R1=I:GOSUB3370:PRINTG2$;" REPAIR COMPLEATED"
1030 NEXTI:IFRND(0)>.2THEN1090
1040 R1=RND(8):IFRND(0)>=6THEN1070
1050 Q(R1)=D(R1)-(RND(0)*5+1):PRINT"DAMAGE CONTROL REPORT:";
1060 GOSUB3370:PRINTG2$;" DAMAGED":PRINT:GOTO1090
1070 D(R1)=D(R1)+RND(0)*3+1:PRINT"DAMAGE CONTROL REPORT:"
1080 GOSUB3370:PRINTG2$;" STATE OF REPAIR IMPROVED":PRINT
1090 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB3320
1100 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
1110 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
1120 FORI=1TON:S1=S1+X1:S2=S2+X2:IFS1<1ORS1>=9ORS2<1ORS2>=9THEN1210
1130 S8=INT(S1)*24+INT(S2)*3-26:IFMID$(Q$,S8,2)="  "THEN1160
1140 S1=INT(S1-X1):S2=INT(S2-X2):PRINT"WARP ENGINES SHUT DOWN AT ";
1150 PRINT"SECTOR";S1;",";S2;"DUE TO BAD NAVIGATION.":GOTO1170
1160 NEXTI:S1=INT(S1):S2=INT(S2)
1170 A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB3320:GOSUB1370:T8=1
1180 IFW1<1THENT8=.1*INT(10*W1)
1190 T=T+T8:IFT>T0+T9THEN2330
1200 GOTO580
1210 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
1220 S2=INT(Y-Q2*8):IFS1=0THENQ1=Q1-1:S1=8
1230 IFS2=0THENQ2=Q2-1:S2=8
1240 X5=0:IFQ1<1THENX5=1:Q1=1:S1=1
1250 IFQ1>8THENX5=1:Q1=8:S1=8
1260 IFQ2<1THENX5=1:Q2=1:S2=1
1270 IFQ2>8THENX5=1:Q2=8:S2=8
1280 IFX5=0THEN1350
1290 PRINT"LT. UHURA REPORTS MESSAGE FROM STARFLEET COMAND:"
1300 PRINT"  'PREMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMITER"
1310 PRINT"  IS HEREBY *DENIED*. SHUT DOWN YOUR ENGINES.'"
1320 PRINT"CHIEF ENGINEER SCOTT REPORTS  'WARP ENGINES SHUT DOWN"
1330 PRINT"AT SECTOR";S1;",";S2;"OF QUADRENT";Q1;",";Q2;"'"
1340 IFT>T0+T9THEN2330
1350 IF8*Q1+Q2=8*Q4+Q5THEN1170
1360 T=T+1:GOSUB1370:GOTO400
1370 E=E-N-10:IFE>=0THENRETURN
1380 PRINT"SHIELD CONTROL SUPPLIES ENERGY TO COMPLEAT MANUVER."
1390 S=S+E:E=0:IFS<=0THENS=0
1400 RETURN
1410 IFD(3)<0THENPRINT"LONG RANG SENSORES ARE INOPERABLE":GOTO590
1420 CLS:PRINT"LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
1430 O1$="--------------------":PRINTO1$
1440 FORI=Q1-1TOQ1+1:N(1)=-1:N(2)=-1:N(3)=-3:FORJ=Q2-1TOQ2+1
1450 IFI>0ANDI<9ANDJ>0ANDJ<9THENN(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
1460 NEXTJ:FORL=1TO3:PRINT": ";:IFN(L)<0THENPRINT"*** ";:GOTO1480
1470 PRINTRIGHT$(STR$(N(L)+1000),3);" ";
1480 NEXTL:PRINT":":PRINTO1$:NEXTI:GOTO590
1490 CLS:PRINT:IFD(4)<0THENPRINT"PHASERS INOPERATIVE.":GOTO590
1500 IFK3>0THEN1530
1510 PRINT"SCIENCE OFFICER SPOCK REPORTS  'SENSORES SHOW NO ENEMY SHIPS"
1520 PRINTTAB(32)"IN THIS QUADRENT'":GOTO590
1530 IFD(8)<0THENPRINT"COMPUTER FAILER HAMPERS ACCURACY"
1540 PRINT"PHASERS LOCKED ON TARGET;  ";
1550 PRINT"ENERGY AVAILABLE=";E;"UNITS"
1560 INPUT"NUMBER OF UNITS TO DIVERT TO PHASER BANKS";X:IFX<=0THEN590
1570 IFE-X<=0THEN1550
1580 E=E-X:IFD(7)<0THENX=X*RND(0)
1590 H1=INT(X/K3):FORI=1TO3:IFK(I,3)<=0THEN1670
1600 JH=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
1604 H=INT((H1/JH)*(RND(0)+2)):IFH>.15*K(I,3)THEN1620
1610 PRINT"SENSORS SHOW NO DAMAGE TO ENEMY AT ";K(I,1);",";K(I,2):GOTO1670
1620 K(I,3)=K(I,3)-H:PRINTH;:PRINT"UNITS HIT ON KLINGON AT SECTOR";K(I,1);",";
1630 PRINTK(I,2):IFK(I,3)<=0THENPRINT"*** KLINGON DESTROYED ***":GOTO1650
1640 PRINT"  (SENSORS SHOW";K(I,3);"UNITS REMAINING)":GOTO1670
1650 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB3320
1660 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IFK9<=0THEN2430
1670 NEXTI:GOSUB2220:GOTO590
1680 REM
1685 IFP<0PRINT"ALL PHOTON TORPEDOES EXPENDED.":GOTO590
1690 IFD(5)<0THENPRINT"PHOTON TUBES ARE NOT OPERATIONAL":GOTO590
1700 INPUT"PHOTON TORPEDO COURSE (1-9)";C1:IFC1=9THENC1
1710 IFC1>=1ANDC1<9THEN1740
1720 PRINT"ENSIGN CHEKOV REPORTS,  'INCORRECT COURSE DATA, SIR!'"
1730 GOTO590
1740 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
1750 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
1770 X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
1780 IFX3<1ORX3>8ORY3<1ORY3>8THEN1980
1785 PRINT@832,STRING$(63," ")
1790 PRINT@832,"TRACKING TORPEDO:";X3;",";Y3;:A$="   ":Z1=X:Z2=Y:GOSUB3460
1800 IFZ3<>0THEN1770
1805 PRINT
1810 A$="+K+":Z1=X:Z2=Y:GOSUB3460:IFZ3=0THEN1860
1820 PRINT"*** KLINGON DESTROYED ***":K3=K3-1:K9=K9-1:IFK9<=0THEN2430
1830 FORI=1TO3:IFX3=K(I,1)ANDY3=K(I,2)THEN1850
1840 NEXTI:I=3
1850 K(I,3)=0:GOTO1960
1860 A$=" * ":Z1=X:Z2=Y:GOSUB3460:IFZ3=0THEN1880
1870 PRINT"STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY.":GOSUB2220:GOTO590
1880 A$=">!<":Z1=X:Z2=Y:GOSUB3460:IFZ3=0THEN1700
1890 PRINT"*** STARBASE DESTROYED ***":B3=B3-1:B9=B9-1
1900 IFB9>0ORK9>T-10-T9THEN1940
1909 NA$="WALDO"
1910 PRINT"CAPTAIN ";NA$;" YOU ARE HEREBY RELIEVED OF COMMAND"
1920 PRINT"AND SENTENCED TO 99 STARDATES OF HARD LABOR ON CYGNUS 12!!"
1925 FORII=1TO2500:NEXT
1930 GOTO2360
1940 PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
1950 PRINT"COURT MARTIAL!":D=0
1960 Z1=X:Z2=Y:A$="   ":GOSUB3320
1970 G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB2220:GOTO590
1980 PRINT:PRINT"TORPEDO MISSED.":GOSUB2220:GOTO590
1990 IFD(7)<0THENPRINT"SHIELD CONTROL INOPERABLE.":GOTO590
2000 PRINT"ENERGY ADVAILABLE=";E+S;:INPUT"NUMBER OF UNITS TO SHIELDS";X
2010 IFX<0ORS=XTHENPRINT"<SHIELDS REMAIN UNCHANGED>":GOTO590
2020 IFX<=E+STHEN2050
2030 PRINT"CAPTAIN "NA$"!  THIS IS NOT THE FEDERATION TREASURY."
2040 PRINT"<SHIELDS UNCHANGED>":GOTO590
2050 E=E+S-X:S=X:PRINT"DEFLECTOR CONTROL ROOM REPORT:"
2060 PRINT"  'SHIELDS NOW AT";INT(S);"UNITS PER UOUR COMAND.'"
2065 FORII=1TO1500:NEXT:CLS:GOSUB2460:GOTO590
2070 IFD(6)>=0THEN2180
2080 PRINT"DAMMAGE CONTROL REPORT NOT AVAILABLE":IFD=0THEN590
2090 D3=0:FOTI=1TO8:IFD(I)<0THEND3=D3+.1
2100 NEXTI:IFD3=0THEN590
2110 PRINT:D3=D3+D4:IFD3>=1THEND3=.9
2120 PRINT"TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP"
2130 PRINT"ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES"
2140 INPUT"WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)";A$
2150 IFA$<>YTHEN590
2160 FORI=1TO8:IHD(I)<0THEND(I)=0
2170 NEXTI:T=T+D3+.1
2180 CLS:PRINT:PRINT"DEVICE             STATE OF REPAIR":FORR1=1TO8
2190 GOSUB3370:PRINTG2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
2200 NEXTR1:PRINT:IFD0<>0THEN2090
2210 GOTO590
2220 IFK3<=0THENRETURN
2230 IFD0<>0THENPRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE":RETURN
2240 FORI=1TO3:IFK(I,3)<=0THEN2320
2245 JH=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
2250 H=INT((K(I,3)/JH)*(2+RND(0))):S=S-H:K(I,3)=K(I,3)/(3+RND(0))
2260 PRINTH;"UNITS HIT ON ENTERPRISE FROM SECTOR";K(I,1);",";K(I,2)
2270 IFS<=0THEN2340
2280 PRINT"      <SHIELDS DOWN TO";S;"UNITS>":IFH<20THEN2320
2290 IFRND(0)>.6ORH/S<=.02THEN2320
2300 R1=RND(8):D(R1)=D(R1)-H/S-.5*RND(0):GOSUB3370
2310 PRINT"DAMAGE CONTROL REPORTS '";G2$;" DAMAGED BY THE HIT'"
2320 NEXTI:RETURN
2330 PRINT"IT IS STARDATE";T:FORI=1TO1500:NEXTI:GOTO2360
2340 FORI=1TO500:NEXTI:CLS:PRINT:PRINT"THE ENTERPRISE HAS BEEN DESTROYED.":PRINT"THE FEDERATION";
2350 PRINT" WILL BE CONQUERED":GOTO2330
2360 CLS:PRINT"THERE WERE";K9;"KLINGON BATTLE CRUISERS LEFT AT"
2370 PRINT"THE END OF YOUR MISSION."
2380 PRINT:PRINT:IFB9=0THEN2420
2390 PRINT"THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
2400 PRINT"FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
2410 INPUT"LET HIM STEP FORWARD AND ENTER 'AYE'";A$:IFA$="AYE"THEN10
2420 END
2425 T=100:B9=1
2430 FORII=1TO1500:NEXT:CLS
2435 PRINT@192,"CONGRATIONS, MON CAPITAN "NA$"!  THE LAST KLINGON BATTLE"
2440 PRINTTAB(5)"CRUISER MENACING THE FEDERATION HAS BEEN DESTROYED!":PRINT
2450 PRINTTAB(10)"YOUR EFFICIENCY RATING IS";1000*(K7/(T-T0))^2:GOTO2380
2460 FORI=S1-1TOS1+1:FORJ=S2-1TOS2+1
2470 IFINT(I+.5)<1ORINT(I+.5)>8ORINT(J+.5)<1ORINT(J+.5)>8THEN2490
2480 A$=">!<":Z1=I:Z2=J:GOSUB3460:IFZ3=1THEN2500
2490 NEXTJ:NEXTI:D0=0:GOTO2520
2500 D0=1:C$="DOCKED":E=E0:P=P0
2510 PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES":FORIX=1TO1500:NEXTIX:S=0:GOTO2540
2520 IFK3>0THENC$="*RED*":GOTO2540
2530 C$="GREEN":IFE<E0*.1THENC$="YELLOW"
2540 IFD(2)>=0THEN2560
2550 IF(PEEK(16000)<>32)OR(PEEK(16064)<>32)THENCLS:PRINT
2555 PRINT"*** SHORT RANGE SENSORS ARE OUT ***":PRINT:RETURN
2560 CLS:O1$="---------------------------------":PRINTO1$:FORI=1TO8
2570 FORJ=(I-1)*24+1TO(I-1)*24+22STEP3:PRINT" ";MID$(Q$,J,3);:NEXTJ
2580 ONIGOTO2590,2600,2610,2620,2630,2640,2650,2660
2590 PRINT"   STARDATE           ";INT(T*10)*.1:GOTO2670
2600 PRINT"   CONDITION          ";C$:GOTO2670
2610 PRINT"   QUADRANT           ";Q1;",";Q2:GOTO2670
2620 PRINT"   SECTOR             ";S1;",";S2:GOTO2670
2630 PRINT"   PHOTON TORPEDOES   ";INT(P):GOTO2670
2640 PRINT"   TOTAL ENERGY        ";INT(E+S):GOTO2670
2650 PRINT"   SHIELDS             ";INT(S):GOTO2670
2660 PRINT"   KLINGONS REMAINING  ";INT(K9)
2670 NEXTI:PRINTO1$:RETURN
2680 IFD(8)<0THENPRINT"COMPUTER DISABLED":GOTO590
2690 INPUT"COMPUTER ACTIVE AND AWAITING A COMAND";A:IFA<0ORA>4THEN2710
2700 H8=1:ONA+1GOTO2810,2930,3010,3270,3070
2710 CLS:PRINT"FUNCTIONS AVAILABLE FROM LIBARY-COMPUTER."
2720 PRINT"   0=CUMULITIVE GALACTIVE RECORD"
2730 PRINT"   1=STATUS REPORT"
2740 PRINT"   2=PHOTON TORPEDO DATA"
2750 PRINT"   3=STARBASE NAV DATA"
2760 PRINT"   4=DIRECTION/DISTANCE CALCULATOR"
2770 PRINT:GOTO2690
2780 H8=0:G5=1:CLS:PRINTTAB(24)"THE GALAXY":GOTO2840
2810 PRINT:PRINT"        ";
2820 CLS:PRINT"COMPUTER RECORD OF GALAXY FOR QUADRENT";Q1;",";Q2
2830 PRINT
2840 PRINT"        1     2      3      4      5      6      7      8"
2850 O1$="      ----- ----- ------ ------ ------ ------ ----- ------"
2860 PRINTO1$:FORI=1TO8:PRINT1;:IFH8=0THEN2900
2870 FORJ=1TO8:PRINT"   ";:IFZ(I,J)=0THENPRINT"***";:GOTO2890
2880 PRINTRIGHT$(STR$(Z(I,J)+1000),3);
2890 NEXTJ:GOTO2920
2900 Z4=I:Z5=1:GOSUB3490:J0=INT(15-.5*LEN(G2$)):PRINTTAB(J0);G2$;
2910 Z5=5:GOSUB 3490:J0=INT(39-.5*LEN/(G2$)):PRINTTAB(J0);G2$;
2920 PRINT:NEXTI:PRINT:GOTO590
2930 CLS:PRINT"    STSTAS REPORT:":X$="":IFK9>1THENX$="S"
2940 PRINT"KLINGON";X$;" LEFT: ";K9
2950 PRINT"MISSION MUST BE COMPLEATED IN";.1*INT((T0+T9-T)*10),"STARDATES"
2960 X$="S":IFB9<2THENX$="":IFB9<1THEN2990
2970 PRINT"THE FEDERATION IS MAINTAINING";B9;"STARBASE";X$;" IN THE GALAXY"
2975 FORIX=1TO1500:NEXTIX
2980 GOTO2070
2990 PRINT"CAPTAIN "NA$"!  YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN"
3000 PRINT"IN THE GALAXY--YOU HAVE NO STARBASES LEFT!"
3005 FORI=1TO2000:NEXT:GOTO2070
3010 IFK3<=0THEN1510
3020 X$="":IFK3>1THENX$="S"
3030 PRINT"FROM ENTERPRISE TO KLINGON";X$
3040 H8=0:FORI=1TO3:IFK(I,3)<=0THEN3260
3050 W1=K(I,1):X=K(I,2)
3060 C1=S1:A=S2:GOTO3110
3070 CLS:PRINT"DIRECTION/DISTANCE CALCULATER:"
3080 PRINT"YOU ARE AT QUADRENT ";Q1;",";Q2;" SECTOR ";S1;",";S2
3090 PRINT"PLEASE ENTER":C1=Q1:A=Q2
3100 INPUT"  DESTINATION COORDINATES (X,Y)";W1,X
3110 X=X-A:A=C1-W1:IFX<0THEN3190
3120 IFA<0THEN3210
3130 IFX>0THEN3150
3140 IFA=0THENC1=5:GOTO3160
3150 C1=1
3160 IFABS(A)<=ABS(X)THEN3180
3170 PRINT"DIRECTION=";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A));:GOTO3250
3180 PRINT"DIRECTION=";C1+(ABS(A)/(ABS(X)+.001));:GOTO3250
3190 IFA>0THENC1=3:GOTO3220
3200 IFX<>0THENC1=5:GOTO3160
3210 C1=7
3220 IFABS(A)>=ABS(X)THEN3240
3230 PRINT"DIRECTION=";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X));:GOTO3250
3240 PRINT"DIRECTION=";C1+(ABS(X)/ABS(A));
3250 PRINT"DISTANCE=";
3252 IFW1=B4PRINTSQR(X^2+A^2)/10:GOTO3254
3253 PRINTSQR(X^2+A^2)
3254 IFH8=1THEN590
3260 NEXTI:GOTO590
3270 IFB3<>0THENPRINT"FROM ENTERPRISE TO STAREBASE:":W1=B4:X=B5:GOTO3060
3280 PRINT"MR  SPOCK REPORTS, 'SENSORS SHOW NO STARBASES IN THIS";
3290 PRINT" QUADRANT '":GOTO590
3300 R1=RND(8):R2=RND(8):A$="   ":Z1=R1:Z2=R2:GOSUB3460:IFZ3=0THEN3300
3310 RETURN
3320 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
3330 IFLEN(A$)<>3THENPRINT"ERROR":STOP
3340 IFS8=1THENQ$=A$+RIGHT$(Q$,189):RETURN
3350 IFS8=190THENQ$=LEFT$(Q$,189)+A$:RETURN
3360 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN
3370 ONR1GOTO3380,3390,3400,3410,3420,3430,3440,3450
3380 G2$="WARP ENGINES":RETURN
3390 G2$="SHORT RANGE SCANNERS":RETURN
3400 G2$="LONG RANGE SCANNERS":RETURN
3410 G2$="PHASER CONTROL":RETURN
3420 G2$="PHOTON TUBES":RETURN
3430 G2$="DAMMAGE CONTROL":RETURN
3440 G2$="SHIELD CONTROL":RETURN
3450 G2$="LIBRARY-COMPUTER":RETURN
3460 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
3470 IFMID$(Q$,S8,3)<>A$THENRETURN
3480 Z3=1:RETURN
