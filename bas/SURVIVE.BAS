10 RANDOMIZE TIMER
20 GOSUB 2850
25 GOSUB 30
26 GOTO 260
30 CLS
40 PRINT "       THE OBJECT OF THIS GAME IS TO ESCAPE YOUR ISLAND ALIVE! YOU,"
50 PRINT "KARANA, HAVE TO GET A CANOE TO ESCAPE, AND SURVIVE AT THE SAME TIME."
60 PRINT "IF YOU'RE TRYING TO KILL AN OCTOPUS WITH NO WEAPONS, IT MIGHT EAT YOU!"
70 PRINT "EVERY SO OFTEN, YOU MUST ALSO GET WATER FROM THE SPRING. IT"
80 PRINT "IS EASIER TO FIND FOOD IF YOU HAVE MORE WEAPONS. ALEUTS, THE ENEMY,"
90 PRINT "CAN KILL YOU, BUT SOMETIMES THEY LEAVE BEHIND THINGS THAT ARE"
100 PRINT "IMPOSSIBLE FOR YOU TO MAKE ON THE ISLAND. YOU MUST AVOID THE WILD"
110 PRINT "DOGS. THEY CAN KILL YOU! YOU HAVE TO LEAVE AT THE DOCK, AND YOU NEED"
120 PRINT "A CANOE, FOOD, AND WATER. TO GET A CANOE, YOU HAVE TO CHOP DOWN TREES"
130 PRINT "WITH YOUR AX. YOU HAVE TO FIGURE THE REST OUT YOURSELF!!!"
140 PRINT
150 PRINT "  SSSSSSS  U     U  RRRRRR  V        V   IIII V        V  EEEEEEEEE"
160 PRINT " SSS       U     U  R     R  V      V     II   V      V   EE"
170 PRINT " SS        U     U  R     R   V    V      II    V    V    EE"
180 PRINT " SSSSSSSS  U     U  RRRRRRR    V  V       II     V  V     EEEEEEEE"
190 PRINT "      SSS  U     U  R    R     V  V       II     V  V     EE"
200 PRINT "       SS  U     U  R     R     VV        II      VV      EE"
210 PRINT " SSSSSSSS   UUUUU   R     R     VV       IIII     VV      EEEEEEEEE"
220 PRINT
230  PRINT "E-UP  S-LEFT  D-RIGHT  X-DOWN  F-FIRE(GUN)  G-GRENADE  T-TAKE  P-FAST/SLOW"
235 PRINT "                              H-HELP"
250 PRINT "    G O O D                                          L U C K ! !"
255 RETURN
260 DIM B(15, 15)
270 DIM C$(26)
280 H = 2
290 C$(1) = "WILD DOGS      "
300 C$(2) = "DITCH          "
310 C$(3) = "VALLEY         "
320 C$(4) = "CAVE           "
330 C$(5) = "GRASS FIELD    "
340 C$(6) = "TREE           "
350 C$(7) = "ROCK           "
360 C$(8) = "SPEAR          "
370 C$(9) = "BOW            "
380 C$(10) = "ARROW          "
390 C$(11) = "CAN FOR WATER  "
400 C$(12) = "OCTOPUS        "
410 C$(13) = "FISH           "
420 C$(14) = "CANOE          "
430 C$(15) = "ALEUT          "
440 C$(16) = "SPRING         "
450 C$(17) = "GUN&BULLETS    "
460 C$(18) = "GRENADES       "
470 C$(19) = "WEAPONS CASE!! "
480 C$(20) = "DEAD WILD DOGS "
490 C$(21) = "DEAD OCTOPUS   "
500 C$(22) = "DEAD FISH      "
510 C$(23) = "DEAD ALEUT     "
520 C$(24) = "AX             "
530 C$(25) = "STUMP          "
540 C$(26) = "DOCK           "
550 B(8, 15) = 26
560 I = 0: II = 0
570 DIM D(25)
580 A = 15
590 D(1) = 2: D(2) = A: D(3) = A: D(4) = A
600 D(5) = 0: D(6) = 20: D(7) = A: D(8) = A
610 D(9) = A: D(10) = A: D(11) = A: D(12) = 8
620 D(13) = A: D(14) = 1: D(15) = 0: D(16) = 10: D(19) = 1: D(24) = 2
630 FOR AB = 1 TO 18
640 A = AB
650 IF AB = 17 THEN A = 19
660 IF AB = 18 THEN A = 24
670 FOR AA = 1 TO D(A)
680 AAA = INT(15 * RND) + 1
690 AAAA = INT(15 * RND) + 1
700 IF B(AAA, AAAA) <> 0 THEN 680
710 B(AAA, AAAA) = A
720 NEXT AA
730 NEXT AB
740 FOR A = 1 TO 15
750 FOR AA = 1 TO 15
760 IF B(A, AA) = 0 THEN B(A, AA) = 5
770 NEXT AA
780 NEXT A
790 PRINT "READY?"
800 K$ = INKEY$: IF LEN(K$) = 0 THEN 800
810 PRINT CHR$(26)
820 E = INT(15 * RND) + 1
830 F = INT(15 * RND) + 1
840 J = 1
850 REM MAINPROGRAM           H  E  R  E
860 FOR A = 1 TO J * 400: NEXT A
870 IF ALE = 1 THEN ALX = ALX + 1
880 IF ALX > 19 THEN ALX = 0
890 IF ALE = 0 OR ALX <> 0 THEN 980
900 FOR A = 1 TO 15
910 FOR AA = 1 TO 15
920 IF B(A, AA) <> 15 THEN 950
930 AAA = INT(2 * RND) + 17
940 B(A, AA) = AAA
950 NEXT AA
960 NEXT A
970 ALE = 0
980 IF ALE = 0 THEN A = INT(50 * RND) + 1: IF A <> 1 THEN 1040
990 IF ALX <> 0 THEN 1030
1000 SOUND 262, 5: PRINT "THE ALEUTS ARE HERE!!!"
1030 ALE = 1
1040 CLS
1050 FOR A = E - 1 TO E + 1
1060 PRINT "     ";
1070 FOR AA = F - 1 TO F + 1
1080 IF E = 1 AND A = 0 THEN PRINT "               "; : GOTO 1130
1090 IF E = 15 AND A = 16 THEN PRINT "               "; : GOTO 1130
1100 IF F = 1 AND AA = 0 THEN PRINT "               "; : GOTO 1130
1110 IF F = 15 AND AA = 16 THEN PRINT "               "; : GOTO 1130
1120 PRINT C$(B(A, AA));
1130 NEXT AA
1140 PRINT
1150 NEXT A
1160 PRINT "----------------------------------------------------"
1170 I = I + 1
1180 II = II + 1
1190 PRINT "YOU ARE ON "; CHR$(E + 64), F; ", A "; C$(B(E, F));
1200 IF I > 13 AND H > 1 THEN H = H - 2: PRINT "YOU ATE A MEAL": I = 0
1210 IF II > 13 AND HH > 1 THEN HH = HH - 2: PRINT "YOU DRANK FROM YOUR CAN": II = 0
1220 IF I > 14 AND II < 15 THEN PRINT "YOU'RE HUNGRY"
1230 IF I < 15 AND II > 14 THEN PRINT "YOU'RE THIRSTY"
1240 IF I > 14 AND II > 14 THEN PRINT "YOU'RE HUNGRY AND THIRSTY"
1250 IF I > 22 OR II > 22 THEN A = INT(3 * RND) + 1: IF A = 1 THEN PRINT "YOU FAINTED!": SOUND 523, 10: FAI = FAI + 1
1260 IF FAI > 4 THEN 2100
1270 IF I < 15 AND II < 15 THEN PRINT
1280 IF WOOD > 5 AND NOE = 0 THEN WOOD = WOOD - 6: NOE = 1
1290 PRINT "YOU HAVE ";
1300 IF H > 0 THEN PRINT H / 2; " MEAL(S) OF FOOD,";
1310 IF HH > 0 THEN PRINT HH / 2; " DRINK(S) OF WATER,";
1320 IF SPE > 0 THEN PRINT SPE; " SPEAR(S),";
1330 IF BOW = 1 THEN PRINT " A BOW,";
1340 IF ARR > 0 THEN PRINT ARR; " ARROW(S),";
1350 IF CAN > 0 THEN PRINT CAN; " CAN(S) FOR WATER,";
1360 IF GUB > 0 THEN PRINT " A GUN,"; GUB; " BULLET(S),";
1370 IF GRE > 0 THEN PRINT GRE; " GRENADE(S),";
1380 IF WOOD > 0 THEN PRINT WOOD; " PIECE(S) OF WOOD,";
1390 IF NOE = 1 THEN PRINT " A CANOE,";
1400 IF AX = 1 THEN PRINT " AN AX,";
1410 IF H = 0 AND HH = 0 AND SPE = 0 AND BOW = 0 AND ARR = 0 AND CAN = 0 THEN PRINT "NOTHING" ELSE PRINT
1420 PRINT
1430 G$ = INKEY$: IF LEN(G$) = 0 THEN 1430
1440 IF G$ <> "H" AND G$ <> "P" AND G$ <> "E" AND G$ <> "S" AND G$ <> "D" AND G$ <> "X" AND G$ <> "T" AND G$ <> "F" AND G$ <> "G" THEN 1430
1450 IF G$ = "P" THEN J = J + 1: IF J = 2 THEN J = 0: GOTO 1430
1452 IF G$ <> "H" THEN 1460
1453 GOSUB 30
1454 K$ = INKEY$: IF LEN(K$) = 0 THEN 1454
1455 GOTO 1040
1460 IF G$ = "E" AND E <> 1 THEN E = E - 1
1470 IF G$ = "S" AND F <> 1 THEN F = F - 1
1480 IF G$ = "D" AND F <> 15 THEN F = F + 1
1490 IF G$ = "X" AND E <> 15 THEN E = E + 1
1500 IF G$ = "F" AND GUB = 0 THEN 1430
1510 IF G$ = "G" AND GRE = 0 THEN 1430
1520 IF G$ = "F" AND GUB > 0 THEN GOSUB 2240
1530 IF G$ = "G" AND GRE > 0 THEN GOSUB 2440
1540 IF B(E, F) <> 1 THEN 1610
1550 A = 2
1560 IF SPE > 0 THEN A = A + 1
1570 IF BOW > 0 AND ARR = 1 THEN A = A + 1
1580 AA = INT(A * RND) + 1
1590 IF AA = 1 THEN PRINT "THE WILD DOGS KILLED YOU!": GOTO 2100
1600 PRINT "YOU SURVIVED THE WILD DOGS!"
1610 IF B(E, F) = 6 AND AX = 1 THEN WOOD = WOOD + 1: PRINT "YOU CHOPPED DOWN THE TREE": B(E, F) = 25
1620 IF B(E, F) = 8 THEN PRINT "YOU FOUND A SPEAR!": SPE = SPE + 1: GOTO 2010
1630 IF B(E, F) = 9 THEN PRINT "YOU FOUND A BOW!": BOW = 1: GOTO 2010
1640 IF B(E, F) = 10 THEN PRINT "YOU FOUND AN ARROW!": ARR = ARR + 1: GOTO 2010
1650 IF B(E, F) = 11 THEN PRINT "YOU FOUND A CAN FOR WATER": CAN = CAN + 1: IF CAN > 8 THEN CAN = 8
1660 IF B(E, F) = 11 THEN 2010
1670 IF B(E, F) <> 12 THEN 1740
1680 IF SPE = 0 AND (BOW = 0 OR ARR = 0) AND H = 0 THEN PRINT "THE OCTOPUS ATE YOU!": GOTO 2100
1690 IF SPE = 0 AND (BOW = 0 OR ARR = 0) AND H > 0 THEN PRINT "THE OCTOPUS STOLE SOME FOOD OF YOURS.": H = H - 1: GOTO 1740
1700 IF SPE > 0 THEN PRINT "YOU THREW A SPEAR AT THE OCTOPUS...": SPE = SPE - 1: A = INT(3 * RND) + 1
1710 IF A = 1 AND SPE > 0 THEN PRINT "YOU KILLED IT!": H = H + 8: GOTO 2010: GOTO 1740
1720 IF BOW = 1 AND ARR > 0 THEN PRINT "YOU SHOT AN ARROW AT THE OCTOPUS...": ARR = ARR - 1: A = INT(4 * RND) + 1
1730 IF A = 1 THEN PRINT "YOU KILLED IT!": H = H + 8: GOTO 2010: GOTO 1740 ELSE PRINT "YOU MISSED!"
1740 IF B(E, F) <> 13 THEN 1800
1750 A = 4
1760 IF SPE > 0 THEN A = A - 1
1770 IF BOW = 1 AND ARR > 0 THEN A = A - 1: ARR = ARR - 1
1780 IF BOW = 0 OR ARR = 0 AND SPE > 0 THEN SPE = SPE - 1
1790 AA = INT(A * RND) + 1: IF AA = 1 THEN PRINT "YOU CAUGHT A FISH!": H = H + 2: GOTO 2010 ELSE PRINT "YOU MISSED THE FISH.": GOTO 1800
1800 IF B(E, F) = 14 AND NOE = 0 THEN PRINT "YOU FOUND A CANOE.": NOE = 1: GOTO 2010
1810 IF B(E, F) = 15 THEN A = INT(2 * RND) + 1: IF A = 1 THEN PRINT "THE ALEUT KILLED YOU!": GOTO 2100
1820 IF B(E, F) = 16 AND CAN = 0 THEN PRINT "YOU HAD A DRINK FROM THE SPRING.": II = 0: HH = 0
1830 IF B(E, F) = 16 AND CAN > 0 THEN PRINT "YOU`RE CAN(S) ARE/IS FULL.": HH = CAN * 4: FAI = 0
1840 IF B(E, F) = 17 THEN GUB = GUB + 1: PRINT "YOU HAVE A GUN AND "; GUB; " BULLETS FROM AN ALEUT WHO DROPPED IT!": GOTO 2010
1850 IF B(E, F) = 18 THEN GRE = GRE + 1: PRINT "YOU HAVE "; GRE; " GRENADE(S) FROM AN ALEUT WHO DROPPED IT!": GOTO 2010
1860 IF B(E, F) = 19 THEN SPE = SPE + 5: ARR = ARR + 5: BOW = 1: CAN = CAN + 2: IF CAN > 8 THEN CAN = 8
1870 IF B(E, F) = 19 THEN PRINT "YOU FOUND A   C A S E   O F   W E A P O N S ! ! !": GOTO 2010
1880 IF B(E, F) = 21 THEN PRINT "YOU COLLECTED 4 MEALS FROM THE OCTOPUS!": H = H + 8: GOTO 2010
1890 IF B(E, F) = 22 THEN PRINT "YOU COLECTED 1 MEAL FROM THE FISH!": H = H + 2: GOTO 2010
1900 IF B(E, F) = 23 THEN A = INT(5 * RND) + 1: IF A = 1 THEN PRINT "YOU COLLECTED A GUN AND BULLETS FROM THE DEAD ALEUT!": GUB = GUB + 1: GOTO 2010
1910 IF B(E, F) = 23 AND A = 2 THEN PRINT "YOU COLLECTED A GRENADE FROM THE DEAD ALEUT!": GRE = GRE + 1: GOTO 2010
1920 IF B(E, F) = 23 AND A > 2 THEN 2010
1930 IF B(E, F) = 24 THEN PRINT "YOU FOUND AN AX": AX = 1: GOTO 2010
1940 IF B(E, F) = 26 AND NOE = 0 THEN PRINT "IF YOU WANT TO LEAVE,YOU'LL NEED A CANOE!": GOTO 2000
1950 IF B(E, F) <> 26 THEN 2000
1960 IF NOE = 1 THEN PRINT "DO YOU WANT TO LEAVE TO ANOTHER LAND?  (Y OR N) "
1970 K$ = INKEY$: IF LEN(K$) = 0 THEN 1970
1980 IF K$ = "Y" THEN 2580
1990 IF K$ = "N" THEN 2000
2000 GOTO 860
2010 A = INT(16 * RND) + 1
2020 IF A = 14 THEN 2010
2030 IF A = 15 AND ALE = 0 THEN 2010
2040 IF ALE = 1 THEN A = INT(3 * RND) + 1
2050 IF ALE = 1 AND A = 1 THEN A = 15
2060 IF ALE = 1 AND A <> 15 THEN A = INT(16 * RND) + 1
2070 IF ALE = 1 AND A = 14 THEN 2060
2080 B(E, F) = A
2090 GOTO 860
2100 REM     DDDDD     EEEEE     AAAAA     TTTTT     HHHHH
2110 SOUND 523, 10
2120 PRINT "YOU HAVE DIED"
2130 FOR A = 1 TO 1000
2140 AAAAA$ = INKEY$: IF LEN(AAAAA$) <> 0 THEN 2190
2150 SOUND 523, .1
2160 PRINT "XHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHX"
2170 PRINT "HXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXHXH"
2180 NEXT A
2190 PRINT "ARE YOU GOING TO CHEAT AND CONTINUE?  (Y)ES OR (N)O"
2200 AAAAA$ = INKEY$: IF LEN(AAAAA$) = 0 THEN 2200
2210 IF AAAAA$ = "Y" THEN 860
2220 IF AAAAA$ <> "N" THEN 2200
2230 STOP
2240 REM G  U  N     &     B  U  L  L  E  T  S
2250 PRINT "WHICH DIRECTION?  (E,S,D,X)"
2260 K$ = INKEY$: IF LEN(K$) = 0 THEN 2260
2270 AA = E: AAA = F: AAAA = 0: GUB = GUB - 1
2280 FOR A = 1 TO 5
2290 IF AAAA = 1 THEN 2420
2300 IF K$ = "E" THEN AA = AA - 1
2310 IF K$ = "S" THEN AAA = AAA - 1
2320 IF K$ = "D" THEN AAA = AAA + 1
2330 IF K$ = "X" THEN AA = AA + 1
2340 IF AA < 1 OR AA > 15 OR A < 1 OR AA > 15 THEN 2420
2350 IF B(AA, AAA) = 1 OR B(AA, AAA) = 12 OR B(AA, AAA) = 13 OR B(AA, AAA) = 15 THEN AAAA = 1
2360 IF B(AA, AAA) = 1 THEN AB = INT(4 * RND) + 1: IF AB < 4 THEN PRINT "YOU KILLED THE WILD DOGS!": B(AA, AAA) = 20
2370 IF B(AA, AAA) = 1 AND AB = 4 THEN PRINT "YOU MISSED THE WILD DOGS!"
2380 IF B(AA, AAA) = 12 THEN PRINT "YOU KILLED AN OCTOPUS!": B(AA, AAA) = 21
2390 IF B(AA, AAA) = 13 THEN AB = INT(3 * RND) + 1: IF AB = 1 THEN PRINT "YOU KILLED THE FISH!": B(AA, AAA) = 22
2400 IF B(AA, AAA) = 13 AND AB <> 1 THEN PRINT "YOU MISSED THE FISH"
2410 IF B(AA, AAA) = 15 THEN PRINT "YOU KILLED AN ALEUT!": B(AA, AAA) = 23
2420 NEXT A
2430 RETURN
2440 REM G  R  E  N  A  D  E  S
2450 PRINT "WHICH DIRECTION?  (E,S,D,X)"
2460 K$ = INKEY$: IF LEN(K$) = 0 THEN 2460
2470 AA = E: AAA = F: GRE = GRE - 1
2480 IF K$ = "E" THEN AA = AA - 1
2490 IF K$ = "S" THEN AAA = AAA - 1
2500 IF K$ = "D" THEN AAA = AAA + 1
2510 IF K$ = "X" THEN AA = AA + 1
2520 IF A < 1 OR AA < 1 OR A > 15 OR AA > 15 THEN 2570
2530 IF B(AA, AAA) = 1 THEN PRINT "YOU KILLED THE WILD DOGS!": B(AA, AAA) = 20
2540 IF B(AA, AAA) = 12 THEN PRINT "YOU KILLED AN OCTOPUS!": B(AA, AAA) = 21
2550 IF B(AA, AAA) = 13 THEN PRINT "YOU KILLED THE FISH!": B(AA, AAA) = 22
2560 IF B(AA, AAA) = 15 THEN PRINT "YOU KILLED AN ALEUT!": B(AA, AAA) = 23
2570 RETURN
2580 SOUND 523, 10: PRINT "Y O U   H A V E   D E C I D E D   T O   L E A V  E   T H E   I S L A N D ! ! !"
2590 FOR A = 1 TO 500: NEXT A
2600 FOR A = 1 TO 150
2610 IF AA = 1.5 THEN 2740
2620 PRINT "FOOD : "; H / 2
2630 PRINT "WATER : "; HH / 2
2640 I = I + 1: II = II + 1
2650 IF I > 13 AND H > 1 THEN H = H - 2: PRINT "YOU ATE A MEAL": I = 0
2660 IF II > 13 AND HH > 1 THEN HH = HH - 2: PRINT "YOU DRANK FROM YOUR CAN": II = 0
2670 IF I > 14 AND II < 15 THEN PRINT "YOU'RE HUNGRY"
2680 IF I < 15 AND II > 14 THEN PRINT "YOU'RE THIRSTY"
2690 IF I > 14 AND II > 14 THEN PRINT "YOU'RE HUNGRY AND THIRSTY"
2700 IF I > 22 OR II > 22 THEN A = INT(3 * RND) + 1: IF A = 1 THEN PRINT "YOU FAINTED!": SOUND 523, 10: FAI = FAI + 1
2710 IF FAI > 4 THEN AA = 1.5
2720 CLS
2730 IF I < 15 AND II < 15 THEN PRINT
2740 NEXT A
2750 IF AA = 1.5 THEN 2100
2760 FOR A = 1 TO 1000
2770 AA = INT(4 * RND) + 1
2780 IF AA = 1 THEN PRINT "YOU ";
2790 IF AA = 2 THEN PRINT "MADE ";
2800 IF AA = 3 THEN PRINT "IT ";
2810 IF AA = 4 THEN PRINT "!!! ";
2820 K$ = INKEY$: IF LEN(A$) <> 0 THEN 2840
2830 NEXT A
2840 STOP
2850 PRINT "CODE?"
2860 A = 0
2870 K$ = INKEY$: IF LEN(K$) = 0 THEN 2870
2880 A = A + 1
2890 IF A = 2 AND K$ <> "S" THEN REM
2900 IF A = 3 AND K$ <> "R" THEN REM
2910 IF A = 1 AND K$ <> "K" THEN END
2920 IF A = 2 AND K$ <> "J" THEN END
2930 IF A = 1 AND K$ <> "T" THEN REM
2940 IF A = 2 AND K$ <> "W" THEN REM
2950 IF A = 3 AND K$ <> "S" THEN END
2960 IF A = 3 AND K$ <> "Y" THEN REM
2970 IF A = 1 AND K$ <> "N" THEN REM
2980 IF A = 3 THEN RETURN
2990 GOTO 2870
3000 END

