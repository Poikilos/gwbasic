10 SCREEN 12: CLS
15 DIM O$(25), OO$(25)
20 DIM A(16, 12)
21 DIM NUC(25)
22 NUC(1) = 3: NUC(2) = 4: NUC(3) = 13: NUC(4) = 3: NUC(5) = 20
23 NUC(6) = 4: NUC(7) = 6: NUC(8) = 1: NUC(9) = 13: NUC(10) = 10
24 NUC(11) = 4: NUC(12) = 14: NUC(13) = 4: NUC(14) = 2: NUC(15) = 3
25 NUC(16) = 8: NUC(17) = 31: NUC(18) = 7: NUC(19) = 2: NUC(20) = 2
26 NUC(21) = 3: NUC(22) = 5: NUC(23) = 1: NUC(24) = 0
40 O$(1) = "SHOVEL ": OO$(1) = "SHOV"
50 O$(2) = "LAKE   ": OO$(2) = "LAKE"
60 O$(3) = "FENCE  ": OO$(3) = "FENC"
70 O$(4) = "BOAT   ": OO$(4) = "BOAT"
80 O$(5) = "TREE   ": OO$(5) = "TREE"
90 O$(6) = "AX     ": OO$(6) = "AX  "
100 O$(7) = "LOGPILE": OO$(7) = "LOGP"
110 O$(8) = "TREASUR": OO$(8) = "TREA"
120 O$(9) = "WALL   ": OO$(9) = "WALL"
130 O$(10) = "RIVER ": OO$(10) = "RIVR"
140 O$(11) = "BRIDGE ": OO$(11) = "BRIG"
150 O$(12) = "CLUE   ": OO$(12) = "CLUE"
160 O$(13) = "TRAP   ": OO$(13) = "TRAP"
170 O$(14) = "TUNNEL ": OO$(14) = "TUNL"
180 O$(15) = "LADDER ": OO$(15) = "LADR"
190 O$(16) = "HILL   ": OO$(16) = "HILL"
200 O$(17) = "GRASS  ": OO$(17) = "GRAS"
210 O$(18) = "HOUSE  ": OO$(18) = "HOUS"
220 O$(19) = "BEAR   ": OO$(19) = "BEAR"
230 O$(20) = "BEES   ": OO$(20) = "BEES"
240 O$(21) = "GATOR  ": OO$(21) = "GATR"
250 O$(22) = "BOMB   ": OO$(22) = "BOMB"
260 O$(23) = "START  ": OO$(23) = "STRT"
270 O$(24) = "STUMP  ": OO$(24) = "STMP"
275 IF SDY = 1 THEN 410
280 RANDOMIZE TIMER
290 PRINT "DO YOU WANT A CHART TO BE PRINTED?"
300 F$ = INKEY$: IF F$ = "" THEN 300
310 IF F$ = "N" OR F$ = "n" THEN PRINT "NOT PRINTING": GOTO 410
320 IF F$ <> "Y" AND F$ <> "y" THEN SOUND 131, 10: GOTO 300
330 PRINT "PRINTING"
340 LPRINT " |1  |2  |3  |4  |5  |6  |7  |8  |9  |10 |11 |12 |13 |14 |15 |"
350 LPRINT "--------------------------------------------------------------"
360 FOR X = 65 TO 75
370 LPRINT CHR$(X); "|   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |"
380 LPRINT " |   |   |   |   |   |   |   |   |   |   |   |   |   |   |   |"
390 LPRINT "--------------------------------------------------------------"
400 NEXT X
410 CLS : PRINT "GETTING BOARD"
420 T = 0: R = 0: F = 0: AX = 0: TT = 10
430 FOR Q = 1 TO 15
440 FOR W = 1 TO 11
450 A(Q, W) = 0
460 NEXT W
470 NEXT Q
480 FOR Z = 1 TO 23
490 FOR X = 1 TO NUC(Z)
500 GOSUB 1390
510 IF Z = 8 THEN N = Q: M = W
520 NEXT X
530 NEXT Z
540 FOR Z = 1 TO 15
550 FOR X = 1 TO 11
560 IF A(Z, X) = 0 THEN A(Z, X) = 17
570 NEXT X
580 NEXT Z
1340 V = Q: B = W
1350 QQ = Q: WW = W
1360 CLS : PRINT "YOU ARE ON "; CHR$(64 + WW); QQ
1361 LOCATE 5, 1: PRINT "     1    2    3    4    5    6    7    8    9   10   11   12   13   14   15"
1362 FOR DF = 0 TO 15
1363 LINE (DF * 40 + 20, 64)-(DF * 40 + 20, 442)
1364 IF DF < 12 THEN LINE (0, DF * 32 + 90)-(620, DF * 32 + 90)
1365 NEXT DF
1366 FOR DF = 1 TO 11
1367 LOCATE (DF * 2) + 5, 1: PRINT CHR$(DF + 64)
1368 NEXT DF
1369 LOCATE 2, 1
1370 T = 0: R = 0: F = 0: AX = 0: TT = 10
1375 LINE (QQ * 40 - 20, WW * 32 + 58)-(QQ * 40 + 20, WW * 32 + 90), 15, BF
1376 LOCATE 1, 40: PRINT "CREATED BY KENNY AND ALAN SILVERMAN"
1380 GOTO 1440
1390 Q = INT(15 * RND) + 1
1400 W = INT(11 * RND) + 1
1410 IF A(Q, W) <> 0 THEN 1390
1420 A(Q, W) = Z
1430 RETURN
1440 IF FH <> 0 AND (A(FH, FI) = 2 OR A(FH, FI) = 10) THEN LINE (FH * 40 - 20, FI * 32 + 58)-(FH * 40 + 20, FI * 32 + 90), 9, BF: FH = 0: FI = 0
1442 IF FH <> 0 AND (A(FH, FI) = 3 OR A(FH, FI) = 9) THEN LINE (FH * 40 - 20, FI * 32 + 58)-(FH * 40 + 20, FI * 32 + 90), 2, BF: FH = 0: FI = 0
1443 IF A(Q, W) <> 23 AND A(Q, W) <> 20 AND A(Q, W) <> 22 AND A(Q, W) <> 2 AND A(Q, W) <> 10 AND A(Q, W) <> 3 AND A(Q, W) <> 9 THEN LINE (Q * 40 - 20, W * 32 + 58)-(Q * 40 + 20, W * 32 + 90), 6, BF
1444 LINE (Q * 40 - 10, W * 32 + 68)-(Q * 40 + 10, W * 32 + 80), 7, BF
1445 QQ = Q: WW = W
1446 LOCATE 1, 1: PRINT "YOU ARE ON "; CHR$(64 + WW); QQ: LOCATE 1, 40
1447 LOCATE 3, 1
1448 SJU = 0
1449 IF QQ > 14 THEN 1451
1450 IF A(QQ + 1, WW) = 13 OR A(QQ + 1, WW) = 19 OR A(QQ + 1, WW) = 21 THEN SJU = 1
1451 IF QQ < 2 THEN 1453
1452 IF A(QQ - 1, WW) = 13 OR A(QQ - 1, WW) = 19 OR A(QQ - 1, WW) = 21 THEN SJU = 1
1453 IF WW > 10 THEN 1455
1454 IF A(QQ, WW + 1) = 13 OR A(QQ, WW + 1) = 19 OR A(QQ, WW + 1) = 21 THEN SJU = 1
1455 IF WW < 2 THEN 1458
1456 IF A(QQ, WW - 1) = 13 OR A(QQ, WW - 1) = 19 OR A(QQ, WW - 1) = 21 THEN SJU = 1
1458 IF SJU = 1 THEN PRINT "YOU'RE CLOSE TO DANGER": SOUND 131, .5
1460 I$ = INKEY$: IF I$ = "" THEN 1460
1461 IF I$ = "Q" THEN END
1462 IF ((I$ = "E" OR I$ = "e") AND WW = 1) OR ((I$ = "X" OR I$ = "x") AND WW = 11) OR ((I$ = "S" OR I$ = "s") AND QQ = 1) OR ((I$ = "D" OR I$ = "d") AND QQ = 15) THEN SOUND 131, 2: GOTO 1460
1463 IF I$ <> "E" AND I$ <> "X" AND I$ <> "S" AND I$ <> "D" AND I$ <> "H" AND I$ <> "e" AND I$ <> "x" AND I$ <> "s" AND I$ <> "d" AND I$ <> "h" THEN 1460
1470 IF I$ = "E" OR I$ = "e" THEN QQ = Q: WW = W - 1: GOTO 1530
1480 IF I$ = "X" OR I$ = "x" THEN QQ = Q: WW = W + 1: GOTO 1530
1490 IF I$ = "D" OR I$ = "d" THEN QQ = Q + 1: WW = W: GOTO 1530
1500 IF I$ = "S" OR I$ = "s" THEN QQ = Q - 1: WW = W: GOTO 1530
1510 IF I$ = "H" OR I$ = "h" THEN LOCATE 1, 1: PRINT "YOU ARE ON "; CHR$(64 + WW); QQ; O$(A(Q, W)): LOCATE 3, 1: PRINT "E,S,D,X,(H)ELP,(Q)UIT               ": GOTO 1440
1520 GOTO 1440
1530 LOCATE 1, 1: PRINT "YOU ARE ON "; CHR$(64 + WW); QQ; "            "
1540 LOCATE 3, 1: PRINT "                                 ";
1545 LOCATE 2, 1: PRINT "                                                                              ": LOCATE 2, 1
1550 ON A(QQ, WW) GOTO 1560, 1570, 1590, 1610, 1620, 1670, 1680, 1690, 1700, 1720, 1740, 1750, 1820, 1830, 1930, 1940, 1950, 1960, 1970, 1980, 2000, 2010, 2030, 2040
1560 PRINT "YOU HAVE FOUND A SHOVEL": IF T = 0 THEN GOSUB 3040
1565 T = 1: PSET (600, 450): GOSUB 3000: GOTO 2050
1570 TT = 10: IF R = 1 THEN PRINT "YOU ARE DRIFTING ACROSS A LAKE": FH = QQ: FI = WW: GOTO 2050
1580 PRINT "YOU DON'T HAVE A BOAT, SO YOU CAN'T CROSS THE LAKE": FH = QQ: FI = WW: GOTO 1440
1590 IF F = 1 THEN PRINT "YOU ARE GOING OVER A FENCE WITH YOUR HANDY LADDER"; : FH = QQ: FI = WW: GOTO 2050
1600 PRINT "YOU DON'T HAVE A LADDER, SO YOU CAN'T GO OVER THE FENCE": FH = QQ: FI = WW: GOTO 1440
1610 PRINT "YOU DISCOVERED AND NOW OWN A BOAT": IF R = 0 THEN GOSUB 3040
1615 R = 1: PSET (580, 450): GOSUB 3010:  GOTO 2050
1620 PRINT "YOU ARE NEXT TO A TREE."; : IF AX = 1 THEN PRINT "WANT TO CHOP IT DOWN?" ELSE 2050
1630 CH$ = INKEY$: IF CH$ = "" THEN 1630
1640 IF CH$ = "Y" OR CH$ = "y" THEN A(QQ, WW) = 24: GOTO 2050
1650 IF CH$ = "N" OR CH$ = "n" THEN 2050 ELSE 1630
1670 PRINT "YOU NOW HAVE A STUPID AX": IF AX = 0 THEN GOSUB 3040
1675 AX = 1: PSET (540, 450): GOSUB 3030: GOTO 2050
1680 PRINT "YOU ARE AT A LOG PILE": GOTO 2050
1690 GOTO 2090
1700 IF F = 1 THEN PRINT "YOU ARE ON A WALL": FH = QQ: FI = WW: GOTO 2050
1710 PRINT "YOU CAN'T PASS THROUGH A WALL": FH = QQ: FI = WW: GOTO 1440
1720 TT = 10: IF R = 1 THEN PRINT "YOU ARE ACROSS A RIVER": FH = QQ: FI = WW: GOTO 2050
1730 PRINT "YOU CAN'T GO ON A RIVER": FH = QQ: FI = WW: GOTO 1440
1740 PRINT "YOU ARE ON A BRIDGE": GOTO 2050
1750 PRINT "YOU FOUND A CLUE : THE TREASURE IS ";
1760 IF M < WW THEN PRINT "UP ";
1770 IF M > WW THEN PRINT "DOWN ";
1775 IF M <> WW AND N <> QQ THEN PRINT "AND TO THE ";
1780 IF N > QQ THEN PRINT "RIGHT ";
1790 IF N < QQ THEN PRINT "LEFT ";
1795 PLAY "<<L8EL16GBGL8A>>"
1800 PRINT
1810 GOTO 2050
1820 PRINT "AS YOU FALL INTO A 500 FOOT DEEP PIT, YOU REALIZE THAT MAYBE YOU'RE TRAPPED?": GOTO 2110
1830 PRINT "YOU ARE AT A TUNNEL ENTRANCE. DO YOU WANT TO GO IN IT?"
1840 Z$ = INKEY$: IF Z$ = "" THEN 1840
1850 IF Z$ = "Y" OR Z$ = "y" THEN 1865
1860 IF Z$ = "N" OR Z$ = "n" THEN 2050 ELSE SOUND 131, 5: GOTO 1840
1865 FGFG = POINT(Q * 40 - 15, W * 32 + 80)
1866 LINE (Q * 40 - 10, W * 32 + 68)-(Q * 40 + 10, W * 32 + 80), FGFG, BF
1880 Q = INT(15 * RND) + 1
1890 W = INT(11 * RND) + 1
1900 QQ = Q: WW = W
1910 GOTO 1530
1930 PRINT "YOU FOUND A LADDER": IF F = 0 THEN GOSUB 3040
1935 F = 1: PSET (560, 450): GOSUB 3020: GOTO 2050
1940 PRINT "YOU ARE STANDING ON TOP OF A MOUNTAIN": GOTO 2050
1950 PRINT "YOU ARE MOVING ALONG IN A GRASS FIELD": GOTO 2050
1960 PRINT "YOU ARE NEXT TO A OLD HOUSE": GOTO 2050
1970 PRINT "SOMETHING HAPPENED, AND A STUPID LITTLE BEAR CAME YOUR WAY": GOTO 2110
1980 PRINT "BEES ARE AFTER YOU.  FIND WATER WITHIN THREE TURNS OR ELSE GET STUNG TO DEATH.": PLAY "<<<L8EL16EL8EGAL16GF+L8EL16DL8E>>>": IF TT > 4 THEN TT = 4: TX = 1
1985 LINE (QQ * 40 - 20, WW * 32 + 58)-(QQ * 40 + 20, WW * 32 + 90), 13, BF
1990 GOTO 2050
2000 PRINT "GEE, AN ALLIGATOR DECIDED TO COME YOUR WAY, AND HE GOBBLED YOU TO PIECES": GOTO 2110
2010 PRINT "A TIMEBOMB IS CHAINED TO YOU. YOU HAVE FIVE TURNS TO FIND WATER TO DEFUSE IT": PLAY "<<<L8EL16EL8EGAL16GF+L8EL16DL8E>>>": IF TT > 6 THEN TT = 6: TX = 2
2015 LINE (QQ * 40 - 20, WW * 32 + 58)-(QQ * 40 + 20, WW * 32 + 90), 13, BF
2020 GOTO 2050
2030 PRINT "YOU ARE AT START": GOTO 2050
2040 PRINT "YOU SEE A STUMP. ANYONE WHO WOULD KILL A TREE FOR FUN MUST BE STUPID": GOTO 2050
2050 FGFG = POINT(Q * 40 - 15, W * 32 + 80)
2056 LINE (Q * 40 - 10, W * 32 + 68)-(Q * 40 + 10, W * 32 + 80), FGFG, BF
2058 Q = QQ: W = WW
2060 IF TT <> 10 THEN TT = TT - 1
2070 IF TT = 0 THEN 2105
2080 GOTO 1440
2090 IF T = 0 THEN PRINT "YOU ARE WALKING ALONG IN A SUSPICIOUS AREA": GOTO 2050
2091 IF T = 1 THEN
2092 LOCATE 2, 1: PRINT "YOU FOUND THE TREASURE! ! !": PRINT : PLAY "<<L16EG>L8C<L16G>L8CL16FL8EL16C<L8A>L16C>"
2093 FGFG = POINT(Q * 40 - 15, W * 32 + 80)
2094 LINE (Q * 40 - 10, W * 32 + 68)-(Q * 40 + 10, W * 32 + 80), FGFG, BF
2095 LINE (QQ * 40 - 20, WW * 32 + 58)-(QQ * 40 + 20, WW * 32 + 90), 14, BF
2098 IF TT <> 10 THEN LOCATE 2, 1: PRINT "HOWEVER YOU DIDN'T ENJOY IT FOR LONG": GOTO 2105
2100 GOTO 2120
2103 END IF
2105 IF TX = 1 THEN PRINT "THE BEES ATE YOU AND NOW YOU LOOK BETTER THAN YOU DID BEFORE"
2106 IF TX = 2 THEN PRINT "YOU BLEW UP FROM THE TIMEBOMB AND BURNT CRISPS IS WHAT YOU ARE"
2110 PRINT "YOU'RE DEAD       ": LINE (QQ * 40 - 20, WW * 32 + 58)-(QQ * 40 + 20, WW * 32 + 90), 12, BF
2111 FGFG = POINT(Q * 40 - 15, W * 32 + 80)
2112 LINE (Q * 40 - 10, W * 32 + 68)-(Q * 40 + 10, W * 32 + 80), FGFG, BF
2115 PLAY "<<<<L8GGL16GL8E-E-L16E-L8CL16DL8E-L16FL8G>>>>"
2120 LOCATE 4, 1: PRINT "(P)RINT IT OUT,(S)AME BOARD,(D)IFFERENT BOARD,(L)OOK AT BOARD,(Q)UIT"
2140 U$ = INKEY$: IF U$ = "" THEN 2140
2150 IF U$ = "P" OR U$ = "p" THEN 2240
2160 IF U$ = "S" OR U$ = "s" THEN 2580
2170 IF U$ = "D" OR U$ = "d" THEN CLEAR : SDY = 1: GOTO 10
2180 IF U$ = "Q" OR U$ = "q" THEN END
2185 IF U$ = "L" OR U$ = "l" THEN 2600
2190 GOTO 2120
2240 PRINT "DO YOU WANT THE STUMPS TO TURN BACK TO TREES?"
2250 J$ = INKEY$: IF J$ = "" THEN 2250
2260 IF J$ = "N" OR J$ = "n" THEN 2330
2270 IF J$ <> "Y" AND J$ <> "y" THEN 2240
2280 FOR W = 1 TO 11
2290 FOR Q = 1 TO 15
2300 IF A(Q, W) = 24 THEN A(Q, W) = 5
2310 NEXT Q
2320 NEXT W
2330 PRINT "DO YOU WANT TO PRINT OUT HOW MANY THINGS THERE ARE OF EACH KIND?"
2340 G$ = INKEY$: IF LEN(G$) = 0 THEN 2340
2350 IF G$ = "N" OR G$ = "n" THEN 2460
2360 FOR X = 1 TO 24
2370 FOR W = 1 TO 11
2380 FOR Q = 1 TO 15
2390 IF A(Q, W) = X THEN XX = XX + 1
2400 NEXT Q
2410 NEXT W
2420 LPRINT "THERE ARE "; XX; "-"; O$(X); "("; X; ")"
2430 XX = 0
2440 NEXT X
2450 LPRINT
2460 LPRINT "START IS AT "; CHR$(B + 64); V: LPRINT
2470 LPRINT "   1    2    3    4    5    6    7    8    9    10   11   12   13   14   15"
2480 LPRINT "-----------------------------------------------------------------------------"
2490 FOR W = 1 TO 11
2500 LPRINT CHR$(64 + W); "|";
2510 FOR Q = 1 TO 15
2520 LPRINT OO$(A(Q, W)); "|";
2530 NEXT Q
2540 LPRINT
2550 LPRINT "-|----|----|----|----|----|----|----|----|----|----|----|----|----|----|----|"
2560 NEXT W
2570 GOTO 2120
2580 Q = V: QQ = V: W = B: WW = B: CLS : T = 0: R = 0: F = 0: AX = 0: GOTO 1360
2600 FOR SRT = 1 TO 15
2610 FOR WRC = 1 TO 11
2620 SFDH = 0
2630 IF SFDH = 0 AND (A(SRT, WRC) = 2 OR A(SRT, WRC) = 10) THEN SFDH = 9: GOSUB 2800
2640 IF SFDH = 0 AND (A(SRT, WRC) = 3 OR A(SRT, WRC) = 9) THEN SFDH = 2: GOSUB 2800
2650 IF SFDH = 0 AND (A(SRT, WRC) = 20 OR A(SRT, WRC) = 22) THEN SFDH = 13: GOSUB 2800
2660 IF SFDH = 0 AND A(SRT, WRC) = 8 THEN SFDH = 14: GOSUB 2800
2670 IF SFDH = 0 AND A(SRT, WRC) = 23 THEN SFDH = 15: GOSUB 2800
2680 IF SFDH = 0 AND (A(SRT, WRC) = 13 OR A(SRT, WRC) = 19 OR A(SRT, WRC) = 21) THEN SFDH = 12: GOSUB 2800
2685 IF SFDH = 0 THEN SFDH = 6: GOSUB 2800
2690 NEXT WRC
2700 NEXT SRT
2790 GOTO 2140
2800 LINE (SRT * 40 - 20, WRC * 32 + 58)-(SRT * 40 + 20, WRC * 32 + 90), SFDH, BF
2810 RETURN
3000 DRAW "A0C0R10C15L2D10R4L6D5F2R4E2U5L2U10L2": RETURN  'SHOVEL
3010 DRAW "A0C0R5C15R10F5G5L10H5E5R5D1C0D1C15R3F3G3L6H3E3R6": RETURN 'BOAT
3020 DRAW "A0C0R6C15D20U15R8U5D20U5L8U5R8": RETURN 'LADDER
3030 DRAW "A0C0R18C15G3L10D17R3U17D3R7F3U9": RETURN 'AX
3040 PLAY "<<<L16GL8EL16G>C>>": RETURN

